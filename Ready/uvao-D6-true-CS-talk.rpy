# Д6-левая. Общение с Виолой в медпункте и допрос Шурика
#
# Устанавливает флаг Полной Информированности alt_uvao_D6_CS_vetrov
#
# Используется флаг Большого Побега alt_day2_us_escape
# Используется флаг вступления в клуб кибернетиков alt_day2_club_join_cyb
# Используется флаг посещения медпункта при обходе в Д2 alt_day2_med_done
# Используется флаг тян-конвоира в Д2 alt_day2_rendezvous
# Используется флаг встречи со Славей на пляже в Д5 alt_uvao_D5_evening_sl
#
# TODO: Спрайты, фоны, звуки в долгом разговоре!
label alt_day6_true_CS_talk:
    window hide
    scene bg ext_aidpost_sunset with fade
    play ambience ambience_camp_entrance_day fadein 3
    $ alt_uvao_D6_CS_vetrov = True
    window show    
    "Наскоро умывшись ледяной водой, я направился к медпункту как был - прямо с полотенцем через плечо. Ничего, свои люди."
    "Впрочем, перед самой дверью я всё-таки притормозил и решил постучаться. Мало ли…"
    dreamgirl "Ну что, ты готов узнать правду?"
    th "Какую еще правду?"
    dreamgirl "Несомненно, ужасную! Такую, чтоб завыть, забиться, захрипеть!"
    th "…"
    dreamgirl "Ну о лагере и всём остальном, балда. Ты разве не за этим сюда шёл? Стучи уже давай!"
    dreamgirl "Мне тоже интересно, что тут такое происходит…"
    play sound sfx_knock_door7_polite
    cs "Открыто, открыто!"
    window hide
    play sound sfx_open_door_1
    play music music_list["eternal_longing"] fadein 3
    stop ambience fadeout 3
    scene bg int_aidpost_day with fade
    show cs normal2 glasses_over far at center with dissolve
    window show
    me "Доброе утро, Виолетта!"
    "Увидев меня, Виолетта рассеянно помахала рукой:"
    cs "Да, доброе… Ты ко мне поговорить, или {i}полечиться{/i}?"
    me "Скорее, первое…"
    cs "Тогда сядь куда-нибудь, я сейчас…"
    hide cs with dissolve
#    play sound_loop sfx_keyboard_mouse_computer_noise
    play sound_loop KBtyping
    "И она снова забарабанила по клавиатуре передового достижения отечественного компьютеростроения."
    "К некоторому моему удивлению, Шурика в медпункте видно не было."
    "Я уселся на кушетку и приготовился ждать."
    $ set_mode_nvl()
    th "Интересно, где Шурик? Наверное, его уже выпустили."
    dreamgirl "Или наоборот, у него опять сорвало крышу и он убежал в леса."
    "Я поёрзал на жёстком гладком покрытии кушетки, безуспешно пытаясь устроиться поудобнее.{w} Если бы меня на ночь уложили спать на это прокрустово ложе, я бы тоже сбежал, пожалуй!"
    th "Главное, чтобы на этот раз {i}не меня{/i} отправили за ним бегать. Вчерашних впечатлений мне вполне достаточно."
    "Заскучав, я покосился на монитор. Кажется, такие назывались алфавитно-циферными?{w} Жаль, что я в подобных раритетах не разбираюсь."
    dreamgirl "Не \"циферный\", а алфавитно-цифровой."
    "Прозвучал в голосе надзидательный голос."
    dreamgirl "Забавно - память у нас общая на двоих, но почему-то пользоваться ей как следует умею только я."
    th "Это называется разделением обязанностей. Я занимаюсь сложными мыслительными процессами. Не барское это дело - отвлекаться на всякую рутину."
    "Препираться с собственным подсознанием было довольно странным занятием, но люди со скуки чего только не делают."
    nvl clear
    "Кажется, скучновато было не мне одному, потому что довольно скоро голос в моей голове поинтересовался:"
    dreamgirl "Кстати, а зачем тебе разбираться в этом хламе, который здесь считают за компьютеры?"
    th "Ну… Смог бы больше узнать об уровне {i}их{/i}\ \ технологического развития.{w} Пока что всё, что я здесь видел, не даёт никаких стоящих привязок." # двойной пробел - намеренно
    th "Тот же \"Икарус\", например - он может и на тридцать лет устареть, а его всё будут использовать, если только не развалится раньше."
    dreamgirl "Ну так и комп этот ни о чём не говорит. Может быть, он последней модели, а может - старьё убогое, которое попросту выбросить жалко."
    "Довод был веским, возразить было особенно нечего. Я мысленно вздохнул:"
    th "Ну, может, хоть покопался бы в документах на нём, приведись такой случай. А так непонятно даже, что и как там запускать…"
    th "Да, пойти по техническому пути поиска ответов не удастся."
    if alt_day2_club_join_cyb:
        dreamgirl "Чего это ты вдруг так приуныл? Наведайся к кибернетикам, у них там похожий комп, помнится, был - авось помогут. А там дальше сам сообразишь, тыжпрограммист."
        th "Да не, бред какой-то несёшь."
        dreamgirl "Да."
        "Легко согласился внутренний собеседник."
        dreamgirl "И имею на то полное право."
    else:
        th "Впрочем, не больно-то и хотелось."
    $ set_mode_adv()
    "Голос Виолы прервал мою беседу с подсознанием."
    show cs normal2 glasses_thr at center with dissolve
    cs "Всё!"
    stop sound_loop fadeout 1
    "Нанеся несколько особенно резких ударов по клавишам, Виола оттолкнула клавиатуру прочь."
    cs "Ох, если бы ты знал, пионер, насколько у меня из-за всего этого прибавилось бюрократической волокиты!"
    show cs badgirl3 glasses_over with dissolve
    "Она откинулась на спинку стула и с удовольствием потянулась.{w} Я поспешно перевёл взгляд на таблицу для проверки зрения."
    show cs normal3 glasses_over with dissolve
    cs "Мне и так хватает регулярной писанины, а после вчерашних подвигов Александра я теперь до осени буду отчёты писать."
    "Она села прямо и осуждающе посмотрела на меня:"
    show cs normal glasses_over with dissolve
    cs "Вот скажи мне, Семён, неужели нельзя было обойтись без этой беготни?"
    "Впрочем, увидев моё возмущённое лицо, Виола поспешила сменить гнев на милость:"
    show cs smile2 glasses_over with dissolve
    cs "Ладно, ладно, не обижайся!{w} Я знаю, что ты здесь не при чём, а наоборот, проявил себя героем, и всё такое.{w} Просто если что-то мне и не нравится в моей работе, так эта вот писанина."
    show cs normal2 glasses_over with dspr
    "Она недовольно поморщилась - похоже, это действительно было для неё больной темой.{w} Потом кивнула на монитор перед собой:"
    cs "Спасибо, хоть эту железяку выделили, писать всё это от руки было бы просто адской мукой."
    me "Ну, если у Вас {i}медицинский{/i} почерк…"
    "Почувствовав, что хватил несколько через край, я поспешно заткнулся, но Виолетта только рассмеялась:"
    show cs laugh2 glasses_over with dspr
    cs "В точку, пионер, в точку! Здесь у нас с начальством действительно была вполне обоюдная заинтересованность."
    "Решительно отвернувшись от стола, она заявила:"
    show cs smile glasses with dspr
    cs "Вот что! Я уже часа два как на ногах, так что изрядно проголодалась.{w} Да и тебя на завтрак лучше не отпускать, а то убредёшь куда-нибудь…"
    "Отмахнувшись от моих возражений, она продолжила:"
    show cs badgirl2 with dissolve
    cs "…Поэтому предлагаю позавтракать прямо здесь и перейти наконец к {i}делу{/i}."
    "Сделав ударение на последнем слове, она искоса взглянула на меня, и, видимо, удовлетворившись достигнутым эффектом, продолжила как ни в чём не бывало:"
    show cs normal3 with dissolve
    cs "Уверена, у тебя накопилось достаточно вопросов.{w} На некоторые из них у меня даже найдутся ответы."
    show cs important with dissolve
    cs "Но сначала - завтрак! Пошарь-ка под изголовьем кушетки, там должна быть плитка. Керамическая."
    show cs normal3 with dissolve
    "Изрядно озадаченный, я заглянул под кровать. Действительно, там обнаружилась солидных размеров облицовочная плитка, которую я и извлёк на свет божий."
    hide cs with dissolve
    "Тем временем Виола встала и, покопавшись в одном из своих шкафчиков, достала оттуда приземистый стальной чайник и провод в какой-то странной, словно бы матерчатой, изоляции."
    dreamgirl "Ух ты, а вот это и вправду раритет!"
    "Наполнив чайник водой, Виола отобрала у меня плитку и положила её на стол. Водрузив чайник сверху, она кивнула на мой недоумённый взгляд:"
    show cs smile3 with dissolve
    cs "Противопожарная безопасность! Учись, студент!"
    show cs normal3 with dissolve
    "Воткнув вилку в розетку и удовлетворённо кивнув, Виола подошла к холодильнику."
    hide cs with dissolve
    "…"
    show cs smile2 close with dissolve
    "Как оказалось, в холодильнике и ящиках стола можно было найти не только лекарства, так что через десять минут мы уже пили крепкий чай с бутербродами с сыром и хрустели печеньем \"Юбилейное\""
    me "Виола, скажите, а как у Шурика дела?"
    "Поинтересовался я, не отрываясь от еды. Медсестра отмахнулась от меня кусочком печенья:"
    show cs normal2 close with dspr
    cs "Да нормально всё с вашим Трофимовым. Отоспался, и теперь бодрый как огурчик."
    cs "Психосоматика у него в порядке, разве что налицо значительные провалы в памяти. Воспоминания о вчерашнем дне преимущественно носят отрывочный характер."
    th "Да, кто-то явно перестарался с составлением отчётов…"
    cs "Я пока что его отправила отсюда, чтобы не мешался. Велела перед обедом заглянуть, попробую его разговорить - может и вспомнит что-нибудь."
# Возможно, нужно больше умных букв про состояние Шурика и причины оного. Всё-таки медик-научник.
    "Когда с едой было покончено, заметно подобревшая Виола благосклонно кивнула:"
    show cs smile close with dissolve
    stop music fadeout 5
    cs "Спрашивай. Ты ведь не ради Александра пришёл?"
    me "Э-э-э… Виолетта, а может быть, лучше Вы мне сначала расскажете что-нибудь? Обрисуете в общих чертах, так сказать?"
    play music music_list["into_the_unknown"] fadein 5
    show cs smile2 close with dissolve
    cs "Хорошо, давай так. Пожалуй, так даже лучше будет, времени не так много осталось - сегодня ведь последний день смены, ты в курсе, кстати?"
    jump alt_day6_true_CS_talk_main


# Короткий разговор с Виолой
# Про эф.Ветрова не узнаём
#
# Зато узнаём, что завтра - последний день смены (ВЦ сожалеет, что ГГ пришёл так поздно, времени, мол, и так мало осталось)
label alt_day6_true_CS_talk_short:
    window hide
    scene bg ext_aidpost_day with fade
    play ambience ambience_camp_entrance_day fadein 3
    $ alt_uvao_D6_CS_vetrov = False
    window show
    "Когда я добрался наконец до медпункта, время уже явно приближалось к полудню."
    "Подойдя к двери, я решил постучаться. Как-никак, медицинский кабинет, мало ли что…"
    play sound sfx_knock_door7_polite
    cs "Открыто!"
    window hide
    play sound sfx_open_door_1
    play music music_list["eternal_longing"] fadein 3
    stop ambience fadeout 3
    scene bg int_aidpost_day with fade
    show cs normal3 far at center with dissolve
    window show
    me "Добрый день, Виолетта!"
    show cs normal3 with dissolve
    "Увидев меня, Виолетта нахмурилась."
    cs "Я уже думала, ты вовсе не придёшь. Что, так сильно напугала тебя вчера на ужине, или у тебя утро начинается с полудня?"
    me "Да я…"
    cs "Ладно, садись. Чай будешь?"
    "Виола кивнула на стоящий на письменном столе странноватого вида металлический чайник, судя по проводу - электрический."
    me "Да спасибо, я только что поел…"
    show cs doubt2 close with dissolve
    "Промямлил я, усаживаясь на краешек кушетки. Виола слегка подняла одну бровь, но больше своё удивление по поводу столь позднего завтрака никак не показала."
    show cs normal2 close with dissolve
    cs "Ну что же, тогда к делу. Времени не так много, скоро Трофимов должен придти, я его ещё с утра выпроводила позавтракать и подышать воздухом, чтобы не мешался здесь, велела перед обедом зайти."
    "Она вновь одарила меня недовольным взглядом, явно досадуя на моё опоздание, и я поспешил увести разговор несколько в сторону:"
    me "Ой, да, Шурик! Как у него дела, кстати? Не бузил больше?"
    cs "Да нормально всё с вашим Трофимовым. Отоспался, и теперь бодрый как огурчик.{w} Психосоматика у него в порядке, разве что налицо значительные провалы в памяти. Воспоминания о вчерашнем дне явно носят отрывочный характер."
    cs "Как придёт, попробую его разговорить - может и вспомнит что-нибудь."
    me "Так, может быть, я потом к Вам зайду, когда времени побольше будет?"
    stop music fadeout 3
    show cs doubt2 close with dissolve
    cs "Потом времени не будет вовсе. Сегодня ведь последний день смены, ты что, не знал?"
    play music music_list["into_the_unknown"] fadein 5
    jump alt_day6_true_CS_talk_main

# Общий блок по длинному и короткому разговорам
# TODO необходимо ввести упоминание Виолой текущей даты (в Д6 это 23 июля 1989) и реакцию ГГ в зависимости от alt_uvao_D5_evening_sl
# возможно - в связи с датой также по alt_uvao_D5_evening_sl краткое упоминание Слави и разъяснение, что она имеет отношение к работе
label alt_day6_true_CS_talk_main:
    if not alt_uvao_D5_evening_sl:
        "От неожиданности я подскочил на месте."
        me "Как последний? Меньше недели ведь прошло!"
        show cs normal2 close with dissolve
        cs "Ну так и ты… гм… прибыл сюда не к началу смены. Сегодня прощальные ужин и дискотека, а завтра на автобус до райцентра - и по домам."
        "Я почувствовал, что в глазах темнеет."
    else:
        me "Вот только недавно узнал, случайно…"
        show cs smile2 close with dspr
        cs "И действительно, кому это придёт в голову прислушиваться к словам вожатой на линейке…"
        show cs normal2 close with dspr
        cs "Так что сегодня прощальные ужин и дискотека, а завтра на автобус до райцентра - и по домам."
# Здесь нужна рефлексия о том, как мало успелось и хочется ли возвращаться назад.
    me "Виола, скажите…{w=1} А я что, тоже… домой?"
    "Последнее слово далось мне с неожиданным трудом. Я вдруг отчётливо понял, что язык не поворачивается назвать приютившее меня до этого место - домом."
    window hide
    $ set_mode_nvl()
    window show
    th "Да, именно приютившее. Место, где можно спать и хранить одежду, для которой сейчас не сезон, - и только. Место, куда приходишь потому, что больше идти некуда, а не потому, что хочешь туда придти."
    th "Место, где меня никто не ждал и не будет ждать."
    "Голос Шизы в моём мозгу прозвучал спокойно и как-то даже буднично:"
    dreamgirl "Подумай-ка лучше вот о чём - весь {i}тот{/i} \ мир - можешь ли ты назвать его своим домом?"
    dreamgirl "А главное - хочешь ли ты его так называть?"
    dreamgirl "Что связывает тебя с ним? Работа, которая тебе неинтересна и неприятна ровно настолько, чтобы её можно было терпеть? Те несколько человек, которые считаются твоими друзьями только потому, что ты всё ещё не удалил их номера из телефона?"
    dreamgirl "Аноны всех мастей, эти призраки по ту сторону экрана, которым нет никакого дела до тебя, а тебе - до них?{w=2} Подумай."
    th "Но ведь и здесь я чужой! Здесь меня тоже никто не ждёт и никому до меня нет дела!"
    dreamgirl "Ты прав. {w=1.0}Почти прав. {w=1.0}Тебе и здесь совсем нетрудно будет отгородиться ото всех стеной безразличия, ты неплохо освоил это умение, так что тебя рано или поздно оставят в покое."
    dreamgirl "Однако, если тебе удастся ухватиться за этот шанс, ты сможешь попробовать сделать что-то иначе."
    dreamgirl "Вспомни, всем ли людям, встреченным тобой за эти несколько дней, не было до тебя никакого дела?"
    nvl clear
    dreamgirl "Да, с тобой никто не нянчился и на руках не носил. Да, над тобой подшутили пару раз не самым приятным образом - но именно подшутили, тебе не желали зла по-настоящему."
    dreamgirl "Зато когда ты из-за этих шуток остался голодным - тебя накормили. Помогли не по обязанности, а просто потому, что тебе была нужна помощь."
    dreamgirl "Кстати, когда тебе вчера чуть не устроили внеплановую проверку содержимого черепа - никто ведь не заставлял Лену тебя спасать. Просто она могла помочь - и сделала это."
    th "Если уж на то пошло, если бы меня вчера не припахали к этим поискам, то мне вообще ничего бы не угрожало!"
    dreamgirl "Это так, но что странного в том, что люди, готовые тебе помочь {i}просто так{/i}, ожидают того же самого и от тебя?{w} Не потому, что ты у них в долгу, а просто потому, что кому-то нужна помощь, а ты можешь помочь."
    th "Тебя послушать, так здесь просто идеальное общество какое-то! Царствие небесное на земле! Все люди братья, все помогают друг другу!"
    dreamgirl "Ну разумеется, нет. Заметь только, что и не каждый человек другому обязательно - волк. Подумай, хочешь ли ты быть кому-то нужным и готов ли к тому, что кто-то снова будет нужен тебе?"
    dreamgirl "Так что ещё раз повторю - ты можешь попытаться сделать что-то иначе."
    dreamgirl "Умный учится на чужих ошибках, дурак - на своих. А как называется тот, кто не учится ни на чьих?"
    window hide
    $ set_mode_adv()
    window show
    cs "Да, Семён, ты завтра тоже отправишься домой, это вне всякого сомнения."
    "Подала наконец голос молчавшая всё это время Виолетта."
    "Я внезапно почувствовал, что не могу вздохнуть, словно чьи-то крепкие безжалостные руки сжали горло, начисто перекрыв кислород."
    th "Неужели эта нелепая фантазия о возможности остаться здесь, этот призрачный шанс, упомянутый Шизой, и вправду так уж нужны мне?"
    "Не сводящая с меня глаз Виола вдруг слегка улыбнулась."
    show cs smile close with dissolve
    cs "Впрочем, я не возьмусь сказать наверняка, где именно окажется этот самый дом, куда ты отправишься.{w} Это будет зависеть только от тебя."
    show cs doubt2 close with dissolve
    "Вероятно, моё лицо в этот момент представляло собой то ещё зрелище, потому что она несколько секунд с явным интересом меня разглядывала, прежде чем пуститься в объяснения."
    show cs normal2 close with dspr
    cs "Видишь ли, то, что ты попал в этот лагерь, да и вообще в наш мир - не совсем случайно.{w} Такое случается время от времени с людьми, которые там, {i}у вас{/i}, не имеют крепких социальных связей, привязанностей, постоянно чувствуют себя ненужными."
    cs "Разумеется, такое происходит далеко не со всеми, на наше счастье - иначе здесь уже не протолкнуться было бы от попаданцев."
    show cs smile2 close with dspr
    "Тут она улыбнулась чуть виновато и развела руками."
    show cs smile close with dissolve
    cs "Надеюсь, ты простишь мне профессиональный жаргон - \"попаданцы\". Не слишком-то благозвучно, но официальное \"лица, непроизвольно осуществившие переход через точку прокола\" даже руководство избегает употреблять." # официальный термин может быть и другим, но непременно громоздким и неуклюжим
    show cs smile2 close with dissolve
    "Я робко кивнул, и Виола продолжила:"
    show cs normal2 close with dspr
    cs "Ну так вот, слабо привязанного к своему родному миру человека иногда может, скажем так, \"притянуть\" сюда. Причём это всегда происходит в местности с достаточно большим количеством благожелательно настроенных людей, готовых принять появление новичка за естественное событие."
    cs "Чаще всего это случается на курортах, в домах отдыха каких-нибудь. Ну или как здесь, например, - в пионерском лагере. Так или иначе, непременно нужен мощный благоприятный эмоциональный фон - это обязательное условие для возникновения в этом месте точки прокола, как мы это называем."
    show cs doubt2 close with dspr
    "Тут она посмотрела на меня с некоторым сомнением и осведомилась:"
    cs "Я не слишком заумно объясняю? Пока что всё понятно?"
    "Кажется, Виола вплотную подошла наконец к весьма важному для меня моменту, так что я лишь нетерпеливо отмахнулся:"
    me "Да понятно всё, понятно! Идею насчёт этих… попаданцев я уловил. Что дальше-то происходит с ними?{w=2} Вернее, с нами…"
    show cs smile2 close with dspr
    "Виола усмехнулась моей нетерпеливости и продолжила:"
    cs "А дальше начинается процесс адаптации. Если он заканчивается успешной ассимиляцией, то в нашем мире становится на одного человека больше."
    cs "Если попаданцу не удалось здесь адаптироваться, то его, по-видимому, выбрасывает обратно в родной мир.{w} Впрочем, сам понимаешь, {i}здесь{/i} \ мы можем лишь строить догадки."
    me "И что же нужно для этой самой… успешной адаптации?"
    "Я затаил дыхание в ожидании ответа, но Виола лишь пожала плечами:"
    show cs smile2 close with dspr
    cs "Да ничего особенного, по большому-то счёту. Достаточно, чтобы человек действительно захотел остаться."
    show cs normal2 close with dspr
    cs "Попросту говоря, человек попадает сюда, только если он чужой {i}там{/i} \ и остаётся, если почувствует себя своим {i}здесь{/i}."
    me "А как понять, что адаптация прошла успешно?"
    show cs smile2 close with dspr
    "Виола мягко улыбнулась."
    dreamgirl "Вообще, она сегодня на редкость улыбчивая, ты не находишь?"
    show cs smile close with dissolve
    cs "Я смотрю, тема адаптации тебя заинтересовала? Что же, ничего удивительного. Все хотят знать про своё будущее."
    show cs smile2 close with dissolve
    cs "Видишь ли, в первое время попаданец сильно связан с местом перехода, удаляться от него далеко он не может."
    cs "При попытке сделать это его либо отбросит назад, в район перехода, либо вообще выбросит обратно в родной мир - зависит от того, насколько он успел здесь закрепиться."
    if alt_day2_us_escape:
        cs "Кстати, подозреваю, что на второй день пребывания здесь ты уже испытал такой перенос в пространстве. Или я ошибаюсь?"
        "Я смущённо заёрзал на месте, вспомнив нашу совместную с Ульянкой авантюру."
        me "Ну, мы вроде бы как, действительно догнали проходящий состав и забрались в него, а потом проснулись снова на острове. Но я думал, всё это нам просто приснилось!"
        cs "Вот именно: {i}вам{/i}. Насколько мне удалось выяснить, Ульяна видела аналогичный сон, так что гипотеза о переносе представляется мне вполне уместной."
        th "И всё-то она знает, даже то, что приснилось Ульянке…{w} Минуточку! Ульянка!"
        me "Виола, подождите, а Ульяна - она что, тоже из… Ну, нездешняя?"
        show cs normal2 close with dspr
        cs "Нет, она местная, это совершенно точно. А с чего ты это взял?"
        "Виолетта недоумённо посмотрела на меня, и я смущённо потупился."
        me "Ну если это был не сон, то, получается, что её тоже перенесло вместе со мной…"
        show cs smile2 close with dspr
        cs "Ах вот ты о чём. Нет, здесь, вероятно, сыграло роль твоё присутсвие рядом. К сожалению, такие случаи вообще пока что плохо изучены."
        cs "Мы, может, и рады были бы прицепить к составу вагон с мобильной лабораторией и собрать побольше информации, но ты же не удосужился о своём намерении прогуляться предупредить заранее, недели за три…{w} Впрочем, всё это сейчас несущественно."
    show cs important close with dissolve
    cs "Важно то, что завтра лагерь опустеет, и привычный эмоциональный фон исчезнет. Поэтому при попытке уехать переноса в пространстве уже не произойдёт - тебя либо выбросит назад в твой родной мир, либо ты окончательно останешься в этом."
    cs "Так что через сутки с небольшим ты самостоятельно сможешь оценить, насколько успешно прошла твоя адаптация."
    show cs normal3 close with dissolve
    "Виола замолчала, откинувшись на спинку стула. Молчал и я, переваривая услышанное."
# Чувствуем нарастающую панику - обратно тоскливо, здесь - ссыкотно
    me "Виола, скажите, а если я останусь здесь…"
    show cs smile close with dissolve
    cs "Да не волнуйся ты так, Семён."
    show cs normal close with dspr
    cs "Не беспокойся раньше времени о том, чего ещё не случилось, да и выплывать совсем уж в одиночку тебе не придётся. С базовой социальной адаптацией тебе помогут, а дальше уже всё от тебя будет зависеть."
    show cs laugh2 close with dissolve
    "Она вдруг рассмеялась."
    cs "Кстати, тебе сколько лет-то, {i}пионер{/i}?"
    me "Э-э… Семнадцать?"
    show cs smile2 close with dissolve
    cs "Ну да, правильно! Так всем и говори. Мало ли что там написано в каком-то странном паспорте несуществующей страны, пусть даже там и твоя фотография?"
    th "Так, похоже, что пальто моё нашлось…"
    show cs important close with dissolve
    cs "Просто не забывай, что по сравнению со среднестатистическим выпускником детского дома у тебя, можно сказать, есть фора в десять лет опыта взрослой жизни. Вернее будет, если сумеешь остаться."
    show cs smile2 close with dissolve
    "Виола замолчала, глядя меня с чуть снисходительной полуулыбкой."
    "Странно, но после её слов меня как будто немного отпустило. В самом деле, не в Антарктиду же меня десантировать собираются. {w}Однажды мне уже было семнадцать - и ничего, выжил, а по второму-то разу и впрямь проще должно быть."
    "С облегчением переведя дух, я спросил неожиданно для себя самого:"
    show cs normal2 close with dissolve
    me "Скажите, Виола, а зачем всё это?{w=1} Я имею в виду, все эти переходы, точки… этих, как их… точки прокола… Вся эта возня с нами, попаданцами?"
    show cs smile2 close with dspr
    cs "Ну насчёт существования феномена проколов как такового - вопрос \"зачем\" ставить некорректно. Всё равно что спрашивать \"зачем светит солнце\". Проколы просто существуют, а мы их изучаем по мере возможности."
    show cs normal2 close with dspr
    cs "А что касается \"возни с попаданцами\", как ты выразился, то здесь же всё достаточно очевидно. Во-первых, люди прибывают, хотим мы этого или нет.{w} Даже если отбросить элементарный гуманизм, всё равно гораздо практичнее сразу помочь им, чем получить впоследствии потенциально асоциальных личностей."
    cs "Ну а во-вторых, большинство из попадающих сюда в итоге оказываются довольно полезны для общества. Люди способные, мыслящие, как-то больше склонны задумываться о тщетности всего сущего и заметно чаще оказываются чужими среди своих {i}там{/i}."
    dreamgirl "Кажется, тебе только что сделали комплимент, причислив к способным и мыслящим, ты не находишь?"
    th "Ну она же не сказала, что {i}все{/i} \ попаданцы обязательно такие…"
    "Словно подслушав мои мысли, Виола внезапно перешла, если так можно выразиться, на личности:"
    show cs smile2 close with dspr
    cs "Вот взять хотя бы тебя, например.{w=1} Достаточно устойчив психически, чтобы сохранить адекватность поведения, попав абсолютно внезапно в непривычную обстановку…"
    "…Я вспомнил, как валялся в обмороке на полу домика вожатой в свой первый день здесь, и почувствовал, что краснею…"
    #if semen_panique: # Паника возле автобуса #ЛБ: на данный момент (0.25с) неактуально. От автобуса ни на шаг.
    #    "… А приступ паники и безумная беготня по дороге?"
    dreamgirl "Всё познаётся в сравнении. Возможно, другие вели себя ещё хуже."
    cs "…и не лишён определённых артистических наклонностей, раз сумел не раскрыться ни перед кем из пионеров за это время."
    show cs normal close with dissolve
    cs "Кроме того, демонстрируешь весьма похвальную любознательность, раз при встрече с… {i}Юлей{/i}, да… пошёл с ней на контакт, да и вообще, я смотрю, ты в это дело втянулся."
    show cs normal2 close with dissolve
    "К этому моменту я от смущения уже готов был начать ковырять пальцем клеёнку на кушетке, так что ухватился за возможность сменить тему на несколько отвлечённую от моей персоны."
    me "Виола, вот, кстати…"
    "Я протянул ей смартфон."
    me "Вы просили Юлю сфотографировать."
    show cs doubt2 glasses_thr close with dissolve
    "С минуту Виола с любопытством разглядывала снимок на экране."
    cs "Интересное проявление…{w} Порождает больше вопросов, чем ответов."
    me "Так кто она… что она такое?"
    cs "Анималистическая персонификация межпространственного прокола."
    show cs normal2 glasses_over close with dissolve
    "Она произнесла это таким тоном, будто я должен был немедленно шлепнуть себя ладонью по лбу и вскричать - ну конечно же, как я сразу не догадался!"
    "К сожалению, единственной вербальной реакцией, которую я был готов выдать было \"Эээ… чо?\""
    "Вслух я этого, конечно, не сказал, но Виола поняла и без слов."
    cs "Видишь ли, пионер, обычно прокол открыт неделю-другую, иногда немногим дольше. О многих из них мы вообще узнаём постфактум, когда попаданец уже совершил переход в наш мир."
    cs "И изучать при этом уже особенно нечего - пока оборудование доставят, пока локализуют точку прокола… Впрочем, подробности тебе вряд ли будут интересны."
    show cs normal glasses close with dissolve
    cs "Но крайне редко, по не вполне ясным причинам, прокол получается аномальным - счёт идёт на годы, но переноса не происходит. И вот вблизи от них со временем и заводятся подобные… э-э-э… существа. Судя по всему, это одно из проявлений самого прокола."
    cs "Собственно, твоя фотография это отчасти подтверждает. Это ведь город из твоего мира, я угадала?"
    me "Да… Очень похоже на вид из моего окна.{w} Так получается, что это первая фотография… как её… персонификации?"
    show cs normal3 close with dissolve
    cs "В общем-то да. С момента начала регулярных исследований подобных долговременных проколов было зафиксировано всего четыре."
    "И, как довольно быстро выяснилось, взаимодействовать с персонификациями следует с большой осторожностью.{w} Были… гм… неприятные инциденты."
    me "Тогда почему Вы были так уверены, что со мной ничего не произойдёт?"
    cs "Видишь ли, общение с пер… с Юлей потенциально опасно для всех, кроме тебя. На тебя, как на субъекта переноса, у неё своего рода привязка, поэтому ты её воздействия не ощущаешь."
    show cs normal3 close with dissolve
    cs "Что может произойти с другими — ты видел вчера."
    me "Значит, это и в самом деле Юля Шурика так…"
    cs "Строго говоря, очевидцев нет, но всё указывает на то, что произошедшее с ним - результат её воздействия."
    if alt_uvao_D6_CS_vetrov: # длинный разговор за чаем
        "Задумчиво покивав чему-то, Виола продолжила:"
        cs "Так вот, более-менее ясно, что персонификация является частью прокола."
        cs "Большинство попаданцев упоминает о том, что в процессе перехода общались с некоей личностью, причём все они описывают её как нейтрально или даже дружелюбно настроенную."
        cs "Кстати, Семён, ты ничего подобного не припоминаешь?"
        me "Дружелюбную личность? Нет, кажется, не..."
        dreamgirl "Полно врать-то. Ну же, вспоминай давай!"
        # цг девочка - виденье.
        play music knock fadein 5
        window hide
        scene anim prolog_1
        window show
        "В голове вдруг встал нечёткий образ девочки из сна. Мы встречаемся в этом повторяющемся сне уже чуть больше года, но вот что странно: в моей памяти так и не отпечаталось, как она выглядит."
        "Наутро я всегда помню сон в общих чертах, из памяти стирается только её внешность. Почему? Сколько я себя не спрашивал, мне никогда не удавалось найти ответа."
        "Девочка медленно идёт в мою сторону. Время словно замирает, а я просто стою и жду, пока она подойдёт ближе. Для меня становится очень важным рассмотреть её."
        "Отчаявшееся сознание предпринимает очередную бесплодную попытку. Какой в ней смысл, если я всё равно ничего не вспомню утром?"
        "Девочка между тем подходит достаточно близко, чтобы я смог разглядеть её одежду. На ней красное детское платьице. Красивое. А ещё почему-то совсем нет никакой обуви."
        "Вот она делает ещё пару шагов и я с удивлением замечаю, что платьице вовсе не новое - оно протёрлось в нескольких местах до дыр и явно в разы старше его обладательницы. Мамино?"
        "Девочка медленно приближается ко мне. Я никак не могу разглядеть её лицо, сколько ни пытаюсь всматриваться."
        "Память почему-то усердно твердит мне, что я её знаю, что я определённо знаком с ней."
        "Она делает ещё несколько шажков. Неожиданно моему взгляду открываются миленькие ушки на её голове. Может, накладные?" 
        "Она подходит всё ближе и ближе, а я вдруг обращаю внимание на то, что откуда-то из-за спины у неё выныривает беспокойный кошачий хвостик."
        "Вот нас разделяет всего несколько метров. Мне ясно открывается, что она вовсе не человек, а ушки, казавшиеся издалека накладными, самые что ни на есть настоящие."
        "Мне почему-то совсем не страшно."
        "Девочка подходит почти вплотную и поднимает на меня взгляд."
        "Мне наконец-то удаётся разглядеть её лицо. Она, не моргая, тоже смотрит на меня ореховыми глазами, полными любопытства и... надежды?"
        "Я застываю от её взгляда и не могу отвести глаза. Мы просто стоим и смотрим друг на друга."
        "Она первой нарушает тишину."
        uv "Ты пойдёшь со мной?"        
        window hide
        stop music fadeout 5
        $ renpy.pause(2, hard=True)
        window show
        # обратно медкабинет.
        "Я не успеваю ответить. Чей-то голос словно разрезает пространство между содержимым моей головы и реальностью."
        scene bg int_aidpost_day with fade
        show cs normal3 close with dissolve
        cs "Семён, ты меня слышишь?"
        "Пощёлкала пальцами у меня под носом Виолетта."
        "Мучительно путаясь в словах, я, как мог, рассказал Виоле об этом смутном видении, о девочке, которая спрашивала меня, пойду ли я с ней."
        "Виола удовлетворённо кивнула:"
        cs "Ну вот видишь. {w}Другое дело, что в обычных проколах все её проявления этим и ограничиваются. Скорее всего, там она и существует только для субъекта переноса - если это вообще можно назвать существованием."
        cs "А вот в случае аномального прокола, судя по всему, персонификация становится довольно самостоятельной сущностью, хотя и связанной по-прежнему с точкой прокола."
        cs "Некоторые исследователи полагают, что здесь она начинает выполнять ещё и защитную функцию по отношению к проколу - не допускает перемещения случайных людей между реальностями."
        cs "Вообще говоря, это уже из разряда явных домыслов… Хотя и неплохо объясняет, почему таких спонтанных перемещений оказавшихся рядом посторонних людей не происходит, хотя напряжённости прогиба в зоне долгосрочных аномальных переходов…"
        show cs smile3 close with dissolve
        "Виола оборвала сама себя, виновато улыбнувшись."
        cs "Извини, что-то я слегка увлеклась.{w} Объясню попроще: все наблюдавшиеся долгосрочные проколы очень быстро становились достаточно, скажем так, широкими, чтобы в них могло затягивать случайно оказавшихся рядом людей, в том числе и в обратном направлении, то есть {i}отсюда{/i}."
        show cs normal3 close with dissolve
        cs "Авторы теории завяляют следующее: во-первых, для перемещения между реальностями необходимо как-то с взаимодействовать с проколом."
        cs "Во-вторых, ни одного случая таких перемещений не зафиксировано, зато большинство контактов с аномальной визуализацией заканчивались помутнением сознания с последующей потерей памяти, либо психическими расстройствами различной тяжести."
        cs "Из этого они делают вывод, что это именно персонификация принимает меры для предотвращения переноса, делая невозможным взаимодействие субъекта со структурой прокола."
        me "Э-э-э…"
        show cs important close with dissolve
        cs "Говоря совсем простым языком - взаимодействия между структурой прокола и разумом, сознанием человека не произойдёт, если нечему будет взаимодействовать. {w}Вот это-то она и обеспечивает, причём вполне эффективно, делая человека непригодным к взаимодействию."
        show cs normal3 close with dissolve
        cs "Впрочем, повторюсь, это всего лишь гипотеза, причём весьма сомнительная, на мой взгляд. С куда большей вероятностью всё это может быть попросту механизмом самозащиты самой персонификации."
        cs "Более того, некоторые факты этой гипотезе прямо противоречат - например, инцидент семьдесят четвёртого года."
        me "А что тогда было?"
        "Виола помрачнела. Кажется, своим вопросом я пробудил в ней какие-то неприятные воспоминания."
        "Я уже было хотел дать задний ход, как она наконец ответила:"
        cs "Куратор тогда мой погиб."
        me "Ох…"
        "Только и сумел выдавить я."
        "В воздухе на некоторое время повисло тяжёлое молчание."
        cs "Лагерь тогда ещё на старом месте был, ты вчера видел оставшееся от него основное здание."
        cs "Решила Юлия Валерьевна в обход всех инструкций контакт наладить. {w}Наладила… Спасибо, эффектом Ветрова лагерь не накрыло."
        "Виола говорила спокойно, однако трудно было не заметить, что тема эта ей неприятна. Я спросил наугад:"
        me "Эффект Ветрова? А что это?"
        show cs normal close with dissolve
        cs "А это, судя по всему, как раз проявление защитного механизма персонификации на полную мощность, что бы там она ни защищала при этом - себя или аномалию."
        cs "Назвали, кстати, по фамилии попаданца из того самого прокола, где это произошло."
        show cs normal2 close with dissolve
        cs "Там всё было примерно так же, как и у нас здесь. Долгосрочный аномальный прокол с выходом в районе турбазы, в какой-то момент произошёл наконец перенос субъекта, процесс адаптации протекал нормально."
        show cs smile2 close with dspr
        cs "Потом внезапно установился контакт и началось взаимодействие субъекта с персонификацией. {w}Опять-таки, похоже на нашу ситуацию, правда?"
        "Сказав это, Виола как-то странно мне подмигнула."
        show cs normal close with dissolve
        cs "А вот потом, как я подозреваю, кому-то из начальства наград захотелось, но это между нами.{w} Так или иначе, бросили группу армейского спецназа на захват."
        cs "Вскоре после этого участок местности… закрылся, скажем так. {w=2.0}Наверное, со стороны это даже забавно должно было выглядеть - турбазу накрыло эдаким куском плотного тумана."
        cs "Входишь в него — ничего особенного не происходит, пара минут в густом тумане, и выходишь с другой стороны. Но внутри ни зданий, ни растений. Ни людей."
        show cs normal3 close with dissolve
        "Виола замолчала, и я не удержался от вопроса:"
        me "Ну так а дальше-то что?"
        show cs doubt3 close with dspr
        cs "А что дальше? Оцепили, стали ждать. На четвёртый, кажется, день из тумана вышли двое - и это всё, больше никого."
        show cs normal3 close with dspr
        cs "Один не помнил вообще ничего, а второй нёс какую-то чушь про карателей, девочек в эсэсовской форме, демонов каких-то… В общем, в итоге установили там зону отчуждения на несколько километров во все стороны, да и всё."
        "Она помолчала, что-то прикидывая, потом медленно продолжила:"
        cs "К сожалению, информация о том инциденте довольно обрывочная. Частично - из-за утраты при инциденте большей части записей и данных наблюдений. Частично… Ты сам понимаешь, наверное, что нашлись достаточно влиятельные люди, которые {i}очень{/i} постарались замять произошедшее."
        cs "До работающих на аномальных проколах довели краткую инструкцию о недопустимости форсирования контактов с персонификациями - без объяснений и подробностей."
        cs "Мне понадобилось довольно много времени и очень много везения, чтобы разузнать хотя бы те крохи, которыми я сейчас с тобой поделилась. Будь у Юлии Валерьевны хотя бы они…"
        cs "А так она решила рискнуть. На тот момент аномалия существовала уже… {w=0.8}да, восемнадцать лет, никаких серьёзных происшествий не было зафиксировано. Персонификацию замечали периодически, но какой-либо агрессии она не проявляла."
        cs "Опасно она не выглядела - да никак она, по большому счёту, не выглядела. Размытый силуэт, ничего больше."
        "Виола с досадой махнула рукой."
        cs "Словом, когда Юлию нашли повесившейся в кабинете, то думали, что теперь прокол закроется. Самоубийство должно было напрочь разрушить весь эмоциональный фон - какие уж тут попаданцы!"
        cs "Меня, как ее подопечную, оставили здесь приглядывать, исследовать процесс коллапса прокола. Построили новые корпуса, перенесли лагерь…"
        cs "Но никакого коллапса не произошло! Прокол по-прежнему оставался открытым, хотя персонификация долгое время после этого не появлялась."
        cs "А в последние лет пять начались вдруг слухи про дикую девочку-кошку, которая ворует вещи и продукты."
        "Мы помолчали. Виола думала о чём-то о своём, а я пытался разобраться в обрушившейся на меня лавине информации."
        "Одна мысль не давала мне покоя."
        me "Виола, но всё-таки… если Юля так опасна - почему бы просто не закрыть лагерь насовсем? Не будет пионеров - не будет эмоционального фона, не будет прокола…" 
        me "Да даже если прокол останется - не на кого будет воздействовать!"
        cs "Мысль разумная, в чем-то даже мудрая. Вместо того, чтоб жить, как на вулкане, просто отгородиться от опасного. Не трогать, не взаимодействовать, в случае непредвиденных случаев - убегать. Так?"
        me "Ну… не совсем уж…"
        cs "Увы, Семён, эта роскошь нам не по карману. Отгородимся здесь - откроется где-нибудь ещё, в совершенно непредсказуемом месте."
        cs "И чем это может грозить - даже представить сложно. Тут-то мы готовы к неожиданностям, в меру сил."
        me "Правда?"
        cs "Ох, если б ты знал, сколько у меня тут инструкций и предписаний!"
        "Она с раздражением кивнула на компьютер, всё так же вяло гудевший вентилятором."
        cs "Целый отдел фантазирует, занимается прогнозом, и на каждую фантазию составляет план действий!"
        cs "На случай расширения прокола, на случай внезапного закрытия, на случай переноса человека от нас туда, даже на случай нашествия орды других… {i}персонификаций{/i}…"
        me "А такое возможно?"
        cs "Теоретически-то возможно всё. Собственно, эта самая {i}теория{/i} сейчас выглядит как \"здесь можно ожидать чего угодно\". Слишком мало мы обо всем этом знаем."
        cs "В будущем, я надеюсь, будем знать больше."
        me "Просто вы так легко об этом говорите - проколы, переходы, персонификация эта анимационная…"
        cs "Послушай, Семён, вот ты ходишь, разговариваешь, и вообще мыслишь в меру возможностей."
        cs "Это же не означает, что ты можешь в подробностях описать природу этих процессов, ну или хотя бы сообщить мне химический состав желудочного сока, с помощью которого сейчас переваривается съеденный тобой завтрак. {w}Я, как врач, знаю об этом побольше твоего, но тоже до определённого предела."
        cs "Во всякой области знаний есть узкие специалисты, но их мало, и понять их могут, в основном, такие же, как они сами, специалисты."
        cs "Или вот, скажем, термоядерный синтез какой-нибудь. Я в нём совершенно не разбираюсь и подозреваю, что ты тоже, но это ведь не мешает нам греться на солнышке - внутри Солнца ведь именно термоядерные реакции происходят, я ничего не путаю?"
        "Я открыл было рот, но наш разговор был прерван стуком в дверь."        
    else:
        # Продолжение короткого разговора, а скорее - попросту согласующий переход к alt_day6_true_SH_exam
        "Я собрался расспросить Виолу об этом поподробнее, но тут в дверь постучали."

label alt_day6_true_SH_exam:
    play sound sfx_knock_door7_polite
    sh "Можно?"
    show sh normal pioneer far at cright
    show cs normal at cleft
    with dissolve
    "В дверь нерешительно заглянул наш вчерашний \"ценный груз\"."
    cs "Да-да, входи, пионер. Позавтракал? Самочувствие не ухудшилось?"
    sh "Н-нет… все в порядке, я себя хорошо чувствую."
    th "И вправду, выглядит вполне нормально."
    cs "Все равно, лишний раз {i}осмотреть{/i} не помешает.{w} Проходи, раздевайся."
    th "Виола снова в своём репертуаре… Хорошо, что на мне не практикуется. Наверное, потому что мы теперь{w=.4} \"коллеги\"?"
    dreamgirl "Просто ты ей нужен более-менее соображающий, а не пускающий слюни. А Шурик наверняка от нее с утра натерпелся, поэтому и зашуганный такой."
    me "Виолетта Церновна, я тогда пойду?"
    "Шурик с мольбой посмотрел на меня. Оставаться с Виолой лицом к лицу ему явно не хотелось."
    cs "Нет, Семён, останься. Вдруг мне потребуется помощь в {i}процедурах{/i}?"
    dreamgirl "Что ты там говорил, \"коллега\"?"
    show sh normal pioneer far at cright behind cs
    show cs normal far at center
    with dissolve
    if alt_day2_med_done: # Если в д2 заходили в медпункт
        if alt_day2_rendezvous in [1,2,3]:
            th "Теперь моя очередь стоять в уголке и хихикать!"
            dreamgirl "Тогда вместо Шурика надо пригласить "
            if alt_day2_rendezvous == 1:
                extend "Лену, "
            elif alt_day2_rendezvous == 2:
                extend "Славю, "
            elif alt_day2_rendezvous == 3:
                extend "Алису, "
            extend "чтоб сполна расплатиться."
        else:
            th "Хоть оценю со стороны, насколько жутко это выглядит."
    else:
        "Я отошёл и опёрся о край стола, чтобы не мешать {i}осмотру{/i}."
    "Несмотря на многозначительные намёки Виолы, медосмотр завершился довольно быстро."
    cs "Да, пионер, ты практически здоров. Физически."
    sh "А… психически?"
    cs "А голова - предмет тёмный… Впрочем, будет полезно, если ты расскажешь, чем ты вчера занимался и что с тобой произошло."
    "Шурик задумался."
    sh "Вчера? Я вчера хотел детали поискать… Радиодетали, для робота… Нам в клубе не хватает, и мы никак закончить не можем."
    "Виола поощрительно кивнула."
    cs "Хорошо, детали."
    sh "А я, когда на чердаке копался, нашёл старую схему лагеря. На ней было бомбоубежище нарисовано, и я подумал… что там может быть старая аппаратура."
    th "Что-то я сомневаюсь, что он за аппаратурой туда попёрся…"
    cs "Понимаю. А дальше?"
    sh "Дальше… не помню.{w} Помню, что ходил по каким-то тоннелям, шахтам… а потом - очнулся от того, что меня Ольга Дмитриевна по лицу бьёт."
    sh "Что потом - вы знаете…{w} Может, я чем-то в шахтах надышался? Каким-нибудь газом?"
    cs "Не исключено…"
    "Она достала из шкафа растрёпанную картонную папку, подписанную \"Трофимов А.\", пролистала страницы, сплошь покрытые врачебными каракулями…"
    cs "Вот что, пионер. Все с тобой будет в порядке, но лучше тебе не переутомляться.{w} Отдыхай, гуляй, дыши свежим воздухом. Купайся."
    cs "И поменьше торчи в своём клубе. У вас там от канифоли не продохнёшь, и здоровому-то поплохеет, а тебе сейчас это вообще противопоказано."
    "Шурик печально вздохнул."
    sh "Но мы тогда… Мы хотели робота доделать… А то не успеем до конца смены."
    cs "Здоровье важнее!{w} Все, кыш отсюда оба."
    "Шурик торопливо выскочил за дверь, завязывая галстук на ходу. Я не спеша последовал за ним."
    window hide
    play sound sfx_open_door_1
    scene bg ext_aidpost_day with dissolve
    play ambience ambience_camp_entrance_day fadein 3
    window show
    th "Да уж. От такого объёма информации голова скоро взорвётся."
    "Предписание Виолы, насчёт отдыха и купания, показалось мне весьма заманчивым. Почему бы и нет?"
    #
    jump alt_day6_true_beach
