label alt_day6_uvao_nuke:
    window hide
    $ renpy.pause(1)
    scene bg ext_house_of_mt_sunset with dissolve2
    window show
    stop sound_loop fadeout 7
    stop music fadeout 5
    "Высунув голову из кустов, я опасливо осмотрелся по сторонам. Никого."
    "С облегчением вздохнув, я снова, как и вчера, устроился в шезлонге."
    "Тем временем в небе разгорался роскошный закатный пожар, яростное буйство красок, такое же недолговечное, как и настоящее пламя."
    "Я лежал, любуясь великолепным зрелищем, и наслаждался покоем. Веки мои становились всё тяжелее…"
    stop ambience fadeout 2
    window hide
    scene bg ext_house_of_mt_night with dissolve2
    play ambience ambience_camp_center_night fadein 3
    $ persistent.sprite_time = "night"
    $ night_time()
    window show
    "Но тут моё уединение было бесцеремонно нарушено."
    show mt angry pioneer at cleft with dissolve 
    play music music_list["always_ready"] fadein 3
    mt "Семён!!! Ты теперь каждый день будешь по вечерам в этом шезлонге валяться?"
    mt "Ну погоди, доберусь я до тебя!"
    stop music fadeout 3
    hide mt with dissolve
    "Возмущённая вожатая скрылась внутри домика."
    th "Доберётся она до меня, как же! Руки теперь коротки!"
    "Я поёрзал, устраиваясь в шезлонге поудобнее и закинул руки за голову."
    $ renpy.music.set_volume(0.3, delay=0, channel='sound_loop')
    play sound_loop sfx_tu95_bear_high fadein 5
    "Моё внимание привлекла ярко блеснувшая на фоне темнеющего востока искорка самолёта. Он летел высоко-высоко, выше облаков, оставляя за собой тонкую ниточку инверсионного следа."
    th "Интересно, а ведь я здесь за эти несколько дней ни одного самолёта не видел."
    "Лениво подумал я. С другой стороны - ну не видел и не видел.{w} Не пролегают здесь регулярные маршруты - ну и ладно. Меньше шума."
    "Солнце уже почти село за горизонт и лагерь тихо погружался в сумерки. Лишь редкие облака над головой по-прежнему были, словно светом красного прожектора, освещены солнцем."
    $ renpy.music.set_volume(0.7, delay=5, channel='sound_loop')
    "Самолёт тем временем всё тянул и тянул в сторону заката, оказавшись уже почти в самом зените. Воздух наполнило становящееся всё громче басовитое урчание, неожиданно громкое для такой большой высоты."
    show mt normal pioneer far at cleft with dissolve
    "Из домика деловито вышла Ольга Дмитриевна."
    $ renpy.music.set_volume(1.0, delay=5, channel='sound_loop')
    mt "Семён, хватит валяться! Марш умываться и спать! Отбой уже скоро."
    "Она задрала голову. Гул двигателей теперь, наверное, было слышно уже и внутри домика."
    show mt surprise pioneer with dissolve
    mt "Откуда здесь самолёт?!"
    "Судя по её изумлённому тону, почему-то она, в отличие от меня, никак не ожидала увидеть здесь самолёт, пусть и случайный."
    "В этот момент в вышине неба что-то изменилось. Позади самолёта, быстро отставая, затрепетал кажущийся крошечным отсюда язычок пламени. Хотя нет, просто какой-то ярко блестящий в солнечных лучах предмет…"
    "Покосившись на вожатую я заметил, что она так и стоит, неудобно запрокинув голову и напряжённо вглядываясь в небо."
    "Через несколько секунд нечто, отделившеся от самолёта и теперь уже окончательно от него отставшее, начало вдруг расти в размерах.{w} Всё больше, больше…"
    "Наконец я понял, что это большой парашют… нет, даже связка из трёх больших… огромных парашютов, кажущихся багровыми в лучах заката."
    "Не меняя курса, самолёт всё с той же кажущейся неторопливостью отправился догонять совсем уже ушедшее за горизонт солнце."
    $ renpy.music.set_volume(0.5, delay=10, channel='sound_loop')
    "На глазах небо менялось. От прежнего пурпура не осталось и следа, облака подёрнулись пеплом, и вечер готовился по-южному быстро уступить место ночи."
    show mt fear pioneer with dissolve
    "Рядом вдруг охнула Ольга.{w} Посмотрев на неё, я разглядел, несмотря на сгустившиеся сумерки, выражение откровенного ужаса на её лице."
    "Встретив мой взгляд, она попыталась что-то сказать, но лишь мучительно всхлипнула."
    me "Ольга… Ольга Дмитриевна, что случилось?! Что с Вами?"
    "Не отвечая, она лишь отчаянно замотала головой.{w} Наконец она с трудом выдавила из себя:"
    show mt cry pioneer with dissolve
    mt "Не могу… Не могу поверить…{w=1.0} Здесь же… Как они…"
    "Не договорив, она спрятала лицо в ладонях."
    $ renpy.music.set_volume(0.2, delay=10, channel='sound_loop')
    "Ничего не понимая, я заворочался, пытаясь вылезти из шезлонга. Это движение словно подтолкнуло её."
    hide mt with dissolve
    "Бросив на меня странный взгляд, Ольга вдруг бросилась бежать, не разбирая дороги, куда-то в сторону столовой."
    play sound sfx_tree_branches
    "Через несколько секунд треск ломаемых ветвей затих и наступила тишина, разбавляемая лишь быстро слабеющим гудением самолёта."
    th "Куда это ей так приспичило?"
    dreamgirl "Да уж наверное не в столовую.{w} В спортзал она побежала. Может и успеет ещё. Минут пять у неё есть, наверное."
    "Внутренний голос прозвучал как-то очень спокойно и немного устало."
    th "Подожди, ты что-то понимаешь в происходящем? Что-то знаешь?"
    "Ответ Шизы был всё таким же непривычно спокойным:"
    dreamgirl "Ну и бестолковый же ты. Я ведь знаю ровно то же, что и ты, не больше и не меньше."
    dreamgirl "Ну напряги память, постарайся. Ты же совсем недавно от скуки читал статью на рукипедии.{w} Самолёт с характерным очень громким гулом, турбовинтовой… Натовцы ещё его \"медведем\" прозвали…"
    "В глазах потемнело и я упал обратно в шезлонг."
    dreamgirl "Ну вот, теперь и ты всё понял."
    "Обливаясь холодным потом, я забарахтался, пытаясь встать."
    dreamgirl "Да не суетись ты. Даже сумей ты добежать до того бункера под старым корпусом, всё равно тебе это мало помогло бы."
    dreamgirl "Так что не парься. Вожатая-то быстро всё поняла и побежала прощаться.{w} А тебе и бежать не к кому, не соблаговолил обзавестись."
    "Отстранённо-равнодушный голос помог мне справиться с обезумевшим от животного ужаса телом. Заставив себя откинуться в шезлонге, я словно зачарованный уставился на повисшую надо мной Смерть."
    th "Странно, неужели сейчас всё и закончится? Вот так вот просто, без объяснений."
    dreamgirl "Ты знаешь,"
    scene cg d6_nuke_hell with flash
    window hide
    stop music fadeout 0
    stop ambience fadeout 0
    stop sound fadeout 0
    stop sound_loop fadeout 0
    $ renpy.music.set_volume(1.0, delay=1, channel='sound_loop')
    $ renpy.pause(7, hard=True)
