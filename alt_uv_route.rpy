init:
    $ alt_uvao_D4_viola_morning = False
    
label alt_day4_start_uvao:
    window hide
    $ routetag = "uv_unknown"
    $ alt_chapter_uv(4, u"Утро")
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    scene bg int_house_of_mt_sunset
    play ambience ambience_int_cabin_day fadein 4
    window show
    "Проснулся я сам, спасая собственное сознание от какого-то дурацкого сна. Что-то про запчасти от когтистой перчатки… {w}Бред, короче. Впрочем, немудрено, после вчерашних-то событий."
    th "Так, будем рассуждать последовательно."
    th "Вчерашняя ушастая тень. Могла она мне привидеться?{w} Могла. Других свидетелей поблизости не было, подтвердить некому."
    dreamgirl "А я?"
    th "Если я привлеку тебя как свидетеля, это обеспечит мне путевку в веселый дом еще надежнее, чем кошастые галлюцинации."
    th "Как проверить, глючит меня или нет? Очевидное решение - наведаться к Виоле."
    dreamgirl "О, йа-йа, доктор Виола пропишет тебе {i}процедуры{/i}, после которых тебя перестанут волновать воображаемые девочки!"
    "Я внутренне содрогнулся - воспоминания о манере Виолы проводить обследования были еще свежи."
    th "Какие еще варианты?"
    dreamgirl "Например, поспрашивать местных, вдруг они все в курсе про эту местную достопримечательность? Просто избегают про нее упоминать.{w} Почему-то."
    th "Может быть. Тут все может быть."
    play music music_list["my_daily_life"] fadein 3
    "Я сел в кровати и принялся одеваться."
    play sound sfx_open_dooor_campus_1
    "Скрипнула дверь, и в домик вошла Ольга Дмитриевна."
    show mt normal panama pioneer with dissolve
    mt "Семен? Уже проснулся?{w} Хорошо, умойся, причешись, и марш на линейку! Сегодня все должны примерно выглядеть.{w} Включая тебя!"
    me "А по какому поводу?"
    show mt angry panama pioneer with dspr
    mt "Родительский день! Я же говорила вчера на линейке. Опять все мимо ушей пропускаешь?"
    me "А, ну да. Забыл."
    mt "То-то же!"
    "Оставив последнее слово за собой, вожатая вышла за дверь, позволяя мне одеться."
    hide mt with dissolve
    window hide
    $ renpy.pause(3)
    scene bg ext_house_of_mt_sunset with dissolve
    play ambience ambience_camp_center_evening fadein 6
    window show
    "Я обнаружил вожатую неподалёку от домика - она терпеливо дожидалась, пока некий нерадивый пионер соизволит подняться и одеться."
    show mt normal panama pioneer with dissolve
    "Тут мне в голову пришла одна мысль, которую я не мог не проверить."
    th "Родительский день, значит? Интересно, а что она ответит на это…"
    me "А мои родители не приехали?"
    show mt smile panama pioneer with dspr
    mt "Приезжали рано с утра, оставили тебе мешок гостинцев, и уехали обратно."
    me "Мешок? А где он?"
    mt "В вожатской остался, не буду же я с ним по всему лагерю бегать! После обеда выдам, если хулиганить не будешь."
    show mt normal panama pioneer with dspr
    mt "А вообще - хватит болтовни, давай бегом к умывальникам и на площадь!"
    th "Пожалуй, между линейкой и медпунктом я выберу медпункт."
    me "Ольга Дмитриевна, можно я сначала в медпункт загляну?"
    show mt surprise panama pioneer with dspr
    mt "А что такое?"
    me "Да что-то странности какие-то со мной творятся, со вчерашнего дня. Видения…"
    show mt normal panama pioneer with dspr
    mt "Ну-ка, давай выкладывай, что стряслось? Что еще за видения?"
    menu:
        "Рассказать про тень":
            th "А почему бы и не рассказать? Закончится-то все равно отправкой к Виоле."
            me "Помните, у меня вчера живот прихватило?"
            show mt sad panama pioneer with dspr
            mt "Конечно, помню. Как раз на мой танец!"
            dreamgirl "Ее это так цепляет? Пользуешься успехом, Семен! Но не ты ее танцевал, не тебе ее ужинать."
            me "Так вот, я потом увидел, что около столовой кто-то ходит.{w} Подошел поближе, думал, Алиса чем-нибудь поживиться решила."
            show mt normal panama pioneer with dspr
            me "Смотрю – а там девочка незнакомая. {w}С кошачьими ушами и хвостом. Как-то так…"
            play music music_list["awakening_power"] fadein 0
            show mt angry panama pioneer with dspr
            dreamgirl "Беги, Форест, беги! Судя по ее виду, тебя сейчас будут съедать заживо!"
            mt "Что за чушь? Семен, ты издеваешься?! Если решил прогулять линейку и зарядку таким образом – не выйдет!"
            mt "Или ты меня разыгрываешь, или тебя разыграли, а ты и рад поддаться!" 
            mt "Так, никакого медпункта, марш умываться! И чтобы я больше такой чепухи не слышала!"
            "Волной ее негодования меня просто снесло в направлении умывальников."
            hide mt with moveoutleft
            "А Ольга Дмитриевна с недовольным и озабоченным видом направилась в сторону площади."
            stop music fadeout 5
            window hide
            jump alt_day4_uvao_morning_wash
        "Не рассказывать":
            th "Что-то не нравится мне такой интерес к моим личным глюкам со стороны вожатой…  Отдаст еще потом в руки карательной психиатрии."
            th "Виола в день моего появления показала себя гораздо более компетентным специалистом в вопросах оценки состояния психики."
            me "Да так, в глазах периодически плывет… Ну и в таком же духе. {w}Наверное, действительно перегрелся."
            show mt normal panama pioneer with dspr
            "Ольга Дмитриевна, услышав столь банальное пояснение, сразу потеряла интерес к деталям."
            mt "Тогда к Виоле загляни обязательно." 
            mt "На зарядку можешь не идти, а то такими темпами в медпункт тебя придется нести, и до конца смены там и пропишешься."
            "Я кивнул и отправился к медпункту."
            hide mt with dissolve
            window hide
            jump alt_day4_uvao_aidpost
            
label alt_day4_uvao_morning_wash: #умываемся, беседуем с Элом про кошочку
    scene bg ext_houses_sunset with dissolve
    window show
    th "Может, действительно показалось?"
    dreamgirl "Ты знаешь, когда так кажется – тут уже и креститься бесполезно, друг мой."
    th "Ну или действительно разыграли…"
    dreamgirl "Кто, Алиска? Ты же ясно видел, фигура не та!{w} На ДваЧе не похоже, а остальные были на дискотеке… вроде бы.{w} Да и Ольга Дмитриевна как-то слишком уж взбеленилась на такую историю…"
    th "Но ведь получается как-то криво. Теория заговора и некодевочка из инкубатора? Пошло, заезженно и бывает только в аниме."
    window hide
    scene bg ext_washstand_day with dissolve
    window show
    "Я поймал себя на том, что уже давно дошел до умывальников и стою, сосредоточенно пялясь куда-то в сторону кустов." 
    "Остатки мыслей из головы выбило хлопком по плечу."
    show el smile pioneer with dissolve
    el "Чего стоишь, как будто рыжих высматриваешь? Они уже к площади идут, не бойся!"
    "Голос Электроника был настолько жизнерадостным и так контрастировал с моим утром, что хотелось его ударить."
    dreamgirl "Может, ты и ему расскажешь? Вдруг он видел, чьими стараниями тебе Ольга Дмитриевна против шерсти проехалась?"
    th "А почему бы и да? Хуже явно не будет. К тому же кошкодевочка – это не какой-нибудь многоцветный морфирующий конь."
    me "Да вожатая пропесочила…"
    me "Представляешь, я вчера около столовой во время дискотеки девочку увидел с кошачьими ушами и хвостом, думал, галлюцинации.{w} Рассказал Ольге Дмитриевне, а она на меня вызверилась так, словно я ее лично оскорбил.{w} Вот теперь обтекаю."
    show el normal pioneer with dspr
    el "Да, не повезло…{w} А вообще радуйся – легенду местную увидел."
    el "У меня друг позапрошлым летом тут отдыхал, рассказывал, что это что-то вроде местной лагерной легенды, мол, ходит иногда по лагерю девочка-кошка.{w} Сладости иногда ворует, другую мелочевку…" 
    el "Вроде даже ее кто-то видит периодически. Может, и ты сподобился.{w} Ну или действительно тебя разыграл кто-то, всякое бывает. Ты не скисай, главное!"
    "Я угрюмо кивнул и направился к умывальнику. Пытка холодной водой – действо неприятное, но неминуемое."
    window hide
    $ persistent.sprite_time = "day"
    scene bg ext_washstand2_day
    with dissolve
    play sound sfx_open_water_sink
    $ renpy.pause(1)
    play sound_loop sfx_water_sink_stream
    window show
    "От холодной воды ломило челюсть, руки немного онемели, но настроение явно стало лучше, да и спать уже не хотелось. Теперь надо бы держать курс на площадь, а то вожатая с меня три шкуры спустит."
    stop sound_loop
    window hide
    scene cg d2_lineup
    window show
    "Линейка и зарядка прошли в уже привычном ключе."
    "Славя оправдывала свой статус помощницы вожатой на все сто, остальные с разной периодичностью зевали и поглядывали в сторону столовой."
    play music eat_horn fadein 3
    "Вот и долгожданные звуки горна!"
    stop music fadeout 5
    window hide
    jump alt_day4_uvao_breakfast

label alt_day4_uvao_aidpost: #беседуем с Виолой
    scene bg ext_square_sunset with dissolve
    stop music fadeout 3
    $ renpy.pause(1)
    scene bg ext_aidpost_day with dissolve
    play sound sfx_knock_door7_polite
    $ renpy.pause(2)
    window show
    cs "Да-да, войдите!"
    "Я поежился, вспомнив  предыдущую встречу с медсестрой, но отступать было некуда."
    window hide
    scene bg int_aidpost_day with dissolve
    play ambience ambience_camp_center_day fadein 3
    window show
    show cs normal with dissolve
    me "Виолетта Церновна, а я к вам с проблемой."
    play music music_list["eternal_longing"] fadein 5
    show cs smile with dspr
    cs "Просто Виола, пионер, для тебя – просто Виола…"
    dreamgirl "Если ты к ней по делу – не красней и взгляд подними выше выреза на халате, дубина! Иначе твое дело ширинку скоро рвать начнет!"
    "Я собрался с мыслями и начал объяснять."
    me "Эм-м-м… Тут так получилось… Вопрос достаточно деликатный…"
    show cs grin with dspr
    cs "В ящике стола, бери, сколько надо."
    "Под взглядом медсестры я подошел к столу, открыл ящик и увидел горку квадратных бумажных пакетиков с надписью «Изделие №2». Лицо Виолы выражало исключительно дружелюбное участие."
    show cs normal with dspr
    me "Нет-нет, не в этом дело! Я… Мне… В общем, у меня, судя по всему, галлюцинации!"
    cs "Ну, раз ты сам пришел за медицинской помощью, значит, не все еще потеряно. И что же тебе мерещилось?"
    me "Ну, вчера во время дискотеки мне живот прихватило, я и ушел…{w} А потом увидел, что около столовой тень какая-то, думал, Алиса шастает, присмотрелся – а у нее уши кошачьи. И хвост.{w} У тени, а не у Алисы, Алисы там вообще не было.{w} Я подумал — нарядился кто-то, а оно ухом дёрнуло.{w} Вот."
    th "Идиот. Полный и беспросветный. Сейчас тебя накормят галоперидолом, потом воткнут в задницу шприц с аминазином, чтобы не особо дергался, и отправят в комнату с мягкими стенами."
    "Как ни странно, Виола не выказывала никакой тревоги и волнения, скорее на ее лице читался вполне себе явный интерес."
    $ renpy.music.set_volume(0.5, delay=5, channel='music')
    cs "Девочку-кошку? Не ты первый, пионер, не ты последний… В твоем возрасте это нормально, фантазии и не такие бывают…{w} но в данном случае ты столкнулся с настоящей легендой. Не знаю, почему Ольга…"
    me "Ольга Дмитриевна? А она здесь при чём?"
    cs "Когда мы в «Совенке» работать начали, эта легенда только появилась. Ольге её подопечные все уши прожужжать успели, теперь из-за каждого упоминания бесится. А потом как-то поутихли слухи понемногу."
    me "И давно вы тут? А что за легенда?"
    show cs shy with dspr
    cs "Пионер, посмотри на меня внимательно и подумай, насколько невежлив был твой первый вопрос. Я выгляжу настолько старой, чтобы это было давно?"
    "Я нечленораздельно квакнул и отрицательно мотнул головой."
    show cs normal with dspr
    cs "То-то же. А легенды как таковой нет, просто начали рассказывать, что видели девочку с кошачьими ушами и хвостом. Еще периодически из столовой всякая всячина пропадает, но тут уж скорее пионеры под шумок действуют.{w} Так говоришь, где ты ее видел?"
    me "Около столовой…"
    cs "И ты уверен, что это не Алиса была?"
    me "Не, там фигура… Не Алиса, короче."
    "В глазах Виолы блеснул интерес."
    cs "Знаешь, пионер… Ты смотри, если её еще где-нибудь заметишь – заходи, рассказывай, я буду {i}очень благодарна.{/i}"
    $ alt_uvao_D4_viola_morning = True
    "Последние слова в исполнении Виолы чисто автоматически трактовались весьма интересным, но несбыточным образом. В горле снова пересохло, но в этот раз я сумел перебороть себя."
    me "Надеюсь, благодарность будет выражена… {i}интересным образом.{/i}"
    dreamgirl "Так держать! Еще бы на полтона ниже – и звучало бы уже совсем намеком!"
    stop music fadeout 3
    show cs smile with dspr
    cs "А ты весьма неплохо держишься… для школьника."
    $ renpy.music.set_volume(1, delay=0, channel='music')
    play music eat_horn fadein 2
    "Нашу беседу прервал горн. Виола направилась к двери."
    stop music fadeout 2
    cs "Завтракать идешь, пионер? Ольге я скажу, что ты в полном порядке."
    "Я кивнул и направился к столовой."
    hide cs with dissolve
    window hide
    jump alt_day4_uvao_breakfast

label alt_day4_uvao_breakfast:
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_sunset  with fade
    window show
    "Свободных мест в столовой, как всегда, не хватало." 
    "Детальный осмотр местности показал, что сесть я могу либо с рыжим дуэтом, от близости с которым всех предостерегал здравый смысл, либо с нашей доблестной девочкой-пулеметом восточного происхождения."
    dreamgirl "Ну что, путь смертника или путь стоика?"
    th "Лучше путь стоика. Мне как раз надо крепко обдумать пару вопросов, под болтовню это явно проще, чем под риск съесть кого-нибудь не того."
    show mi normal pioneer with dspr
    "Я сел рядом с Мику, улыбнулся и начал разбираться с содержимым тарелки. Она что-то тараторила, я кивал и погружался все глубже в размышления.{w} Вернее, в пустоту, из которой должны были родиться хоть какие-то мысли."
    th "Я тут уже четвертый день, а все еще забиваю на самые важные вопросы.{w} Для начала – текущая дата, местоположение лагеря и памятник на площади, при первом же вопросе о котором Лена только что не отпрыгнула."
    dreamgirl "Может, она тебя за начинающего пикап-мастера приняла. Мол, начал тут один шаблоны рвать. А они-с барышни стеснительные и таких подкатов не понимают, у них Митчелл и романтика."
    th "Ша, о важном думать надо. На реконструкцию это не тянет – не поверю, что столько народа так старательно будут косить под пионерлагерь и добровольно откажутся от своих девайсов."
    th "Да и как я в таком случае здесь оказался? К тому же из зимы в лето…{w} Вариантов всего два – путешествие во времени и путешествие в параллельный мир.{w} И в том, и в другом случае, раз меня не встречают с цветами или наручниками, разгребаться придется самому."
    show mi serious pioneer with dspr
    "Я заметил, что Мику встревоженно смотрит на меня, и только тогда обнаружил, что в очередной раз подношу ко рту пустую ложку."
    me "Извини, задумался!"
    show mi smile pioneer with dspr
    "Мику рассмеялась с облегчением."
    mi "Ну, Сёма, ты даешь, так задуматься. Я уж думала, тарелку процарапаешь или ложку съешь! А о чем ты таком задумался? Не выспался, что ли? Я вот если не высплюсь, тоже могу вот так вот задуматься, почти сплю тогда на ходу даже. Или что-то случилось?"
    mi "Ой, ты знаешь, Шурик вчера с крыши упал, ушибся сильно, в медпункте лежит. Но вроде бы ничего страшного. А к тебе сегодня родители приедут?" 
    "Очередной поток болтовни практически смыл все попытки определиться с планом действий. Я отрицательно мотнул головой на ее расспросы и молча отправился в сторону выхода."
    mi "… заходи в клуб, если что! Вдруг скучно будет!"
    "догнал меня голос Мику."
    hide mi with dspr
    stop ambience fadeout 3
    window hide
    $ renpy.pause(2)
    $ persistent.sprite_time = "day"
    $ day_time()
    scene bg ext_dining_hall_near_day
    play ambience ambience_camp_center_day fadein 3
    play music music_list["my_daily_life"] fadein 3
    window show
    th "Где разжиться более-менее актуальной информацией о происходящем, не вызывая подозрений?{w} Правильно, в интернете. Чем заменить при отсутствии?{w} Телевидением и газетами."
    th "Насчет ти-ви не знаю, а вот газеты… Надо бы наведаться в библиотеку."
    dreamgirl "Ну наконец-то ты пошевелил мозгами хоть как-то.{w} Попроси подшивку журналов каких-нибудь, энциклопедию, что-нибудь еще…"
    th "А ведь действительно! Попрошу, к примеру, прошлогоднего «Юного техника» - вот и год вычислил, и не спалился. Да и вообще.{w} Только Жужелица…"
    dreamgirl "А ты ее палкой по голове тюкни – она мешать и не будет.{w} Можешь даже воспользоваться беспомощностью девушки в своих мерзких целях, разрешаю.{w} Нарисовать на ней что-нибудь, например…"
    stop music fadeout 0
    play sound2 sfx_bodyfall_1 
    show mt surprise panama pioneer close at center with hpunch
    "Разговаривая с Шизой, я перестал следить за дорогой и буквально врезался в Ольгу Дмитриевну."
#
    jump alt_day4_uvao_garlands_removal

label alt_day4_uvao_garlands_removal_debug:
# Д4-Утро-Уборка гирлянд
#

label alt_day4_uvao_garlands_removal:
    me "Извините…"
    show mt normal panama pioneer with dissolve
    mt "О, Семён! Ты-то мне и нужен."
    "Этот тон мне сразу не понравился."
    me "Очередное архиважное поручение?"
    show mt smile panama pioneer with dspr
    mt "Как ты догадался?"
    me "Если подумать, то создается впечатление, что я вам нужен только в качестве чернорабочего."
    show mt laugh panama pioneer with dspr
    "Ольга Дмитриевна рассмеялась."
    mt "Не паясничай. Тебе наверняка все равно нечем заняться."
    "На клеточном уровне я очень-очень занят!"
    show mt normal panama pioneer with dspr
    mt "Так вот, на линейке видел состояние нашей площади?"
    me "А что, красиво. Особенно вчера было."
    "Я понял, куда она клонит. Кажется, поиск ответов придется отложить."
    "В принципе я мог бы обойтись и без свидания с Жужелицей, но если хочется понять, что здесь происходит - придется рисковать."
    mt "Как ты уже, наверное, понял, требуется убрать площадь, снять гирлянды и отнести их на склад."
    me "Вы всерьез думаете, что один я справлюсь?"
    mt "Можешь попросить помощи у Электроника. Тем более стремянка как раз была у них."
    th "Интересно, а не та ли эта стремянка, по которой Шурик забирался?"
    me "Но…"
    show mt angry panama pioneer with dspr
    mt "Никаких но, Семён! Пионеры говорят «Всегда готов!» и не задают лишних вопросов."
    me "Да, белая госпожа…"
    "Пробубнил я себе под нос.{w} К счастью, Ольга Дмитриевна меня не услышала."
    show mt normal panama pioneer with dspr
    mt "В общем, задание получил, вперед выполнять."
    show mt smile panama pioneer with dspr
    "Она улыбнулась и твердой походкой направилась в сторону домика."
    #Я вспоминаю, тебя вспоминаю! А радость шальная легла как заряяяяя! Летящей походкой! Ты вышла из мая! Потом из июля и фееевраляяяя!
    hide mt with dissolve
    dreamgirl "Ну что, работай, нигер! Солнце еще высоко!"
    "Что же, уборку без стремянки не начать. Я направился в сторону кружка кибернетиков."
    window hide
    $ alt_chapter_uv(4, u"Всегда готов!")
    play music music_list["my_daily_life"] fadein 3
    scene bg ext_clubs_day with dissolve
    window show
    "Я постучал и дернул было ручку, но дверь оказалась заперта."
    th "Где это Электроника носит? Шурика, что ли, навещает?{w} Ладно, подожду его тут."
    show us grin sport far with dissolve:
        xalign 0.999 yalign 0.999 zoom 0.7
    "Объявился тот спустя двадцать минут, я же за это время успел заметить в кустах рыжую шевелюру местной «охотницы на жуков»."
    "Сейчас мне было ужасно жаль того, кому Ульяна захочет похвастаться своей коллекцией."
    hide us with dissolve
    me "Явился - не запылился."
    show el surprise pioneer with dissolve
    el "Привет Семён, а я это…"
    "Электроник что-то спрятал за спину."
    "Я ловко выхватил у него небольшую стопку журналов.{w}\n«Юный техник». А я уж подумал - «Playboy»…"
    show el serious pioneer with dspr
    el "Это Шурику. Скучно ему в медпункте, просил принести, вот я в библиотеку и сходил."
    "Ясно, чем занимался Электроник все эти двадцать минут. Кажется, Жужелица сегодня будет более нервной, чем обычно."
#
    show el sad pioneer with dspr
    el "Да, ты же не знаешь, наверное? Шурик вчера с крыши навернулся…"
    me "Слышал уже, слышал!"
    "Перебил я нетерпеливо.{w} Выслушивать эпическое повествование о падении с крыши у меня особого желания не было."
    me "Как у него дела?{w} Что-нибудь серьёзное?"
    el "Нет, как ни странно, по мелочи только - ушибы да ссадины. Повезло, одним словом."
    el "Виолета мне сказала, что завтра его отпустит, наверное."
    "При упоминании о медсестре Эл заметно покраснел.{w} Похоже, она не только надо мной одним любит мило пошутить…"
    me "Ладно, всё это очень хорошо, но я не для того тебя здесь ждал."
#
    me "Помощь твоя нужна."
    show el normal pioneer with dspr
    el "В чем же?"
    "Тот явно заинтересовался."
    me "Ольга Дмитриевна велела убрать гирлянды, а стремянка, кажется, у вас."
    el "Да, она тут."
    hide el with easeoutright
    "Тот оббежал здание клубов, чем-то поскрипел и выволок сложенную жестяную стремянку."
    show el normal pioneer with easeinright
    me "Отлично, пошли."
    show el upset pioneer with dspr
    el "Ну, вообще-то у меня дела."
    "Он покраснел."
    th "Опять в библиотеку собрался?"
    dreamgirl "Причем, явно не за журнальчиками… А что, руководитель на больничном, подчиненные развлекаются как умеют."
    me "Э, нет. Один я ни за что не полезу. Ольга Дмитриевна сказала, что ты можешь помочь."
    el "Ну, если надо…"
    me "Не представляешь как."
    show el normal pioneer with dspr
    "Мы направились к площади."
    window hide
    scene bg ext_houses_day
    with dissolve
    $ renpy.pause(3)
    show el normal pioneer at left with dissolve
    window show
    "Эл, кряхтя, нес стремянку, а я осматривал лагерь.{w} В голову приходили разные мысли о путешествиях во времени, параллельных мирах, экспериментах ученых…"
    show el sad pioneer with dspr
    el "Давай передохнем?"
    me "Выдохся уже? Давай сюда."
    "Я схватил стремянку и понес ее к площади."
    window hide
    scene bg ext_square_day
    with dissolve
    show el sad pioneer at left with dissolve
    window show
    "Вскоре показалась площадь, с ее лавочками, украшенными гирляндами деревьями и с ее основным атрибутом – одиноким загадочным Гендой."
    "Тот как всегда надменно взирал на происходящее вокруг.{w} На воркующую парочку, сидящую на скамейке в тени деревьев, на Славю, машущую метлой, на меня, медленно несущего стремянку."
    "Я с той же надменностью взглянул в его очки."
    th "Ну, погоди, неизвестный товарищ. Мы еще узнаем твои тайны."
    dreamgirl "Мда-а. Натуральные Авгиевы конюшни!"
    "Действительно, работы оказался практически непочатый край."
    "Я прикинул примерный вектор работы и поставил стремянку у одного из крайних деревьев."
    me "Кто полезет?"
    show el upset pioneer with dspr
    el "Э нет, только не я."
    me "Мне тоже как-то не очень хочется."
    show el normal pioneer with dspr
    "Никто из нас не хотел повторять подвиг Шурика, посему было принято решение решить вопрос «по-мужски»."
    "…"
#    play music music_list["my_daily_life"] fadein 3
#здесь можно сделать мини-развилку (Лезет Сёма/Лезет Эл)
    show el smile pioneer with dspr
    el "Ножницы режут бумагу. Я победил."
    th "Ничего не остаётся…"
    me "Держи, только крепче!"
    show el normal pioneer with dspr
    "Эл кивнул и схватился за стремянку."
    show sl smile pioneer far at fright with dissolve
    "Работа пошла."
    "Славя с улыбкой наблюдала за нашей деятельностью, пристроив на лавочке рядом с собой метлу."
    "Ну да, на что там можно смотреть вечно?" 
    me "Эл, держи крепче!" with hpunch
    show el sad pioneer
    show sl normal pioneer far at fright
    with dspr
    el "Прости, отвлекся."
    show mz bukal glasses pioneer far at right with dissolve
    "Я взглянул за плечо и заметил как Славя о чем-то разговаривает с Жужелицей."
    show el smile pioneer with dspr
    "Эл, как и ожидалось, смотрел туда же."
    "Женя, что-то сказала, покосилась в нашу сторону и, развернувшись, пошла в сторону библиотеки.{w} Отлично, значит, она будет там."
    hide mz with dissolve 
    play sound sfx_tree_branches
    with hpunch
    "Стремянка громко скрипнула, я от испуга выронил моток гирлянды и схватился за ветку дерева." 
    me "Эл!!!"
    show el upset pioneer with dspr
    el "Извини!"
    show el sad pioneer with dspr
    "Электроник тут же крепко сжал стремянку."
    "Спустя полчаса последняя гирлянда была смотана и брошена в общую кучу."
    show el normal pioneer
    show sl normal pioneer at right
    with dissolve
    sl "Вам помочь, мальчики?"
    me "Если не трудно."
    show sl happy pioneer with dspr
    "Она лучезарно улыбнулась."
    sl "Нет, не трудно. Как раз с площадью закончила."
    show sl smile pioneer with dspr
    "Я схватил большой моток проводов и понес его в сторону склада."
    "Эл, помедлив, схватил стремянку и пошел за мной. Славя взяла остатки гирлянд."
    window hide
    scene bg ext_houses_day
    with dissolve
    show el sad pioneer at right
    show sl normal pioneer at left
    with dissolve
    window show
    el "Аккуратнее, Семён. Не упади."
    $ renpy.pause(1)
    "Нести моток было неудобно, и провода то и дело попадались под ноги. С горем пополам мы дошли-таки до склада."
    window hide
    scene bg ext_shed_day
    with dissolve
    play ambience ambience_camp_entrance_day fadein 3
    $ renpy.pause(3)
    show el sad pioneer at right
    show sl normal pioneer at left
    with dissolve
    window show
    sl "Одну минутку, мальчики."
    show sl normal pioneer far at left
    show el normal pioneer
    with dspr
    "Она подбежала к дверям, звякнула ключами и ловко открыла навесной замок."
    hide sl with dissolve
    sl "Загружай!"
    "Донесся голос из помещения."
    "Я поволок гирлянды внутрь."
    stop ambience fadeout 0
    window hide
    scene int_shed_day with dissolve
    $ persistent.sprite_time = "sunset"
    $ renpy.pause(2)
    show sl normal pioneer far at left with dissolve
    window show
    me "Куда?"
    sl "В коробку, вот сюда."
    "В помещении было темно и Славю было едва видно."
    "Я бросил большой моток туда, куда указал Славин силуэт и наконец разогнулся."
    me "Ох! Как будто вагон разгрузил!"
    show sl happy pioneer far with dspr
    "Славя хихикнула."
    show el normal pioneer far at right
    show sl smile pioneer far
    with dspr
    "Рядом раздался скрип стремянки. Электроник поставил железяку в другой угол и теперь отдыхал, прислонившись к стене."
    me "Что ж, мавр сделал свое дело, мавр может уходить. Спасибо за помощь."
    show el smile pioneer far
    show sl smile pioneer far
    with dspr
    "Эл кивнул, а Славя улыбнулась."
    sl "Обращайтесь."
    window hide
    scene bg ext_shed_day
    play ambience ambience_camp_entrance_day fadein 3
    with dissolve
    $ persistent.sprite_time = "day"
    window show
    "После темноты склада солнце было необычайно ярким."
    show el normal pioneer far at right with dissolve
    "Пока я привыкал к свету, мимо меня проскользнул Эл и с деловым видом направился куда-то в сторону сцены."
    hide el with easeoutleft
    "Я же присел на крыльцо перевести дух. Шутка ли, битых два часа корячиться на верхотуре! Руки ощутимо гудели, затекшая шея ныла."
    show sl normal pioneer far at left with dissolve
    "Тем временем Славя заперла дверь и, мимолетно улыбнувшись мне, побежала куда-то по своим делам. Активистка!"
    hide sl with easeoutright
    dreamgirl "Ну что, передохнул? Ты вроде собирался приобщиться к знаниям."
    stop music fadeout 3
    "Кряхтя, я поднялся и направился к хранителю местных архивов, в надежде откопать побольше информации о мире, куда меня занесло."
    stop ambience fadeout 1
    window hide
    jump alt_day4_uvao_library
#

label alt_day4_uvao_library:
#Д4 посещение библиотеки
#
#
    window hide
    $ alt_chapter_uv(4, u"Повелитель книг")
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day fadein 1
    scene bg ext_library_day with fade
    window show
    th "Все, пора с этим заканчивать, а то мне уже начинает казаться, что сами Высшие Силы не хотят, чтобы я узнал правду…"
    dreamgirl "Впервые на арене! Смертельный номер!{w} Укрощение безумной библиотекарши! Эх, жаль, похрустеть нечем…"
    th "Спасибо за поддержку."
    dreamgirl "Обращайся!"
    "Остановившись возле двери, я несколько раз глубоко вздохнул."
    th "Знал бы, что понадобится ее помощь, был бы хоть немного повежливее…"
    dreamgirl "Не думаю. Бешеный лев обычно хреново реагирует на комплименты…"
    "Я собрался с мыслями и повернул ручку."
    play sound sfx_door_squeak_light
    $ renpy.pause(2)
    window hide
    scene bg int_library_day with dissolve
    $ persistent.sprite_time = "day"
    play ambience ambience_library_day fadein 3
    window show
    "Шаг, другой.{w} Я еще жив? И на меня даже не наорали? Удивительно!"
    "Уютное гнездышко библиотекарши пустовало. Где же хранитель сего места?"
    "Меня напугал внезапный крик из-за соседнего стеллажа:"
#    play sound sfx_angry_ulyana
    play music music_list["glimmering_coals"] fadein 5
    mz "Да сколько можно? Двадцатый раз уже! Зачем сюда приходишь? Ты же их уже до последней буквы знаешь."
    el "Жень, я… Это…"
    th "Вот он куда так торопился, оказывается."
    "Голос Электроника немного дрожал, и я даже немного его понимал."
    mz "Я знаю, как меня зовут! Заруби себе на своем конопатом носу…"
    "Вот это подфартило! Впервые Электроник там, где нужен. Если он еще потянет резину…"
    dreamgirl "Правильно. Пусть пираний кормит другой водолаз. Давай скорее, они же не вечно будут ворковать."
    th "Сейчас найду какую-нибудь книгу вроде «Истории СССР» или что там ещё…"
    "Тут я с огорчением понял, что до картотеки-то мне не добраться - там я как раз окажусь в поле зрения препирающейся парочки."
    th "И что же делать? Смотреть все книги подряд? Так я до вечера провожусь, даже если не прогонят."
    "Я отчаянно зашарил взглядом по аккуратным бумажным наклейкам на стеллажах."
    "Разумеется, здесь должно быть полно литературы для воспитания патриотического духа среди пионеров, в том числе и книги по истории…"
    "Одно плохо - все стеллажи с книгами подходящей тематики находились в опасной близости от злобствующей Жени."
    th "Да что ты будешь делать! Ведь застукают же…"
    dreamgirl "А ты Женю попроси помочь! И Эла."
    "Тут в глаза бросилось слово «УЧЕБНИКИ». Может быть, там что-нибудь найдётся?"
    "Я тихонько направился к стеллажу с учебниками. Не в моем положении было мешать сейчас голубкам."
    "Голос Жени немного стих, но она продолжала поносить Электроника."
    mz "Каждый день! Каждый день, ты заявляешься сюда с неизвестной целью…"
    "Нужный стеллаж."
    "«Природоведение»… Не то.{w} «Геометрия»… К чёрту геометрию!"
    "«История»! Есть!"
    "Я ухватился за зажатый между другими учебник, на корешке которого красовалось «ИСТОРИЯ СССР. 10»"
    "Значит, хоть что-то я узнаю."
    mz "…прогуливайся сам! А мне и так хорошо!"
    stop music fadeout 6
    el "Но…"
    dreamgirl "Зря парень старается. Она, кажется, почкованием размножается, не иначе…"
    "А голоса меж тем приближались. Еще чуть-чуть и накроют. И шкаф угловой, не спрячешься. Что делать? Может, в подпол?"
    dreamgirl "Какой подпол? Кто тебе будет в библиотеке делать подпол?! Давай лучше запрыгивай наверх и зависни между полками, как трёхглазый ублюдок!"
    th "…"
    window hide
    scene pioner_lib_hiding
    play sound sfx_head_heartbeat fadein 2
    "Вместо этого я пригнулся и на трех конечностях - четвертая была занята книгой - шустро забежал за стеллаж."
    play music music_list["you_won_t_let_me_down"] fadein 6
    
    
    dreamgirl "…Ну или так."
    "Стук каблуков подсказывал, что Жужелица снова заняла свое место у дверей. Электроник тащился за ней."
    mz "Разговор окончен! Иди, поищи себе кого-нибудь другого, кто согласится выгулять тебя, например Шурика своего!"
    el "Но он в медпункте…"
    mz "Да плевать, ты мне здесь не нужен! Уходи."
    play sound sfx_open_door_kick
    th "Боже, какой у нее противный голос! И кто на {i}такую{/i} поведется?"
    play sound sfx_close_door_1
    dreamgirl "Вот всякие Электроники и ведутся. С другими у него немного шансов, сам пойми."
    "Я стоял на четверенках и напрягал слух, пытаясь улучить момент для побега."
    "Женя, видимо, никуда не торопилась. Не было слышно ни шагов, ни ее скрипучего голоса.{w} Может быть она ушла?"
    dreamgirl "Не дождешься! Дрыхнет небось, как всегда, на своих книжках."
    "Не ждать же мне до обеда?"
    play sound sfx_open_door_kick
    "Внезапно скрипнула дверь, и кто-то вошел."
    mz "Это кто это там?"
    un "Женя, здравствуй, это я."
    mz "Снова ты? Что ж вы ходите-то все подряд… Уже прочитала?"
    un "Угу."
    mz "Давай тогда. Та-ак. Страницы на месте, ничего не изрисовано… Что-то еще?"
    un "Мне бы еще что-нибудь…"
    "Я почти уверен, что Лена сейчас краснее знамени с Лениным, висящего за спиной библиотекарши, хотя видно лишь ее сандалии."
    mz "Опять про любовь? А ты разве не все еще прочитала?"
    "Раздался скрип отодвигаемого стула."
    mz "И что вам так нравится в бульварных романах? Я больше предпочитаю книги по естественным наукам…"
    "Лена промолчала."
    "Шаги девочек удалялись. Судя по всему, романтике жужелица отделила место в самом дальнем углу, посему…"
    dreamgirl "Ваш выход, мсье Фишер!"
    window hide
    scene bg int_library_day with dissolve
    window show
    "Я перехватил учебник поудобнее во вспотевшей ладони и выглянул из-за шкафа. Никого."
    "Отлично, теперь главное - не спеша…"
    "Мимо стеллажа, обходим столы… Дверь."
    window hide
    play sound sfx_open_door_kick
    scene bg ext_library_day with fade
    stop music fadeout 3
    $ renpy.pause(2)
    play ambience ambience_camp_center_day fadein 1
    window show
    "Весь путь к двери я даже не дышал. Но теперь, вырвавшись из лап дракона с трофеем, я просто не мог надышаться."
    "Сладкий воздух, воздух победы…"
    "Так, теперь посмотрим, что это я добыл…"
    play sound sfx_paper_bag
    "Оглавление, зажатое почему-то между приложениями, удалось найти не сразу."
    "Так… Что тут…"
    show us smile sport with dissolve
    play music music_list["i_want_to_play"] fadein 3
    us "Семён! А ты чего это тут делаешь?"
    "Я аж подскочил, пряча книгу за спину."
    show us normal sport with dspr
    me "Ульяна! Подкрадываться к людям нехорошо."
    "Я уже описывал широкую дугу вокруг девочки."
    show us dontlike sport with dspr
    us "Ты прям как Саныч, честное слово. Хотела мяч взять, футбольный, зашла, а он дрыхнет на матах. Давай его расталкивать…"
    us "Вот он мне прям то же самое сказал!"
    me "Мяч не дал, что ли?"
    show us normal sport with dspr
    us "Дал, почему же?"
    me "Тогда почему не играешь?"
    show us sad sport with dspr
    us "Ску-учно! Алиса куда-то пропала, а с младшими отрядами играть неинтересно. Пошли сыграем, Семён?"
    me "Да я сейчас занят…"
    show us grin sport with dspr
    us "И чем же? Из библиотеки книжки тыришь?"
    "Ульяна хитро взглянула на меня…"
    us "Дай поглядеть!"
    "… и выхватила учебник, который я пытался спрятать за спиной."
    me "Эй, верни!"
    show us grin sport far at fright with dissolve
    "Ульяна отбежала на безопасное расстояние и только потом посмотрела на обложку."
    show us dontlike sport far at fright with dspr
    us "История СССР? Фе. Скукотень! Я думала про индейцев, или про шпионов… Зачем он тебе?"
    dreamgirl "Ну что, теперь думай как добычу вернуть…"
    dreamgirl "Догнать мы уже пробовали, подкупить нечем, осталось только уговорить.{w} Или припугнуть. Кого она боится?"
    me "Поручение от Ольги Дмитриевны. Кстати, ты мне поможешь!"
    show us calml sport far at fright with dspr
    us "Я?! Чем это?"
    me "Про концерт слышала? Ольга Дмитриевна велела мне найти материал для сценки.{w} Что-нибудь про пионеров-героев, например, или про революцию."
    th "Надеюсь, она не в курсе, что в учебнике для 10 класса про пионеров-героев вряд ли что-то может быть…"
    show us normal sport far at fright with dspr
    me "Вот ты и будешь пионером-героем!"
    dreamgirl "Ты соображаешь чего несешь? Она же сейчас согласится!"
    me "А я - фашистом.{w} Ми тебя будем вешайт унд убивайт!"
    show us laugh sport far at fright with dspr
    us "Не, я всех из пулемета перестреляю - тра-та-та-та!!! И никаких вешайт и убивайт!"
    me "Не пойдет, они потому и герои, что геройски погибли. Придется тебе…"
    show us dontlike sport far at fright with dspr
    us "Не-е-е, тогда не хочу!{w} Найди кого-нибудь другого, Лену, например!"
    show us normal sport far at fright with dspr
    us "Забирай свою скукотень, а я пойду, может Алиса вернулась."
    show us grin sport far at fright with dspr
    us "Заодно проверю своих… э… друзей…"
    dreamgirl "Ну вроде разобрались. И совет, дружок: есть с ней не садись, а то снова в тарелке обнаружишь ее… друзей…"
    hide us with dissolve
    "Я облегченно вздохнул и, стараясь никому не попадаться на глаза, направился к домику.{w} Лучше поглощать знания в уединеном месте."
    stop music fadeout 3
    stop ambience fadeout 1
    window hide
    scene bg int_house_of_mt_noitem_day with dissolve
    play music eat_horn fadein 5
    window show
    "Стоило только открыть дверь, как раздался обеденный горн."
    stop music fadeout 2
    "В тот же момент я, как настоящая собачка Павлова, немедленно ощутил пустоту в желудке."
    play sound sfx_stomach_growl
    th "Поглощать знания лучше на сытый желудок."
    "Спрятав книгу под подушку и стряхнув пыль знаний со штанов, я направился в столовую. "
    stop music fadeout 3
    window hide
    
    jump alt_day4_uvao_lunch
#
label alt_day4_uvao_lunch:
# Д4-обед
#
# используется флаг посещения медпункта alt_uvao_D4_viola_morning
# устанавливает флаг ухода на концерт alt_uvao_D4_concert #(временный для отладки)
# устанавливает флаг обеда с  Леной  alt_uvao_D4_lunch_un
# устанавливает флаг обеда со Славей alt_uvao_D4_lunch_sl
#
    $ alt_uvao_D4_concert = False
    $ alt_uvao_D4_lunch_un = False
    $ alt_uvao_D4_lunch_sl = False
#
    window hide
    $ alt_chapter_uv(4, u"Обед")
    $ persistent.sprite_time = "day"
    $ day_time()
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_day with fade
    window show
    "В столовой почему-то было просторнее, чем обычно."
    "То ли часть пионеров выбыла из строя, наевшись чего-то не того{w}, то ли их просто задержали на каком-то мероприятии, о котором я не знал."
    "Впрочем, не всё ли мне равно? Меньше народа - больше кислорода!{w} Главное, в столовой было достаточно свободных мест, что давало мне некоторую свободу выбора."
#
    "После акробатических упражнений на площади есть хотелось особенно сильно, но и любопытство, раззадоренное чтением статьи в библиотеке, тоже не давало покоя."
    th "Вообще-то, кто мешает мне совместить полезное с полезным? Можно подсесть к кому-нибудь и за едой попробовать ненавязчиво выведать что-нибудь о местных реалиях…"
    dreamgirl "«Ненавязчиво выведать» ты уже пробовал в первый день - у Лены. Гениально проведённая разведывательная операция, преисполненная тонким знанием психологии."
    "Не удостоив Шизу ответом, я взял с раздачи поднос и окинул взглядом столовую, выбирая, к кому бы из знакомых присоседиться."
    show mi normal pioneer far at fleft with dissolve
    "Ближе всего было свободное место рядом с Мику.{w} Определённо, никаких проблем с тем, чтобы её разговорить, не предвиделось - скорее уж наоборот."
    "Вопрос только в том, удастся ли направить фонтан её красноречия в интересующем меня направлении.{w} В своих способностях в этой области я не сомневался ни секунды{w} - однозначно, не справлюсь."
    hide mi with dissolve
    th "Тем более, что она, можно сказать, иностранка. Так ли уж много она знает про {i}эту страну{/i}. Лучше попробовать с аборигенами пообщаться."
    dreamgirl "Ну ты, барин, и задачи ставишь! Где же их здесь взять, аборигенов-то?{w} Хотя подожди, вот же как раз сидят две прелестных рыжеволосых аборигенки. Как раз и место рядом свободно!"
    show dv smile pioneer far at fright
    show us grin pioneer far at right
    with dissolve
    "Сидящие поодаль Алиса с Ульянкой уже некоторое время на меня ехидно поглядывали."
    show dv grin pioneer far at fright with dspr
    $ renpy.pause (1.5)
    show us laugh pioneer far at right with dspr
    "Заметив, что я смотрю в их сторону, Двачевская что-то быстро зашептала подруге на ухо, та прыснула и состроила мне гримасу."
    th "Вот уж не думал, что у меня, оказывается, латентная склонность к мазохизму!"
    th "Если уж собственное альтер эго предлагает мне поучаствовать в комедии «Девочки с рыжими волосами, или Тридцать три подзатыльника»!{w} Меня будут обливать борщом, кормить саранчой, давать пощёчины… Словом, унижать морально и физически. Это очень смешная комедия…"
    hide dv
    hide us
    with dissolve
    "Шиза изобразила демонический хохот, а я, рассудив, что обед в обществе сразу двух рыжих бандиток вряд ли принесёт какую-то пользу, а вот нервотрёпки избежать точно не удастся, огляделся в поисках более подходящей компании."
#
    menu:
        "Сесть с Леной":
#
# Исходим из того, что в Д2-Д3 с Леной общались мало (конвой в Д2 невозможен, а остальные выборы в её пользу не использовались/использовались мало, иначе не проходим в ЮВАО по ЛП)"
# Соответственно, до сих пор Лена свою симпатию к ГГ с ним не обсуждала, ибо не имела возможности/стеснялась"
#
            show un sad pioneer far at center with dissolve
            "Я почувствовал на себе чей-то взгляд и, обернувшись, увидел как Лена поспешно отвела глаза и уткнулась носом в тарелку."
            th "Пожалуй, стоит с ней поговорить.{w} Извинюсь за странные расспросы в первый день, скажу что неудачно пошутил, или что-то вроде этого.{w} А там, глядишь, и разговоримся."
            show un sad pioneer at center with dissolve
            "Решительно подойдя к столу, за которым она сидела, я кивнул на свободное место напротив."
            me "Ты не против, если я присяду?"
            show un surprise pioneer at center with dspr
            "Лена как-то странно глянула на меня, но тут же снова уставилась в стол и неловко кивнула, слегка при этом покраснев."
            show un shy pioneer close at center with dissolve
            "Решив принять этот жест за знак согласия, я уселся и составил с подноса тарелки."
            me "Приятного аппетита."
            "Она снова кивнула и, не поднимая глаз, беззвучно шевельнула губами."
            "Слегка обескураженный таким приёмом, я принялся за еду, периодически поглядывая на Лену."
            stop ambience fadeout 15
            play music music_list["lets_be_friends"] fadein 10
            "Та сидела, гоняя вилкой по тарелке несколько оставшихся макаронин, и упорно не глядя на меня."
            th "Странно, до этого она вела себя вполне нормально.{w} Насколько это возможно для такой стесняши, разумеется."
            th "Неужели я так сильно смутил её тогда расспросами про Генду?"
            th "Это даже и расспросами-то назвать нельзя, для неё это должно выглядеть как неумная шутка или, максимум, проявление лёгкого слабоумия, но не более того."
            "Пауза явно затягивалась и, заканчивая с супом, я наконец решил попробовать ещё раз завести светскую беседу."
            me "Э-э-э… как дела?{w} Я имею в виду, как прошло утро?"
            show un surprise pioneer close at center with dspr
            "Кажется, даже Шиза не нашлась, что сказать на это вступление."
            "Лена подняла на меня озадаченный взгляд, но быстро опомнилась и снова принялась сосредоточенно разглядывать опустевшую тарелку."
            show un shy pioneer close at center with dspr
            "Внутренне поморщившись от собственного идиотизма, я решил, тем не менее, давить до последнего:"
            me "Послушай, я извиниться перед тобой хотел. В тот вечер я тебя про Генду спрашивал… Ты не обращай внимания, это я так… э-э-э…"
            show un surprise pioneer close at center with dspr
            "Теперь девочка уставилась на меня уже с явным недоумением, и дальнейшие слова сами собой замерли у меня на языке."
            dreamgirl "Знаешь, здесь даже я не понимаю, что происходит. Может быть, дело не в твоих дурацких расспросах?"
            show un sad pioneer close at center with dspr
            dreamgirl "Слушай, может быть, она в тебя влюбилась? Хотя нет, вы почти и не общались."
            dreamgirl "А с другой стороны, ты мужчина хоть куда - в самом рассвете сил. Точно, влюбилась!"
            "Скрипнув мысленно зубами, я попытался продолжить выступление:"
            me "Э-э-э… Видишь ли, это у меня такое задание на лето, провести э-э-э… своего рода социологический опрос…"
            show un shy pioneer close at center with dspr
            "Тут я окончательно потерялся и понял, что единственное спасение для меня - это быстренько всё доесть и свалить отсюда поскорее."
            "С минуту всё было тихо. Я поспешно заглатывал свою порцию макарон по-флотски. Лена сидела напротив и продолжала гипнотизировать тарелку, постепенно краснея всё больше."
            "Я уже схватился за стакан с компотом, когда вдруг она отложила вилку в сторону и решительно посмотрела мне прямо в глаза:"
            show un serious pioneer close at center with dspr
            un "Семён, я хотела тебя спросить…"
            "От неожиданности я чуть компотом не захлебнулся."
            show un shy pioneer close at center with dspr
            "Лена смутилась и снова отвела было взгляд{w}, но неожиданно быстро взяла себя в руки и продолжила, пусть и запинаясь после каждого слова, но совершенно отчётливо, вместо своей тарелки гипнотизируя теперь уже меня своими бездонными зелёными глазами."
            show un sad pioneer close at center with dspr
            un "Понимаешь…{w} последние… несколько дней…{w} Вернее, с того дня…{w} как ты приехал… я…{w} Вернее, со мной…"
            show sl happy pioneer at fright
            stop music fadeout 1
            play ambience ambience_dining_hall_full fadein 1
            show un surprise pioneer close at center with dspr
            sl "А! Я смотрю, вы обед уже закончили!"
            "Раздался откуда-то сверху жизнерадостный до отвращения голос."
            "Я вперил в его обладательницу полный ненависти взгляд, но Славя этого то ли не заметила, то ли её такие мелочи не смущали."
            sl "Ну что, пойдёмте на концерт? Посмотрим, что ребята приготовили!"
            show un sad pioneer close at center with dspr
            th "Вот делать мне больше нечего, только самодеятельностью любоваться!{w} Меня мирового масштаба тайны ждут, а она…"
            me "Славя, мне бы…"
            show sl serious pioneer at fright with dspr
            sl "Нет-нет, ты и так, Семён, всё время отрываешься от коллектива, а концерт это важное общественное мероприятие, все старались, готовились, Мику там изо всех сил…"
            "Наша активисточка продолжала разливаться дальше про ответственность перед обществом и что-то ещё в том же духе, но я её уже не слушал."
            show un normal pioneer at center
            show sl normal pioneer at fright
            with dspr
            "Лена тем временем встала и, нацепив на лицо отстранённо-нейтральное выражение, покорно пошла на выход.{w} Словно и не было только что этого странного разговора."
            "Словно это не она пыталась не то спросить у меня что-то, не то рассказать о чём-то для неё очень важном."
            hide un with easeoutright
            dreamgirl "Знаешь, а вот это уже и в самом деле свинство.{w} Сидят люди, непринуждённо общаются со взаимным удовольствием, и вдруг эта Славя подкрадывается со своими глупостями."
            dreamgirl "Хотя с другой стороны - а не оказала ли тебе активисточка только что любезность?"
            th "Какую ещё любезность?!{w} Что, моя карманная Шиза тоже сошла с ума?"
            dreamgirl "Чем обзываться, лучше пораскинь мозгами.{w} Лена явно собиралась основательно тебя загрузить какими-то своими проблемами, это даже тебе должно быть понятно."
            dreamgirl "А теперь подумай - оно тебе надо?{w} Нет, я понимаю, ты немного поплыл - такая милая скромная девочка, фигурка что надо, глазища эти… Так и хочется её утешить по самое не балуйся."
            dreamgirl "Но ты, вроде бы, собирался делом заниматься, разве нет?"
            show sl angry pioneer at fright with dspr
            sl "…меня слушаешь вообще?!"
            "Вздрогнув, я отвлёкся от увлекательного диалога с самим собой и обратил внимание на Славю. Кажется, она уже успела всерьёз рассердиться."
            sl "Имей в виду, на концерт ты пойдёшь обязательно."
            sl "Если понадобится - за руку потащу! Не сможешь идти - понесём на носилках."
            sl "Ольга Дмитриевна сказала собрать всех - значит пойдут все!"
            show sl serious pioneer at fright with dspr
            "Сказала она, как отрезала."
            "Посмотрев ещё раз на Славю я понял, что у меня на выбор только два варианта - либо я иду добровольно, либо меня действительно силком отведут за руку, как маленького."
            "Обречённо вздохнув, я поплёлся за Славей."
            hide sl with easeoutright
#
            $ alt_uvao_D4_lunch_un = True
            $ alt_uvao_D4_concert = True
#
        "Сесть со Славей":
            show sl normal pioneer far at center with dissolve
            "Поймав мой вопросительный взгляд, Славя улыбнулась и помахала мне рукой."
            th "А что, хорошая идея!"
#
#необходимо добавить проверку на флаги ивентов со Славей в дни 1-3 (если они возможны при сычевании) и соответствующие трогательные упоминания о них."
#
            show sl normal pioneer at center with dissolve
            "Славя все эти дни относилась ко мне доброжелательно."
            "Кроме того, она наверняка назубок знает историю страны и партии и может рассказать мне что-нибудь полезное, если удастся придумать походящую отмазку для расспросов."
            me "Я так понимаю, ты не против моей компании?"
            "Спросил я, подходя к её столу."
            show sl happy pioneer at center with dspr
            sl "Ну конечно же не против! Садись!"
            "Она гостеприимным жестом указала на место напротив."
            show sl normal pioneer close at center with dissolve
            "Я уселся, сгружая посуду с подноса на стол."
            show sl smile pioneer close at center with dspr
            sl "Приятного аппетита!"
            "Улыбнулась мне Славя."
            me "И тебе приятного."
            "С удовольствием ответил я."
            play music music_list["take_me_beautifully"] fadein 10
            stop ambience fadeout 15
            show sl normal pioneer close at center with dspr
            sl "Спасибо ещё раз, что помог с уборкой на площади. В одиночку Электроник до сих пор бы с гирляндами возился."
            me "Да не вопрос. Тем более, если бы ещё и он сверзился с лестницы, то пришлось бы закрывать кружок кибернетики, а это уже серьёзный удар по общественной деятельности в лагере."
            show sl angry pioneer close at center with dspr
            "Кажется, моя шутка не слишком понравилась Славе, потому что она нахмурилась и даже есть перестала."
            me "Извини, глупость сказал. Надеюсь, что с Шуриком всё будет в порядке."
            "Поспешил исправиться я."
            show sl serious pioneer close at center with dspr
            me "Мне Электроник сегодня сказал, что забегал в медпункт. Виола ему пообещала, что Шурика ещё денёк подержит для порядка и отпустит."
            sl "Да, я знаю."
            sl "Но всё равно, не шути так больше."
            me "Не буду, обещаю. Честное пионерское!"
            show sl normal pioneer close at center with dspr
            th "А кстати…"
            me "Тем более, у меня найдётся другой повод тебя немного шокировать."
            show sl surprise pioneer close at center with dspr
            "Хитро улыбнулся я."
            me "Я тебе сейчас задам неожиданный, но действительно важный для меня вопрос, а ты постарайся не задумываясь на него ответить - если сочтёшь возможным, разумеется."
            me "Обещаю, что сразу после твоего ответа я всё объясню."
            show sl shy pioneer close at center with dspr
            "Можно было догадаться, о чём подумала Славя, потому что на мгновение она явно смутилась, но быстро взяла себя в руки и решительно кивнула."
            show sl serious pioneer close at center with dspr
            sl "Ладно, спрашивай. Не обещаю, что отвечу на {i}любой{/i} твой вопрос, но постараюсь."
            dreamgirl "Да, да! Спроси, сколько у неё было парней, какая у неё любимая поза и… и…{w} Блин, такое раздолье для фантазии, что поневоле растеряешься!"
            "С серьёзным лицом я выдержал паузу…"
            me "Не могла бы ты коротко, буквально в несколько фраз, обрисовать роль товарища Генды в становлении нашего государства?"
            show sl surprise pioneer close at center with dspr
            "Разумеется, я бил наугад, но вряд ли этот самый Генда мог быть вовсе уж незначительной личностью, раз в пионерском лагере ему памятники ставят."
            "Смотреть на Славю было одно удовольствие. Определённо, она ожидала услышать что угодно, но только не это."
            "Мне стоило немалых усилий удержаться от смеха, но я справился."
            me "Я совершенно серьёзно - именно такой вопрос. И ты обещала ответить на него, если сможешь."
            "Спокойно заявил я."
            me "Или эта тема тебя смущает?{w} Разумеется, тогда я не буду настаивать."
            "Здесь я позволил себе улыбнуться, но только самую малость. Так, намёк на улыбку."
            show sl serious pioneer close at center with dspr
            sl "Подожди, ты и вправду хочешь, чтобы я…"
            "Наконец-то пришла в себя Славя."
            me "Да, я именно прошу тебя рассказать своими словами про Генду. Потом, как обещал, объясню зачем."
            "Девочка взмахнула косами и улыбнулась."
            show sl normal pioneer close at center with dspr
            sl "Ну хорошо. Стоит постараться как минимум для того, чтобы услышать потом твоё объяснение."
            "Как я и предполагал, со знанием истории у нашей активисточки всё было в полном порядке."
            "Я слушал, затаив дыхание, хотя и пытался делать безразличное лицо, как будто речь шла о фактах, известных мне с пелёнок."
            th "Генда… Руководящая роль… Верный соратник и продолжатель дела В.И.Ленина… Играл важнейшую роль в становлении советской науки…"
            "В голове творилось чёрт знает что. Либо это часть какой-то колоссальной мистификации, жертвой которой я всё-таки стал, либо…"
            show sl serious pioneer close at center with dspr
            "Возможно, мне не слишком хорошо удалось совладать с лицом, потому что, закончив, Славя посмотрела на меня как-то странно.{w} Пожалуй, даже с подозрением."
            sl "А теперь твоя очередь."
            me "Что?"
            "Растерялся я."
            show sl serious pioneer close at center with dspr
            sl "Ты же обещал объяснить, зачем тебе это было нужно!"
            "Возмутилась Славя."
            show sl angry pioneer close at center with dspr
            sl "Я надеюсь, это не какая-то глупая шутка?"
            "Спросила она с упрёком."
            me "Да нет, что ты! На самом деле всё очень просто.{w} Даже немного скучно, если честно."
            show sl serious pioneer close at center with dspr
            "Заторопился я с ответом.{w} Что бы здесь ни происходило, совсем незачем портить отношения со Славей.{w} Да и просто не хотелось обижать хорошего человека."
            show sl normal pioneer close at center with dspr
            me "Всего лишь часть школьного проекта по истории. Своего рода небольшой социопсихологический опрос."
            me "Мы с ребятами за лето опрашиваем всех, кого сможем, на такие вот темы.{w} Потом, осенью, всё это соберём воедино, обработаем…"
            me "В общем, такой вот эксперимент."
            show sl serious pioneer close at center with dspr
            sl "Экспериментатор!"
            "С упрёком бросила мне Славя, надув губки и рассеяно обводя черенком ложки клеточки на клеёнке, слегка смахивающей на шахматную доску."
            me "Славя, извини, я совсем не хотел тебя обидеть, но здесь всё дело именно в неожиданности вопроса.{w} К тому же, всё-таки общественное поручение…"
            sl "Да ладно тебе, ничего я не обижаюсь!"
            "Буркнула Славя."
            show sl normal pioneer close at center with dspr
            sl "Но чтобы окончательно загладить свою вину, тебе придётся принять участие ещё в одном общественно важном мероприятии."
            show sl happy pioneer close at center with dspr
            "Заулыбалась вдруг она, глядя на моё насторожившееся лицо."
            sl "Мы сейчас вместе идём на концерт.{w} И не вздумай отказываться! Ольга Дмитриевна велела всех собрать, так что явка обязательна."
            show sl smile pioneer close at center with dspr
            sl "И вообще, что ты будешь один сидеть, как сыч какой-нибудь?!"
            show sl normal pioneer at center with dissolve
            stop music fadeout 3
            play ambience ambience_dining_hall_full fadein 1
            "Встав из-за стола, она выжидающе посмотрела на меня."
            "Меньше всего мне сейчас хотелось любоваться на какую-то дурацкую самодеятельность."
            "В конце концов, в домике меня под подушкой дожидался многострадальный учебник, но выбора у меня, похоже, не было."
            th "Кажется, отказаться в такой ситуации будет не только рискованно, но и попросту невежливо…"
            hide sl with easeoutright
            "Стараясь не слишком показывать своё разочарование, я встал и поплёлся следом за Славей."
#
            $ alt_uvao_D4_lunch_sl = True
            $ alt_uvao_D4_concert = True
#
        "Сесть одному":
            "Идея болтать с пионерами об истории страны нравилась мне всё меньше и меньше. Только время зря терять!{w} Совсем не обязательно, что я смогу узнать хоть что-то полезное, а вот за сумасшедшего очень даже могут принять. Мало мне разговора с Леной насчёт Генды!"
            th "Лучше сначала почитать добытую в библиотеке книгу, а уже потом, если приспичит, доставать окружающих вопросами.{w}\nПо крайней мере, не буду похож на совсем уж наивного дурачка."
            "Я уселся за свободный столик в углу, изобразил самое мрачное выражение лица, на какое был способен, и принялся торопливо поглощать пищу."
            "Обед прошёл довольно спокойно.{w} Пару раз в поисках места к столу подходили было незнакомые пионеры, но встретив мой взгляд, благоразумно ретировались подальше."
            "Залпом проглотив компот, я поспешно встал из-за стола и, пока меня никто не успел перехватить, выскочил вон из столовой."
            "Очень хотелось поскорее добраться до домика и наконец-то заняться учебником истории."
            stop ambience fadeout 2
            window hide            
            jump alt_day4_uvao_meet_Yulia_after_lunch
    stop ambience fadeout 2
    window hide
#
label alt_day4_uvao_meet_Yulia_after_lunch:
#Д4 встречаем Юлю после обеда
#
# используется флаг рассказа Слави про Генду за обедом alt_uvao_D4_lunch_sl
#
    window hide
    $ alt_chapter_uv(4, u"Идентификация")
    $ persistent.sprite_time = "day"
    scene black with dissolve
    $ renpy.pause(2)
    play sound sfx_open_door_2
    play ambience ambience_int_cabin_day fadein 1
    scene bg int_house_of_mt_noitem_day with dissolve
    window show
#Придя, Семён приоткрывает окно, потому что домик прогрелся на солнце и читать умные книжки жарко
    "Домик успел здорово прогреться на солнце, поэтому первым делом я открыл окно нараспашку."
    if alt_uvao_D4_concert:
# Ходили на концерт
        "В ушах до сих пор звенело от адской какофонии, которую мне приходилось терпеть последние полтора часа."
        th "И дался Славе этот концерт. Точнее я на нем."
        th "Если ты видел хоть один детский утренник - считай, видел их все."
        "Вожатые рассадили отряды по предназначенным им местам, а часть пионеров сгрудилась сбоку от сцены, возле лестницы.{w} Должно быть, это - наши юные дарования."
        "Ведущая - Мику - объявила начало концерта, все захлопали, и - на-ча-лось."
        "Не попадающие ни в одну ноту певцы."
        "Музыканты, которым следовало бы прописать мануальную заборотерапию."
        "Танцоры, от природы лишенные чувства ритма."
        "И что самое удивительное - каждого провожали бурными аплодисментами!{w} Хотя, возможно, это был звук дружного фейспалма."
        "В общем, улучив момент, когда Славя отвлеклась, я благоразумно слинял с сего мероприятия.{w} Терпеть не могу самодеятельность, у меня от нее приступы мизантропии!"
        th "Столько времени потрачено зря!"
        dreamgirl "Ладно, хватит ныть об упущенных возможностях!{w} Вырваться-то ты всё равно не мог. Славя сторожила как…"
        dreamgirl "Посмотри лучше, что это за рюкзак возле кровати валяется.{w} Не про него ли Олька говорила?"
        th "А знакомый рюкзак. Точь-в-точь мой старый «Гриззли».{w} Разве что мой-то {i}дома{/i} остался…"
        th "Так и что тут нам «родители» прислали?"
        "Конфеты…"
        dreamgirl "А это точно твое? Скорее мелкой прислали. Напутали чего-то где-то."
        "Проволока… спички… перочинный нож… веревка… свинец… леска… ещё какая-то мелочь…"
        th "Да что за фигня? На что мне это все?"
        dreamgirl "Отлично, будем делать заграждение, отольем пуль, добудем пушку, возьмем заложника и будем требовать ответов."
        th "Да ну тебя."
        "Я задвинул рюкзак под кровать, и устало упал на неё."
        th "Что за день? Бездарно потраченное время, ненужный концерт, бесполезная посылка…{w} {i}И никаких ответов{/i}! Шикарно! Что дальше, интересно?"
        th "Ладно, посмотрю хоть учебник. Зря с риском для жизни тырил, что ли?!"
    else:
# Избежали концерта
        "Возле моей кровати обнаружился подозрительно знакомый рюкзак.{w} Можно было бы даже подумать, что это мой старый добрый «Гриззли», не будь я уверен, что прибыл сюда без рюкзака."
        th "Наверное, это про него мне Ольга утром говорила. Интересно, что там?"
        "Прямо под молнией, поверх остального содержимого, обнаружился внушительный пакет с конфетами."
        "Под пакетом - колода игральных карт, перочинный нож, спички, моток лески, коробочка с рыболовными крючками и, почему-то, мятый кусок свинца."
        th "Странный набор. Особенно свинец этот мне прямо-таки позарез нужен… Хотя, можно из него грузил понаделать, если вдруг приспичит."
        "Отчаявшись понять причудливую логику моих «родителей», кто бы они ни были, я махнул на всё рукой и вытащил из-под подушки заветный учебник."
        th "Пора уже разобраться с этим Гендой вплотную!"
# Конец вилки с концертом №1
    window hide
    show blinking
    $ renpy.pause(5)
    window show
    "Не знаю, сколько прошло времени, прежде чем я отложил учебник в сторону и тупо уставился в стену."
    "Голова была готова взорваться."
    if alt_uvao_D4_lunch_sl:
        "По всему выходило, что Славя за обедом мне всё очень точно изложила про этого самого Генду."
        "Из книги я не узнал почти ничего принципиально нового. Так, подробности."
    else:
        th "Генда…{w} Академик из народа…{w} Верный соратник и продолжатель дела В.И.Ленина…{w} Научная работа…{w} Множество статей…{w} Важнейшая роль в становлении советской науки…{w} Беззаветная преданность идеям коммунизма…"
    play music music_list["my_daily_life"] fadein 3
    "Казалось бы, после внезапного марш-броска из промозглой зимы прямиком в середину лета я должен быть готов ко всему. {w}Оказалось, что нет, не готов."
    "Я и сам понимал, что все эти дела минувших дней меня напрямую не касаются, но ничего не мог с собой поделать. {w}Вроде бы, какое мне дело до того, кто здесь что возглавлял, но ощущение было такое, словно под ногами закачалась опора, с детства казавшаяся незыблемой."
    dreamgirl "Ой, а что тут у нас такое? Потрясение основ? Ниспровержение устоев? Чужого очкастого дядю подсовывают?"
    dreamgirl "А ну давай-ка, быстренько напомни мне основные вехи славного пути вождя мирового пролетариата {i}нашей{/i} Родины! Того самого, который Ильич!"
    th "Э-э…"
    window hide
    $ set_mode_nvl()
    window show
    "Память услужливо принялась подсовывать что-то про добрый прищур глаз и чернильницы из хлебного мякиша… {w}Шалаш в Разливе… {w}Или Разлив - это не местность, а что-то другое? {w}Ссылка, Надежда Константиновна и обручальные кольца, сделанные из монет…"
    "\nСтарший брат Александр, который покушался взорвать… этого, как его… Александра III, вот!{w}\nИли нет, это-то здесь при чём. Не помню даже, кто того царя в итоге укокошил - этот самый брат, или кто-то другой?"
    "Хотя погодите, это же Александра II взорвали! И о чём это я? При чём здесь вообще все эти Александры?!"
    "Всё это на вехи пути вождя пролетариата тянуло как-то слабо."
    "\nЯ зажмурился и попробовал ещё раз."
    "Так. Возглавлял партию большевиков, там ещё враги-меньшевики были… {w}Минуточку, так они ведь тоже были членами компартии? Вот ведь…"
#    nvl clear
    window hide
    $ set_mode_adv()
    window show
    dreamgirl "Ну и какое отношение имеет та чушь, которая тебе лезет в голову, к тому, как этот самый вождь руководил страной и что он для неё сделал? Даже неважно, плохого или хорошего."
    dreamgirl "Ну ладно, последний вопрос, на двойку - назови даты рождения и смерти. {w}Хотя нет, это слишком сложно. Хотя бы год смерти, а?"
    th "1928-й?"
    "Шиза только вздохнула. Кажется, я сморозил очередную глупость…"
    th "Зато я Ленина в мавзолее видел! Сам, лично!"
    dreamgirl "Тогда, да! Конечно же, это полностью опровергает существование Генды, а заодно - и учебника, который валяется рядом с тобой!"
    "Внутренний голос насмешливо фыркнул и замолчал."
    "Я задумался. {w}Получалось, что про родной мир я знал не так уж много."
    "Пожалуй, о новейшей истории здешнего мира я теперь знаю даже больше - вон, хотя бы учебник пролистал."
    "И кстати, по всему выходит, что это именно какой-то {i}другой{/i} мир. Разве что всё вокруг - очень убедительная подделка…"
    "Я снова взял в руки учебник.{w} В меру потрёпанная книга - слегка потёртая обложка, переплёт немного перекосился. Многие страницы, особенно те, что в начале, хранили следы рук предыдущих читателей.{w} Похоже, большинство из них успевало продвинуться в изучении материала не так уж далеко."
    th "Ну если уж и {i}это{/i} подделка…"
    play sound sfx_tree_branches
    "От размышлений о множественности миров меня отвлек негромкий шелест, донёсшийся с улицы.{w} За приоткрытым окном вздрогнула ветка сирени…"
    window hide
    $ renpy.pause(1)
    window show
    if alt_uvao_D4_concert:
# Ходили на концерт
        dreamgirl "У нас завелись гости."
        th "Ага, и я даже догадываюсь, какие именно."
        th "Мелкие и рыжие."
        dreamgirl "Как тараканы. Непонятно, ты их гоняешь или они тебя."
        "Копошение продолжилось."
        th "Сделать, что ли, себе приятное, а ближнему гадость?{w} Тем более, Мелкая Зараза там явно не просто так засела."
        dreamgirl "Карателем подрабатываем? Одобряю! Шнель, шнель, партизанен капут!"
        th "Яволь, майн фюрер!"
        "Я встал с кровати… "
        stop music fadeout 1
        play sound2 sfx_bodyfall_1
        extend "и забытый учебник плюхнулся на дощатый пол!"
        dreamgirl "Самое главное - все делать тихо, ага. Ты б еще в барабан ударил."
        "Торопясь, чтобы рыжая бестия не успела смыться, я высунулся в окно."
    else:
# Избежали концерта
        "Похоже, кто-то там есть, причём этот «кто-то» старается не афишировать своё присутствие, раз по кустам шастает, а не ходит, как нормальные люди, по дорожке."
        "Я замер, прислушиваясь.{w} Несколько секунд всё было тихо…"
        "Потом за окном промелькнуло что-то тёмное, но так быстро, что ничего толком разглядеть не получилось."
        "Стараясь не заскрипеть пружинами, я тихонько встал с кровати и, крадучись словно ниндзя, двинулся в сторону окна{w}… Чтобы немедленно споткнуться о проклятый рюкзак, так и валявшийся на полу!"
        stop music fadeout 1
        with vpunch
        play sound2 sfx_bodyfall_1
        $ renpy.pause(1)
        dreamgirl "Не расстраивайся, у тебя есть и сильные стороны! То есть, должны быть, непременно должны…"
        "Охая, я кое-как поднялся на ноги. Хорошо хоть, головой о стол не приложился, а не то был бы у них тут детективный сюжет с моим трупом в качестве главной улики."
        "Кто бы там под окном ни прятался, после такой шумной встречи он наверняка уже смылся от греха подальше."
        "С этой мыслью я высунулся в окно."
# Конец вилки с концертом №2
    $ routetag = "uv"
    window hide
    scene bg int_house_of_mt_noitem_day:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 2.0 zoom 1.6 xalign 0.7 yalign 0.35
    $ renpy.pause(2)
    stop music fadeout 1
    scene bg ext_house_of_mt_day with dissolve:
        zoom 2.5 xalign 0.7 yalign 0.55
    $ meet('uv','Существо')
    show uv dontlike close at center
    window show
    play music music_list["mystery_girl_v2"] fadein 5
    "…Где чуть ли не нос к носу столкнулся с… э-э-э?"
    "На меня в упор смотрела пара настороженных глаз необычного золотисто-янтарного цвета."
    "Бледноватое лицо, густые, изрядо всклокоченные каштановые волосы, напряжённо расставленные уши."
    th "Что?! Уши?!"
    "Я внезапно понял, что на расстоянии вытянутой руки от меня сидит, напряжённо припав к земле, странное существо, которое я с уверенностью определил бы как девочку-подростка, если бы не уши, больше всего похожие на кошачьи, и не боязливо поджатый кошачий же хвост."
    dreamgirl "Эй, не тупи! Неужели ничего не напоминает? Ну, вспомни ты вчерашний вечер!"
    th "Да помню я всё, не мешай"
    "Пауза явно затягивалась. Не сводя с меня пристального взгляда, существо осторожно попятилось."
    show uv dontlike at center with dissolve
    "Всего-то на полшага, но я почувствовал, что ещё пара секунд - и она попросту убежит."
    th "В конце концов, я всегда неплохо ладил с кошками, в отличие от собак.{w} Да и в отличие от большинства людей тоже, если уж на то пошло."
    "Опасной она мне не казалась, а если удастся наладить с ней контакт, то мало ли, что сможет рассказать такая… такое… необычное существо."
    me "Привет."
    "Негромко сказал я. Кажется, получилось не слишком уверенно, но мне было здорово не по себе."
    "Она замерла, по-прежнему глядя мне прямо в глаза."
    me "Извини, не хотел тебя напугать."
    dreamgirl "Тоже мне, мастер контакта! Тебя только с террористами договариваться посылать! Я имею в виду…"
    uv "Я думала, внутри никого нет. А тут грохот!"
    "Осуждающе сказало вдруг существо (или, всё-таки, девочка - и не поймёшь сразу, кем её считать)."
    show uv normal with dspr
    "Она наконец моргнула и, похоже, немного расслабилась."
    dreamgirl "Ты смотри, она ещё и разговаривать умеет. Надо же!"
    if alt_uvao_D4_concert:
        #не падали
        "Стараясь не делать резких движений, я облокотился на подоконник, попытавшись принять непринуждённую позу."
        me "Понимаешь, я услышал, что под окном кто-то ходит, и хотел посмотреть кто, но уронил книжку на пол."
    else:
        "Стараясь не делать резких движений, я облокотился на подоконник, попытавшись принять непринуждённую позу, насколько это было возможно с ноющими локтями и коленями."
        me "Понимаешь, я услышал, что под окном кто-то ходит, и хотел посмотреть кто, но споткнулся и упал."
    "Сказал я, решив на всякий случай строить фразы попроще."
    uv "Вы, люди, всегда такие неуклюжие."
    show uv normal with dspr
    "Сообщила она.{w} Вздохнув, уселась на траву, поджав под себя ноги и аккуратно уложив хвост вокруг. Судя по всему, убегать она раздумала."
    "Теперь, когда она перестала сжиматься в комок, я поневоле отметил, что фигурка-то у неё очень даже ничего."
    "Изрядно оборванное короткое летнее платьице не скрывало изящный изгиб бёдер.{w} Стройные точёные голени, тонкие щиколотки, маленькие ступни…"
    dreamgirl "Да что тебе ступни, ты посмотри, какие у неё…"
    th "Цыц!"
    "Призвал я к порядку одновременно и Шизу и себя."
    "Впрочем, похоже было, что хвостатую девочку нисколько не смутило столь пристальное внимание с моей стороны."
    "Наоборот, в ответ она также разглядывала меня с самой наивной непосредственностью."
    me "Э-э-э… Послушай, а как тебя зовут?"
    show uv surprise with dspr
    uv "Меня?"
    "Искренне удивилась она."
    me "Ну да. Вот меня, например, Семён зовут."
    show uv normal with dspr
    uv "Я знаю. Слышала, как тебя ваша старшая так называла. И ещё та, с длинной косой - Славя."
    me "А у тебя есть имя?"
    show uv smile with dspr
    uv "А меня никак не зовут. Я - это просто я, зачем мне имя? И так всё ясно."
    show uv normal with dspr
    "Она немного подумала."
    uv "Хотя раньше меня Юлей называли. Наверное, и тебе можно. Если хочешь."
    $ meet('uv','Юля')
    "Мне показалось, что беседа начала ей надоедать и я поспешил сменить тему, ляпнув первое, что пришло в голову:"
    me "А ты здесь что-нибудь искала, да?"
    uv "Интересно было, что там в домике. В других я уже бывала, а тут всё время заперто."
    show uv upset with dspr
    "Она недовольно надула губки."
    uv "Еду вкусную искала."
    me "Так ты, наверное, есть хочешь?"
    "Догадался я."
    show uv normal with dspr
    uv "Не очень… В лесу всегда можно что-нибудь найти. Обычно я на мышей охочусь, только их поймать почему-то трудно. Странно, правда?"
    "Я-то как раз ничего удивительного в этом не находил - уши там, или не уши, а великовата она, мышей-то ловить.{w} Впрочем, ей этого говорить явно не стоило, поэтому я молча кивнул."
    uv "Ещё я в столовой еду беру иногда, только там вкусной еды мало остается, ваши всю съедают."
    "Она сказала это так просто, что сразу стало ясно - принципы собственности, что частной, что коллективной, для неё вовсе не пустой звук.{w} Она о них попросту не знает."
    me "Слушай, так это вчера, наверное…"
    play music eat_horn fadein 2
    "Из динамиков разнёсся по лагерю призыв к пионерам восполнить иссякшие за день силы."
    stop music fadeout 2
    play ambience ambience_int_cabin_day fadein 1
    "Я как-то сразу вспомнил, что за чтением учебника, делом пусть и полезным, пропустил полдник."
    show uv upset far with dspr
    "Юля дёрнула ушами и одним гибким движением поднялась на ноги."
    "Да-а-а, пожалуй, насчёт девочки-подростка я был неправ. Ничего детского в ней не было заметно.{w} Скорее уж, очень стройная молодая девушка, просто… компактная, что ли."
    uv "Ваши сейчас есть пойдут. Не хочу, чтобы меня кто-нибудь увидел."
    show uv upset far at fright with ease
    "Она повернулась и скользнула прочь."
    me "Подожди! Хочешь, я тебе что-нибудь вкусное с ужина принесу?"
    show uv smile far at fright with ease
    uv "Принеси! Булочку!"
    "Живо обернулась ко мне хвостатая."
    show uv grin far
    uv "Их сегодня пекли, я знаю! По запаху!"
    "С восторгом облизнулась она."
    me "Хорошо, будет тебе булочка. Где встретимся?"
    show uv normal far at fright
    "Юля задумалась на пару секунд."
    uv "За воротами, куда автобус приезжает.{w} Там вечером никто не ходит."
    me "Договорились…"
    hide uv with moveoutright
    "Сказал я её удаляющейся спине."
    dreamgirl "Нет, ты заметил, какие у неё…"
    th "Да заметил я, заметил! Не слепой.{w} И вообще, сделай милость - отстань. Здесь и так полно девчонок бегает, далась тебе эта ушастая!"
    dreamgirl "Вот то-то и оно, что девчонок полно, но хвост и уши - только у этой! Такая няшка!"
    scene bg int_house_of_mt_noitem_day with dissolve
    "Я слез с подоконника, едва не наступив на рюкзак, и закрыл окно."
    th "Кстати о рюкзаке!{w} Конфеты наверняка должны подойти для бартера с этим ушастым чудом природы!"
    dreamgirl "Позволь узнать, за какие же это услуги ты собираешься расплачиваться в таких объёмах?"
    th "За информацию! И заткнись уже наконец, или я сегодня же ночью заберусь в медпункт и буду есть там все таблетки подряд, пока не найду средство от шизофрении!"
    dreamgirl "Ладно, ладно, не горячись. Я нисколько не сомневаюсь, что способы охоты на мышей, которым она может научить, тебе очень пригодятся в жизни."
    "Недовольно буркнула шиза, но всё-таки заткнулась."
    stop music fadeout 3
    window hide
    jump alt_day4_uvao_supper
#
label alt_day4_uvao_supper:
# Д4-Ужин
#
# используется флаг посещения медпункта alt_uvao_D4_viola_morning
# устанавливает флаг ужина с Виолой alt_alt_uvao_D4_supper_cs
#

    $ alt_uvao_D4_supper_cs = False
    window hide
    $ alt_chapter(4, u"Юля. Ужин")
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    window hide
    scene bg int_dining_hall_people_sunset
    with dissolve
    play ambience ambience_dining_hall_full fadein 3
    window show
    "Войдя в столовую я огляделся."
    if alt_uvao_D4_viola_morning and alt_prologue:
# Утром были у Виолы и в прологе выбрали "Я пойду с тобой", можем поужинать вместе
        "Свободных мест практически не было. Впрочем, за одним из столиков их было сразу три."
        "Четвёртое занимала Виолетта."
        th "Похоже, не меня одного её общество смущает…"
        "Впрочем, поговорить с ней было надо, так что совместить беседу с Виолой и ужин показалось мне хорошей идеей."
        menu:
            "Подсесть к Виолетте":
                th "По крайней мере, лучше поговорить с ней здесь, чем наедине в медпункте."
                show cs normal with dissolve
                me "Виола? Можно к вам присоединиться?"
                dreamgirl "Неплохой заход. Ужин, потом пара коктейлей - и она вся твоя!"
                "Она неторопливо оглядела меня с ног до головы."
                play music music_list["eternal_longing"] fadein 3
                show cs smile with dspr
                cs "Конечно, {i}пионер{/i}, присаживайся. Я никогда не против интересной компании за ужином."
                "Как обычно, в этой фразе уместился целый ворох двусмысленностей, но отступать было поздно, и я уселся напротив неё."
                show cs normal close with dissolve
                me "Эээ… На самом деле я хотел поговорить, по поводу вашего {i}утреннего приглашения{/i}."
                dreamgirl "Ты что, собрался переиграть её на её же поле?! Без шансов, чувак, без шансов…"
                "Виола понимающе усмехнулась."
                show cs shy close with dspr
                cs "Само собой, приглашение всегда в силе, в {i}любое{/i} время дня и ночи. Только предупреди заранее, чтобы я могла подготовить все {i}необходимое{/i}."
                "Да, кажется, этот противник мне не по зубам."
                show cs smile close with dissolve
                me "Виола, я про местную легенду, девочку-кошку.{w} Я её видел сегодня. И даже парой слов перекинулся."
                $ renpy.music.set_volume(0.3, delay=3, channel='music')
                show cs normal close with dspr
                "Виола поставила стакан с компотом. Никакой томности во взгляде уже не наблюдалось."
                cs "Видел? Где?"
                me "Да возле домика. Услышал треск за окном, думал, опять Ульянка жуками забрасывать собирается, высунулся. А там она."
                "Виола помолчала, о чём-то размышляя."
                cs "И о чём вы беседовали, если не секрет?"
                me "Да ни о чём практически. Сказала, что я неуклюжий. Ещё что раньше меня видела."
                cs "Интересно. Она не представилась?"
                me "Представилась. Говорит, что раньше её Юлей звали."
                "Виола как-то оценивающе на меня посмотрела."
                "Под её пристальным взглядом я невольно заёрзал на стуле и уставился в тарелку."
                me "Мне с вами идти, да?"
                cs "Зачем?"
                me "Ну, как… Аминазину покушать, или что там у вас…"
                "Похоже, что-то для себя решив, Виола смягчила вгляд и улыбнулась."
                show cs smile close with dspr
                cs "Успокойся, всё с тобой нормально. Вообще, ты неплохо держишься для четвёртого дня в новом мире."
                th "Она знает?! Откуда?"
                me "Что?"
                show cs normal close with dspr
                cs "Что слышал. Не ты первый, пионер."
                me "А… Вы тоже?…"
                cs "Я? Нет, конечно. Я из группы исследователей."
                dreamgirl "О чёрт. Парень, ты, кажется, серьёзно влип. Сейчас тебя в такое место потащат, что дурдом раем покажется."
                "Кажется, Виола поняла, почему я вдруг побледнел, и поспешила меня успокоить:"
                show cs smile close with dspr
                cs "Пионер, не нервничай ты так. Никто не будет тебя исследовать... Если сам не попросишь.{w} Изучаем мы точки проколов между реальностями, а не тех, кто сквозь них приходит."
                cs "Смысл тебя потрошить? Такой же человек, как и все. Ну, жил на двадцать пять лет в будущем, подумаешь."
                th "Уф-ф-ф! Отлегло. Может, и вывернуться получится."
                show cs normal close with dspr
                "Виола ненадолго замолчала, задумчиво покачивая стакан с остатками компота."
                cs "Насколько я могу судить, то, что представилось тебе Юлей, одно из проявлений точки прокола.{w} Визуализация, можно сказать."
                "Я сложил немногие известные мне факты в кучку."
                me "Получается, что моё появление здесь и эта… Юля как-то взаимосвязаны?"
                "Виола решительно отставила стакан в сторону."
                cs "Думаю, раз ты её уже дважды за сутки видел, то весьма вероятно, увидишь в скором времени ещё раз."
                me "Навер… Ой, да! Мы же договорились с ней после ужина на стоянке встретиться."
                "Виолетта только головой покачала на мою забывчивость."
                cs "У тебя ведь есть фотокамера в {i}смарт-фоне{/i}?"
                "Последнее слово она выговорила как-то непривычно."
                me "Откуда вы?… А, ну да. Есть, конечно."
                cs "Попробуй её сфотографировать, и многие вопросы сразу отпадут."
                me "Лад… Подождите, так мне что, к ней идти?"
                cs "А почему нет? Есть все основания полагать, что для {i}тебя{/i} она должна быть безопасна, если специально злить не будешь."
                $ renpy.music.set_volume(0.6, delay=3, channel='music')
                show cs smile close with dspr
                cs "Только хочу сразу предупредить насчёт {i}чересчур{/i} близких контактов — тут тебе и изделие №2 не поможет."
                "Я снова покраснел."
                me "Да я и не думал даже!"
                show cs smile close with dspr
                cs "Ну-ну. Смотри, тут ты не котятами обзавестись рискуешь."
                stop music fadeout 5
                show cs normal close with dspr
                cs "Последствия могут быть весьма тяжёлыми, и не только для тебя. Понимаешь меня, {i}пионер{/i}?"
                me "Угу."
                show cs normal close with dspr
                cs "Вот и хорошо. Она что-нибудь еще говорила? Не просила чего-нибудь принести?"
                me "Просила. Булку просила. Мышей, говорит, сложно ловить, а булки ещё и вкуснее."
                cs "Ну так бери и иди. И постарайся побольше с ней говорить, всё-таки случай… редкий, скажем так."
                cs "Если получится к ней в гости напроситься — совсем хорошо. Ольгу я предупрежу, чтобы не искала тебя."
                me "Ольгу Дмитриевну? А она тоже… с вами?"
                cs "Да, отвечает за адаптацию.{w} Иди на своё свидание, пионер, не заставляй аномалию ждать. Потом мне все доложишь."
                me "А… Виола, а я вообще где?"
                show cs smile close with dissolve
                cs "Долгий разговор. Да и не в столовой же об этом беседовать?"
                "Покорно кивнув, я сунул в карман сдобную булку с изюмом и встал из-за стола."
                cs "Подожди-ка. Мне этот убийца фигуры ни к чему, а тебе для дела пригодится."
                "С этими словами она протянула мне свою порцию сдобы."
                dreamgirl "Да-а-а, такую фигуру грех не поберечь!"
                "Я сунул вторую булку в ощутимо раздувшийся карман и поспешил откланяться."
                hide cs
                $ renpy.music.set_volume(1, delay=0, channel='music')
                th "Всё равно, ещё немного разговоров в том же духе - и стены столовой придётся отмывать от моих мозгов."
                th "Проколы между реальностями, понимаешь, аномалии… Визуализации…"
                "Мне явно следовало взять паузу и привести содержимое головы в порядок."
                $ alt_uvao_D4_supper_cs = True
                jump uvao_D4_supper_end
            "Сесть одному":
#                "Очень СТРАШНО!"
                th "А с другой стороны, рассказывать-то пока особенно и нечего."
                th "Вот поговорю после ужина с этой Юлей ещё раз…"
                dreamgirl "Ой, да ладно! Признайся хотя бы самому себе, ты попросту боишься, что тебя съедят.{w} АМ!"
                th "О! Как раз место свободное есть."
                "Проигнорировав насмешки Шизы, я забился в угол и принялся спешно поглощать еду."
    else:
# Утром не ходили к Виоле
        "Как и за обедом, в столовой было неожиданно много свободных мест."
        "Здраво рассудив, что поддерживать с кем-то беседу всё равно сейчас не в состоянии, я забился в угол и принялся спешно поглощать еду."
# Ужинаем в одиночестве.
    dreamgirl "Эй! Ты булочку-то не сожри, проглот!"
    dreamgirl "И вообще, мало ли, какой случай может приключиться."
    th "И правда… Случаи-то, они разные бывают."
    "На всякий случай, я сунул полагающуюся мне булочку с изюмом в карман."
    "На месте мне не сиделось, очень уж хотелось поскорее убедится, что неожиданное знакомство перед ужином - не плод моего воспалённого воображения."
    "Или, как минимум, в том, что мои галлюцинации достаточно устойчивы."
    "Наконец я решительно отложил вилку и вскочил из-за стола."
label uvao_D4_supper_end:
    th "А нюх-то у хвостатой что надо - действительно, булочки свежие, сегодняшние."
    "Неожиданно для самого себя ухмыльнувшись этой мысли и избавившись от подноса с недоеденным ужином, я вышел из столовой."
    stop ambience fadeout 3
    window hide
    jump alt_day4_uvao_meet_Yulia_at_evening
#
#День4 встречаем Юлю после ужина за воротами
#
label alt_day4_uvao_meet_Yulia_at_evening:
    window hide
    $ alt_chapter(4, u"Юля. Вечерние встречи")
    $ persistent.sprite_time = "sunset"
    scene bg ext_dining_hall_away_sunset with fade
    play ambience ambience_camp_center_evening fadein 1
    window show
    "К воротам лагеря я шёл целеустремлённой походкой, не глядя по сторонам и постаравшись изобразить на лице чувство ответственности за выполнение очень важного дела, которое мне, несомненно, поручили."
    window hide
    scene bg ext_square_sunset with fade
    window show
    "В итоге, до ворот я добрался без помех - то ли проявились дремавшие раньше способности к лицедейству, то ли до меня попросту не было никому дела."
    stop ambience fadeout 1
    window hide
    scene bg ext_no_bus_sunset with fade
    play ambience ambience_camp_entrance_evening fadein 1
    window show
    "Быстро оглянувшись и убедившись, что за мной никто не увязался, я вышел на стоянку."
    "Здесь тоже было пусто."
    "Я постоял, прислушиваясь."
    play sound sfx_tree_branches
    show uv normal far at fleft with easeinleft
    uv "Эй, иди сюда!"
    "Из кустов слева высунулась Юля. Осмотревшись по сторонам, она выбралась на стоянку."
    show uv normal at left with dissolve
    uv "Принёс?"
    me "Конечно, как обещал!"
    "Я протянул ей прихваченную с ужина булочку."
    play music music_list["she_is_kind"] fadein 5
    show uv smile at center with dissolve
    "Юля проворно сцапала гостинец и, усевшись на поребрик, окружающий статую пионера, впилась в сдобу ровными белыми зубками."
    "Машинально отметив про себя, что клыки у неё тоньше и длиннее, чем обычно можно встретить у людей, я тоже присел чуть в стороне."
    show uv smile close at center with dissolve
    me "Давно ждёшь?"
    "Она неопределённо мотнула головой."
    "Похоже, моя новая знакомая целиком сосредоточилась на еде."
    "Она жмурилась от удовольствия и явно не собиралась отвлекаться на меня. Тем не менее, я попробовал ещё раз."
    me "А ты где живёшь? Где-то в лагере? Или в лесу?"
    uv "Нев. Тав."
    "Невнятно сказала Юля сквозь булку и неопределённо махнула рукой куда-то вдоль забора."
    "Она доела остатки и со вздохом облизнулась."
    show uv normal close at center with dspr
    uv "Вкусно. Жалко только, что мало."
    uv "У тебя с собой больше ничего нет?"
    "Я улыбнулся."
    if alt_uvao_D4_supper_cs:
        me "А вот есть!"
        "Острые зубки вонзились во вторую булочку."
        me "А кстати, ты конфеты любишь?"
    else:
        me "Нет, с собой нет. А кстати, ты конфеты любишь?"
    dreamgirl "Девочка, девочка, пойдём со мной в подвал! Я тебе там конфет дам!"
    show uv laugh close at center with dspr
    uv "Конечно люблю! А у тебя есть конфеты?"
    "Она явно оживилась и снова облизнулась."
    me "Я могу принести, если хочешь.{w} Может, я бы в гости к тебе зашёл? Ну туда, где ты живёшь."
    show uv surprise close at center with dspr
    "Кажется, Юлю такая идея искренне удивила."
    "Впрочем, через пару секунд она улыбнулась и кивнула."
    show uv smile close at center with dspr
    uv "Приходи. И конфеты приноси. Смотри, не забудь!"
    me "Так а куда приходить-то?"
    show uv normal close at center with dspr
    uv "Так говорю же - я там живу!"
    "Сказала Юля нетерпеливо и снова махнула рукой куда-то вдоль забора."
    "Вероятно, на моём лице отражалась некоторая заторможенность мыслительных процессов, потому что она снисходительно улыбнулась и добавила:"
    show uv smile close at center with dspr
    uv "Там большой старый дом, за лесом."
    if alt_uvao_D4_supper_cs:
        extend " По тропинке идти надо, тогда придешь."
        uv "Здесь в заборе калитка есть, незапертая. А дальше тропинка."
    me "А ког…"
    show uv upset at center with dissolve
    stop music fadeout 3
    "Начал было я, но Юля вдруг вздрогнула и навострила ушки."
    "Из-за ворот донеслись детские голоса, потом - приглушённый взрыв смеха."
    hide uv with easeoutleft
    play sound sfx_tree_branches
    "Я было повернулся обратно к Юле, но той и след уже простыл - только ветки кустов закачались."
    dreamgirl "И что этим пионерам спокойно не сидится?!{w} Лучше бы с умной книжкой посидеть, так нет же, шляются везде, ржут как лошади, мешают серьёзным людям устанавливать контакт с чуждыми формами разума!"
    "В кои-то веки я был согласен с Шизой - в самом деле, из-за них я так толком ничего и не выяснил про эту ушастую."
    "Хорошо хоть, я теперь хотя бы приблизительно знаю, где её искать."
    "Иначе можно было бы до старости бродить по лесу и ничего не найти…"
    "Я с досадой махнул рукой и поплёлся обратно в лагерь."
    stop ambience fadeout 3
    window hide
    jump alt_day4_uvao_evening_business
#
label alt_day4_uvao_evening_business:
#День4 Ходим по лагерю перед сном после второй встречи с Юлей
#
# устанавливает флаг вечернего визита к Виоле в медпункт и выхода на тру-энд alt_uvao_true
#
    $ alt_uvao_true = False
    $ persistent.sprite_time = "sunset"
    scene bg ext_square_sunset with fade
    play ambience ambience_camp_center_evening fadein 1
    window show
    "Снова вечер, снова недовольный Генда."
    "Я сел на скамейку напротив памятника и откинулся назад."
    th "Так. По крайней мере вырисовывается план действий."
    th "Завтра с самого утра отправляюсь в старый лагерь, к Юле в гости."
    th "Надеюсь, не зря придётся ходить."
#
    if (alt_uvao_D4_supper_cs):
        # ужинали Виолу
        dreamgirl "Вообще-то тебя просили доложить о результатах переговоров."
        th "И то верно."
        "Я поднялся, пересек площадь и свернул на дорожку к медпункту. Алиса, сидевшая с гитарой на отдаленной скамейке, проводила меня странным взглядом. "
        window hide
        scene bg ext_aidpost_sunset with fade
        window show
        th "Что это она так смотрит? Как на психа."
        dreamgirl "А ты и есть он. Добровольно идешь в логово сексуальной маньячки, затерроризировавшей все мужское население лагеря. Тебе только букета и бутылки вина не хватает для полной картины."
        th "Да ну тебя. Я же по делу."
        dreamgirl "Она-то этого не знает, что у тебя {i}дело{/i}. Жди, теперь слухи пойдут."
        "Переругиваясь сам с собой, я неторопливо добрел до медпункта и постучал."
        play sound sfx_knock_door7_polite
        $ renpy.pause(2)
        cs "Открыто!"
        window hide
        stop ambience fadeout 2
        play sound sfx_open_door_1
        $ renpy.pause(1)
        $ persistent.sprite_time = "day"
        scene bg int_aidpost_day
        with dissolve
        play ambience ambience_medstation_inside_day fadein 3
        play music music_list["eternal_longing"] fadein 3
        show cs normal with dissolve
        me "Добрый вечер, Виола."
        "Она оторвалась от своего допотопного компьютера, в который вбивала какие-то записи с карточек."
        show cs grin with dspr
        cs "Входи, входи… пионер. Ты с отчетом, или так, заскучал в одиночестве?"
        dreamgirl "Держи себя в руках, прекращай думать всякие пошлости! Думаешь, я не вижу? И взгляд выше."
        me "С отчетом."
        $ renpy.music.set_volume(0.3, delay=3, channel='music')
        show cs normal with dspr
        cs "Тогда присаживайся и выкладывай."
        "Я послушно уселся на край кушетки и принялся докладывать:"
        me "Встретил снова Юлю, покормил булками, немного поболтали.{w} Сказала, что она в каком-то большом старом доме живет. Я так понял, где-то к югу отсюда."
        me "Говорит, в заборе какая-то калитка есть и от неё надо по тропинке идти."
        "Виола кивнула."
        cs "А! Это старый корпус, есть здесь такой, за пределами территории.{w} Вобщем-то мы это предполагали. Вполне подходящее место - людей нет, и от лагеря недалеко.{w} К тому же… Впрочем неважно, продолжай."
        me "Ну так вот. Она вроде бы не против, если я к ней заявлюсь в гости. С конфетами и булками, само собой - очень практичная девушка."
        cs "Практичная - вполне объяснимо… Но еще раз повторяю, она - не девушка! Не человек. Я даже не уверена, что она живая, а не какая-нибудь массовая галлюцинация."
        cs "Завтра, когда пойдешь в гости - веди себя прилично. Слишком близкие контакты закончатся очень печально. Для тебя - как минимум."
        me "Эээ… А мне обязательно идти?"
        show cs smile with dspr
        "Виола снисходительно усмехнулась."
        cs "Если не хочешь - я не заставляю. Но в твоем положении разбрасываться шансами глупо, где ты еще сможешь докопаться до самой истины?{w} Во всяком случае, интересные впечатления я тебе гарантирую."
        me "Ясно…"
        dreamgirl "Эй! Она что, на слабо тебя пытается взять?"
        show cs normal with dspr
        cs "Так что, пойдешь? Я тогда с утра Ольге передам, что у меня для тебя важное поручение. Иди, пионер, отсыпайся, пока можешь."
        th "Однако, какой решительный подход! Похоже, она за меня уже всё решила."
        th "Хотя, с другой стороны - я ведь и сам не против пойти…"
        me "Виола, так а куда идти-то? Я имею в виду, где мне эту тропинку-то искать, про которую Юля говорила?"
        cs "Ну с этим-то как раз всё просто.{w} От площади иди к реке и возьми правее от лодочной станции, по дорожке вдоль домиков. Дойдёшь до ней забора - там калитка будет."
        th "Ну хоть что-то прояснилось. А кстати…"
        me "Виола… Вы еще обещали рассказать, куда это я попал."
        cs "Если вкратце - параллельный мир. Хотя ты это, наверное, уже понял. А подробный рассказ пока подождет."
        cs "К слову сказать, ты эту Юлю сфотографировать не пробовал, как я советовала?"
        me "Да не успел я - кто-то рядом по лагерю шлялся, спугнули они её.{w} Толком поговорить не дали."
        cs "Досадно… Ну ничего, при случае - попытайся."
        show cs grin with dspr
        cs "Ступай, уже поздно."
        stop music fadeout 3
        "Она практически выставила меня за дверь."
        hide cs with dissolve
        $ renpy.music.set_volume(1, delay=0, channel='music')
        $ alt_uvao_true = True
        window hide
        play sound sfx_close_door_1
        $ night_time()
        $ persistent.sprite_time = "night"
        scene bg ext_aidpost_night with fade
        play ambience ambience_camp_center_night fadein 2
        window show    
        "До отбоя еще оставалось время, и я пошел обратно на площадь…"
        scene bg ext_square_night with dissolve
        window show
        "…Где немедленно наткнулся на Алису."
        show dv normal pioneer at center   with dissolve
        dv "Что, Семен, в медпункт ходил? Заболел?"
        me "Да нет. В гости заходил, поболтать."
        show dv grin pioneer at center   with dspr
        dv "Поболтать?"
        "Ехидно ухмыльнулась она."
        show dv laugh pioneer at center   with dspr
        dv "Судя по тому, как тебя выпнули, {i}поболтать{/i} ты ничем не успел."
        me "Успел, успел. Долго ли, умеючи-то…{w} А что, ты тоже собиралась… {i}поболтать{/i}?"
        show dv angry pioneer at center   with dspr
        dreamgirl "Молодец. Виола бы тобой гордилась!"
        "Алиса все с тем же выражением смотрела на меня - что-то среднее между «Вот придурок!» и «Ни фига себе дает чувак!»."
        "Я гордо расправил плечи и пошёл восвояси."
        hide dv with dissolve
        #плюшки не тырим
    else:
        # НЕ ужинали Виолу
        "Вечерняя площадь была безлюдной - видимо, после родительского дня большинство пионеров собиралось лечь спать пораньше."
        "Мне спать пока не хотелось, я сидел и наслаждался тихим вечером."
        "Незаметно стемнело."
        window hide
        $ persistent.sprite_time = "night"
        scene bg ext_square_night with dissolve2
        play ambience ambience_camp_center_night fadein 2
        window show
        "Я решил пройтись туда-сюда перед сном, и в итоге вырулил к столовой."
        window hide
        scene bg ext_dining_hall_away_night with dissolve
        window show
        "На крыльце кто-то копошился. Опять Юля?"
        play sound sfx_alisa_picklock
        "А, нет. На этот раз действительно Алиса. Снова пытается вскрыть замок, вполголоса ругаясь себе под нос."
        scene bg ext_dining_hall_near_night with dissolve 
        me "Опять замок ломаешь, Двачевская? Сейчас доломаешь, и завтра все без завтрака останемся."
        show dv scared pioneer at center   with dissolve
        "Она аж подпрыгнула - кажется, я подкрался совсем тихо."
        dv "Заикой сделаешь, блин! Чего приперся?"
        me "Того же, чего и ты. А тебе что, из дому припасов не прислали?"
        show dv angry pioneer at center   with dspr
        "Кажется, я что-то не то сказал - Алиса набычилась."
        dv "Не хочешь помогать - так и скажи!"
        me "Да ладно, помогу, не кипятись."
        show dv normal pioneer at center   with dspr
        "Тем более что и мне для завтрашнего похода не помешает запастись."
# тырят плюшки
        if (keys_keep or keys_take == 1):
        #если есть ключи
            if dv_help == 1:
            #если помогал Алисе в 1 день
                dv "Ты те ключи еще не потерял? Открывай давай."
            else:
                "Алиса уже собралась снова ковырять замок булавкой, но я вытащил из кармана ключи и демостративно позвенел ими."
                show dv grin pioneer at center   with dspr
                dv "Фига себе! Запасливый. Ну открывай тогда."
            th "С ключами любое дело становится намного проще."
            "Я открыл двери и мы проникли в пустую столовую."
        else:
            "Алиса принялась ковырять замок булавкой, как и в тот раз, я же вспомнил, в какое окно залезала Юля, и подошел проверить."
            "Оказалось, если немного подергать створку, шпингалет сам выходит из гнезда."
            show dv surprise pioneer at center   with dspr
            me "Эй, Алиса, хватит замок ломать! Иди сюда!"
            show dv normal pioneer at center   with dspr
            "Стараясь не натоптать следов на подоконнике, мы влезли в пустую столовую."
        window hide
        scene bg int_dining_hall_night with dissolve
        window show
        "Пяток булочек на брата - неплохой результат набега на столовую."
        "Алиса еще прихватила пару пирамидок с кефиром, а я решил обойтись сухпайком."
        window hide
        scene bg ext_dining_hall_near_night with dissolve
        show dv normal pioneer at center   with dissolve
        window show
        "Поделив добычу, мы замели все следы."
        "Мы уже собирались разойтись в разные стороны, по направлению к домикам, когда мне в голову пришла неплохая, как мне показалось, мысль."
        "Пока Двачевская в хорошем настроении, этим надо воспользоваться."
        me "Алиса, а ты ничего не слышала про какое-то старое здание где-то снаружи, в той стороне?"
        "Я неопределённо махнул рукой куда-то в сторону реки."
        show dv surprise pioneer at center   with dspr
        dv "Это ты про старый корпус что ли? Тебе-то зачем?"
        th "Бинго! Теперь главное - не спугнуть."
        "Я состроил загадочную физиономию."
        me "Зачем-зачем! Затем же, зачем и тебе!"
        me "Сама-то, небось, сто раз там бывала!"
        show dv normal pioneer at center   with dspr
        dv "Вот ещё! Делать мне нечего, только по развалинам всяким шляться."
        "Фыркнула Алиса, посмотрев, однако, на меня с некоторым даже уважением.{w} Впрочем, в темноте я мог и ошибиться."
        dv "Вообще, туда добраться несложно. От лодочной станции иди направо, по дорожке вдоль домиков."
        dv "Дойдёшь до забора - там калитка будет.{w} А дальше по тропинке иди, через лес."
        th "Ну конечно, ни разу не была там, как же! А дорогу-то знает."
        "Алиса внезапно изобразила испуганное выражение лица и перешла на зловещий шёпот:"
        show dv scared pioneer at center   with dspr
        dv "Только учти, место там проклятое, можешь и не вернуться!"
        "С этими словами она развернулась и, весьма довольная собой, направилась в сторону своего домика."
        hide dv with dissolve
        dreamgirl "Ага, запугала прямо-таки до смерти! Как бы штаны не намочить от страха."
        th "Ладно, хоть что-то прояснилось. По крайней мере понятно, куда идти."
        th "Конечно, с Двачевской станется меня отправить в кругосветное путешествие, но придётся поверить ей. Других вариантов всё равно нет."
        "Я пожал плечами и пошёл к себе домой."
    # конец развилки по alt_uvao_D4_supper_cs
    jump alt_day4_uvao_sleep_time
    
# приходит в домик
label alt_day4_uvao_sleep_time:
    window hide
    play sound sfx_open_dooor_campus_1
    stop ambience fadeout 1
    scene bg int_house_of_mt_noitem_night with dissolve
    play ambience ambience_int_cabin_night fadein 1
    window show
    "Ольги Дмитриевны в домике не было.{w} И хорошо."
    if not alt_uvao_true:
        "Сгрузив припасы в рюкзак, я разделся и занырнул под одеяло."
    else:
        "Я быстро разделся и занырнул под одеяло."
    th "Завтра рано вставать. Лучше даже до завтрака."
    if not alt_uvao_true:
        "Я выставил будильник на телефоне на 6:45."
        "Подумав немного, отключил звук, поставив на вибро - чтобы не спалиться, если вдруг вожатая к этому времени не уйдёт."
        "Сунул телефон под тощую подушку и закрыл глаза."
    else:
        pass
    stop ambience fadeout 5
    window hide

    scene bg black with fade2
    stop music fadeout 2
    $ renpy.pause (2)
    jump alt_day5_start_uvao


label alt_day5_start_uvao:
# Д5-подъём
label alt_day5_uvao_getting_up:
    $ alt_uvao_D5_hentai = False
    $ alt_uvao_D5_sh_mines = False
    $ alt_uvao_D5_evening_sl = False
    $ alt_uvao_D5_evening_dv_un = False
    window hide
    $ routetag = "uv"
    $ alt_chapter(5, u"Юля. Утро")
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    scene bg int_house_of_mt_sunset
    play ambience ambience_int_cabin_day fadein 4
    window show
    if alt_uvao_true:
        $ meet('mt', 'Голос')
        mt "Семён! Семён!" with hpunch
        th "М-м-м…"
        mt "Семён!" with hpunch
        "Голос продолжал неумолимо ввинчиваться в голову, а плечо неумолимо продолжали трясти, и мне пришлось-таки открыть глаза."
        show mt angry pioneer close at center with dissolve
        $ meet('mt','Ольга Дмитриевна')
        "Надо мной нависало недовольное лицо Ольги."
        me "Ох… Ольга Дмитриевна… Что случилось?"
        mt "Семён, вставай давай!"
        "Я обречённо вздохнул… И удивлённо раскрыл глаза!"
        "Исходящий от вожатой сложный букет запахов наводил на совершенно определённые мысли:"
        show mt sad pioneer at center with dissolve
        dreamgirl "Коньяк. Вчера. Много."
        "Я сел в постели.{w} Внешность вожатой при ближайшем рассмотрении подтвердила догадку Шизы - лицо имеет откровенно зеленоватый оттенок, одежда мятая…"
        mt "Семён, вставай!"
        "Ольга слегка покачнулась и, поморщившись, поспешно присела на свою незаправленную кровать."
        "Судя по тому, как мне хотелось спать, до подъёма было ещё далеко и я решил снова уточнить:"
        me "Ольга Дмитриевна, так что случилось-то?"
        mt "Да ничего не случилось, нам идти пора. Мне на летучку…"
        "Тут она снова поморщилась."
        mt "…И тебе тоже есть куда!"
        "Не удержавшись, я протяжно зевнул."
        me "Так а мне-то зачем в такую рань идти?"
        mt "Сам знаешь, пока поздняя пташка…"
        "Она попыталась улыбнуться, но немедленно осознала всю опрометчивость своего поступка и схватилась за голову."
        show mt angry pioneer at center with dspr
        mt "Короче говоря, марш по делам, пока ещё всё тихо! Мне совсем не надо, чтобы весь лагерь знал о том, что один из моих пионеров оставил территорию и шляется неизвестно где."
        show mt normal pioneer at center with dspr
        mt "Если что - на мероприятиях ты с утра отсутствовал, потому что приболел, Виолетта подтвердит."
        "Кажется, столь длинный монолог её окончательно утомил, потому что она замолчала, махнув рукой."
        "Потом с некоторым трудом поднялась, взяла со стола какой-то пакет и сунула его мне."
        play sound sfx_paper_bag
        mt "Это тебе вместо завтрака, перекусишь по дороге."
        dreamgirl "Монстр перегарный! Как будто после завтрака нельзя было пойти!"
        "Подойдя к двери, Ольга сердито обернулась."
        show mt angry pioneer at center with dspr
        mt "Семён, да вставай ты уже! И только попробуй мне снова заснуть!"
        hide mt with dissolve
        play sound sfx_close_door_campus_1
        "После этого она освободила наконец помещение. Вздохнув, я встал и принялся одеваться."
        "В пакете оказались несколько сдобных булок со вчерашнего ужина. Недолго думая, я сунул пакет в рюкзак."
    else:
        play sound_loop phone_vibro fadein 2
        "Проснулся я от какого-то неприятного жужжания."
        th "Вибровызов…"
        "Не открывая глаз, привычно потянулся было вправо, чтобы добраться до стола и заткнуть этот чёртов телефон…{w} И уткнулся носом в стену!"
        th "Что?…"
        "Через пару секунд до меня дошло, что я же не дома!"
        stop sound_loop fadeout 1
        "Я спустил ноги с кровати, достал из-под подушки телефон и выключил наконец будильник."
        "Потряс головой, прогоняя остатки сна, и попытался сфокусировать взгляд на экране. «6:47»"
        "Вожатой, как я и рассчитывал, не было."
        th "У неё сейчас летучка, наверное…{w} Как раз успею собраться и свалить по-тихому…{w} Пока ещё меня хватятся…"
        th "…"
        "Тут я обнаружил, что ноги мои по-прежнему касаются прохладного деревянного пола, а вот туловище уже безвольно завалилось набок…{w} Голова коснулась вожделенной подушки…"
        dreamgirl "Пра-а-авильно, плюнь ты на эту возню. Завтра сходишь. Или послезавтра."
        "Насмешливо шепнуло мне подсознание."
        "Со стоном я выволок себя из постели и встал, покачиваясь, посреди комнаты."
        th "Господи, как спать-то хочется…"
        "Всё-таки через пару минут я наконец-то проснулся достаточно, чтобы заняться хоть какой-то осмысленной деятельностью{w} - для начала, одеться."
    play music music_list["everyday_theme"] fadein 5
    th "Так… Смартфон в карман…{w} Конфеты с булками в рюкзаке…{w} Штаны на мне. Ничего больше не забыл?"
    "Подумав, на всякий случай переложил перочинный ножик из рюкзака в карман."
    "Я широко зевнул напоследок, чуть не вывихнув челюсть, закинул рюкзак на плечо и вышел, заранее ёжась, из домика в утреннюю прохладу."
    stop ambience fadeout 3
    play sound sfx_open_dooor_campus_1
    window hide
    $ renpy.pause(2)
    scene bg ext_house_of_mt_sunset with dissolve
    play ambience ambience_camp_center_day fadein 6
    window show
    "На улице оказалось даже холоднее, чем я думал."
    th "Надо было свитер прихватить."
    "Запоздало подумал я, шагая по дорожке и отчаянно зевая. Впрочем, возвращаться из-за него обратно мне не хотелось."
    "Пытаясь согреться, я припустил трусцой по спящему пока ещё лагерю."
    "Рассудив, что спешка - отличный повод избежать ежедневного аттракциона с умыванием ледяной водой, я ограничился посещением гостепреимного домика с трафаретом «МЖ» на боковой стене."
    window hide
    scene bg ext_square_sunset with dissolve
    window show
    "Уже почти миновав площадь со сторожащим её Гендой, я случайно бросил взгляд в сторону главных ворот и увидел какую-то фигуру возле здания клубов."
    th "Кому это не спится в такую рань?"
    "Замедлив ход, я пригляделся. Судя по форменным шортам, это парень, но кто?"
    "Пионер тем временем воровато огляделся, сверкнув очками в лучах утреннего солнца, и исчез за кустами, растущими возле входа в клубы."
    th "Шурик? А он-то куда в такую рань попёрся?"
    dreamgirl "Да в клуб, куда же ещё. Мысль приснилась. Изобретатель, что с него взять!"
    "На странности юного кибернетика мне было, по большому-то счёту, наплевать, так что я выбросил его из головы и заспешил дальше."
    window hide
    scene bg ext_houses_sunset with dissolve
    window show
    "Через несколько минут я уже шёл мимо последних домиков в дальнем конце лагеря."
    stop music fadeout 5
    stop ambience fadeout 5
    "Дорожка здесь кончалась, но дальше в заборе действительно обнаружилась небольшая калитка, закрытая на засов без замка."
    jump alt_day5_uvao_road_to_old_camp
    
# Д5-Дорога в старый лагерь
#
# Используется флаг alt_uvao_true
label alt_day5_uvao_road_to_old_camp:
    window hide
    $ persistent.sprite_time = "day"
    $ day_time()
    $ alt_chapter(5, u"Юля. Старый лагерь")
    scene bg ext_path_sunset with fade
    play ambience ambience_forest_day fadein 3
    window show
    "Выйдя наружу и хозяйственно прикрыв за собой калитку, я решительно двинулся в сторону деревьев, подступавших здесь к самому забору."
    "Лес становился всё гуще. Торчащие из земли корни норовили попасть прямо под ноги - несколько раз я чудом избегал падения."
    "Несмотря на то, что с каждым шагом я отходил от лагеря, тропинка всё не кончалась."
    th "Кстати, о тропинке. К ней что, весь лагерь ходит? Понапротаптывали…"
    dreamgirl "Не ты первый, не ты последний, мой дружок."
    th "Не думаю, что всё так прозаично, иначе зачем она меня позвала?"
    dreamgirl "Ну как ты думаешь, зачем девушка может позвать юношу в уединенное место?"
    th "Да ну тебя!"
    if alt_uvao_true:
        th "Ещё и Виола эта со своими страшилками. Сама же говорит, что Юля для меня безопасна, а потом - что 'близкие контакты закончатся печально'."
        th "И вот как это понимать? Что, съест она меня?"
        "Я вспомнил остренькие зубки… Всё-таки до волка или даже до собаки ей далеко, да и я не мышь какая-нибудь.{w} Не съест."
        dreamgirl "Разве что понадкусывает немного!"
    "Очередной корешок подвернулся под ногу, но я устоял, практически сев на шпагат."
    th "Главное рюкзак донести. А то буду ползать по лесу и собирать просыпанные конфеты."
    if alt_uvao_true:
        dreamgirl "А ещё при {i}слишком близких{/i} контактах ты можешь заразиться, и у тебя тоже вырастут уши и хвост. А ещё…"
    "Я вздохнул.{w} Тропинка и не думала заканчиваться. Мне даже казалось, что я иду по кругу."
    window hide
    scene bg ext_path2_day with dissolve2
    window show
    "Немного подумав, я решил оставить зарубку на дереве."
    th "Если хожу кругами, то хотя бы пойму это."
    "Однако отметину в следующие несколько минут пути я больше не увидел, а там и лес начал редеть."
    "Вскоре показалась небольшая опушка, на которой стояло…"
    window hide
    scene bg ext_old_building_day with dissolve
    play music music_list["door_to_nightmare"] fadein 3
    window show
    dreamgirl "Избушка, избушка, повернись к лесу передом, а к Ивану задом!"
    "«Избушкой» оказалось здание, судя по всему, когда-то бывшее собственностью либо пионерского лагеря, либо какого-то детсада."
    "Ржавая ограда, заросшая бурьяном детская площадка, прохудившаяся местами крыша. Ночью здесь наверняка страшно."
    th "И она здесь живет?"
    dreamgirl "Хочешь проверить?"
    "Была  - не была."
    "Я открыл скрипящую калитку и вступил на вымощенную плиткой тропинку к входу."
    "Зияющая чернота входа, с валявшейся рядом дверью, словно приглашали меня прогуляться по внутренностям ужасного чудовища."
    dreamgirl "Штанишки не намочил?"
    th "Что, я трус какой? Нас такой ерундой не запугаешь!"
    window hide
    scene int_old_building_day_uvao with fade
    window show
    "Внутри оказалось не так темно, как я ожидал. Солнечные лучи нет-нет, да и пробирались через дыры в потолке и разбитые окна."
    "Здание выглядело необитаемым, но осмотреть его на предмет признаков жизни стоило."
    "Вокруг царило запустение."
    "Всюду валялся мусор. Толстый слой пыли покрывал старые деревянные столики, подоконники, гардеробные вешалки…"
    "Я задумался и не заметил, как под ногу подвернулось что-то мягкое. Я взглянул вниз."
    "Старый пыльный плюшевый медведь, без правой задней лапы, печально взирал на меня своим единственным глазом-пуговицей."
    "Возникало ощущение, будто я очутился то ли в каком-то городе-призраке, то ли в низкобюджетном фильме ужасов."
    dreamgirl "Ага. Теперь иди проверь вооон ту тёмную комнату. Главное, не забудь перед этим громко поинтересоваться, есть ли там кто-нибудь."
    th "Очень смешно."
    "Осмотр первого этажа результатов не дал, я лишь наглотался пыли и начихался вдоволь, поэтому решил подняться на второй."
    "На втором этаже было ничуть не лучше, чем внизу.{w} Те же разбросанные пыльные игрушки вперемешку с книгами, поваленные шкафы и тумбочки."
    "Пару раз под ноги попадались старые куклы, а один раз я чуть не упал, наступив на маленькую матрешку."
    "Создавалось впечатление, что это было излюбленное место для вандализма."
    play sound sfx_carousel_squeak
    "Внизу что-то громко и протяжно заскрипело. По моей спине прошел холодок."
    "Перочинный ножик моментально перекочевал из кармана в руку, однако двинуться с места я не решался."
    "Минуты медленно тянулись одна за другой, но ничто больше не нарушало вновь воцарившуюся тишину."
    "Наконец я набрался храбрости и решился спуститься вниз.{w} Аккуратно, шаг за шагом двинулся по лестнице…{w} Вот уже преодолел один лестничный поворот…"
    stop music fadeout 3
    show uv normal far at left with dissolve
    "В дверях я заметил чей-то силуэт и облегченно выдохнул: на пороге, спиной ко мне, стояла Юля и смотрела куда-то вдаль."
    "Я, убрав нож от греха подальше, собрался было окликнуть ее…"
    "Внезапно ступени под моей ногой скрипнули."
    show uv rage far with dspr
    "Ушки Юли тут же дёрнулись в мою сторону, а хвост распушился и стал напоминать ершик для чистки бутылок. С кошачьей проворностью она развернулась и уже готова была атаковать…"
    me "Стой! Это я, Семён!"
    show uv shocked far with dspr
    uv "Семён? Ты меня напугал. Незаметно пришёл."
    "Она заметно успокоилась, хотя её распушенный хвост всё так же ходил из стороны в сторону."
    show uv normal at center with dissolve
    me "Ты тут живёшь?"
    uv "Не тут, рядом. Тут часто бродят ваши… Я живу там."
    "Она махнула рукой куда-то вниз, как мне показалось, и снова развернулась к выходу."
    me "А что ты высматриваешь? Там кто-то есть?"
    uv "Показалось. Никого нету."
    "Она повернулась ко мне и потянула носом."
    show uv smile at center with dissolve
    uv "Булочки! Вкусно пахнут."
    me "А… да. Будешь?"
    show uv laugh with dspr
    uv "Конечно, буду! Давай скорее!"
    "Я выудил из рюкзака одну из плюшек и протянул Юле. Та принялась ее с аппетитом уплетать, а я решил завести светскую беседу."
    show uv smile with dissolve
    me "И как тебе здесь живётся? Нравится?"
    show uv normal with dspr
    uv "Ну да. Удобно очень. Нор много. Есть, где запасы прятать. В лесу не спрячешь, белки и мыши найдут."
    me "Какие запасы?"
    uv "Всякие. Вкусную еду когда нахожу - прячу. Грибы ещё прячу. Запасы нужны!"
    me "На зиму запасаешься, что ли?"
    show uv upset with dspr
    uv "Зимой? Это когда белое всё? Холодно. Хорошо, что недолго."
    me "Хм, недолго… Ну как скажешь. И как же ты зимой?"
    show uv smile with dissolve
    uv "Сплю. Только просыпаюсь, когда дерево блестяшками обвешивают. Интересно!{w} Я себе некоторые беру, играть.{w} Дай ещё булку."
    "В светской беседе наступила небольшая пауза, пока я рылся в рюкзаке."
    me "Юля, а скажи… Откуда ты?"
    show uv normal with dspr
    uv "Я - отсюда."
    me "Нет, а откуда ты здесь появилась?"
    show uv surprise with dspr
    uv "Появилась? Я всегда тут была."
    "От удивления она даже есть перестала на пару секунд. Впрочем, я был удивлён не меньше."
    me "То есть как это - всегда?"
    uv "Да, всегда. Тут интересно. Есть, за кем посмотреть."
    me "А раньше ты с людьми не разговаривала?"
    show uv normal with dissolve
    uv "Разговаривала. Давно. Со мной вожатая говорила."
    me "И о чём разговаривали?"
    show uv upset with dspr
    uv "Тоже спрашивала, откуда я. Мы недолго разговаривали.{w} Она потом испугалась и убежала. Глупая."
    "Она обиженно прижала уши. Потом улыбнулась мне:"
    show uv smile with dissolve
    uv "Ты не пугайся, с тобой ничего не будет."
    th "Интересный поворот… Получается, что-то со мной {i}могло бы быть{/i}? Или с кем-то другим уже {i}было{/i}?"
    "Пытаясь скрыть своё беспокойство, я отвернулся и принялся сосредоточенно копаться в содержимом рюкзака, затягивая паузу."
    "Справившись наконец со своим лицом, я достал последнюю булку и отдал Юле, постаравшись улыбнуться ей в ответ:"
    me "Да я и не пугаюсь, в общем-то… А почему ты от меня не убежала?"
    uv "А я тебя знала. Во сне видела, долго."
    dreamgirl "Чувак, а тебе это ничего не напоминает, часом?"
    th "Нет. Что ты имеешь в виду?"
    "Ответа я, к сожалению, получить не успел."
    show uv normal with dspr
    "Юля вдруг насторожилась, повела ухом в сторону выхода и вскочила."
    show uv dontlike with dspr
    uv "Там кто-то ходит. Ты кого-то привёл."
    "В голосе показались нотки угрозы."
    me "Я?! Не, я никого не приводил. Я вообще один шел!"
    show uv upset with dspr
    uv "А кто это тогда?"
    "Она указала куда-то в сторону леса. Я подошел и, щурясь от яркого солнца, взглянул туда, куда указала Юля."
    "Из кустов показалась шатающаяся фигура, которая тут же принялась себя отряхивать от листьев и налипшего к пионерской форме репейника."
    "Короткие светлые волосы, бликующие на солнце очки…"
    me "Шурик?!"
    me "Он не должен нас видеть!"
    show uv surprise with dspr
    uv "Он не с тобой?"
    me "Нет, он, наверно, шпионил за мной. Тоже тебя ищет, что ли?{w} Наверно, нам лучше спрятаться."
    "Юля проворно скользнула мимо меня и направилась куда-то мимо лестницы."
    show uv smile far at fright with dissolve
    uv "Сюда."
    "Она поманила меня вглубь здания.{w} Пройдя по коридору, мы оказались в бывшем помещении администрации, судя по останкам массивного письменного стола и застеклённых когда-то шкафов."
    play sound sfx_carousel_squeak
    "Юля повозилась с чем-то у стены и с громким скрипом откинула железную крышку люка."
    hide uv with dissolve
    "Ловко скользнув вниз, она оставила меня возле открытого люка. Выхода не было, я полез за ней."
    stop ambience fadeout 2
    play sound sfx_carousel_squeak
    "Осмотревшись напоследок, я закрыл за собой скрипучий люк и стал спускаться вниз."
    jump alt_day5_uvao_tunnel

# Д5 - Спуск в бункер.
label alt_day5_uvao_tunnel:
    window hide
    $ routetag = "uv"
    $ alt_chapter(5, u"Юля. Под землёй")
    $ night_time()
    $ persistent.sprite_time = "night"
    scene black with fade
    play ambience ambience_catacombs fadein 3
    window show
    dreamgirl "Эй! Да здесь же темно, как в…"
    th "…Наверное, как в подземелье, да? Жаль, фонарика нет!"
    me "И куда теперь дальше?"
    uv "Никуда. Подождём здесь."
    uv "Когда твой Шурик уйдёт - вернёмся."
    "Юля замолчала, слышно было лишь её спокойное дыхание где-то рядом. С поверхности не доносилось ни звука."
    th "Интересно, а если он наверху решит поселиться? Будем сидеть здесь вечно?"
    "С минуту я просто стоял на месте столбом, боясь отойти от лестницы."
    "Против ожиданий, воздух в подвале, или где там мы оказались, вовсе не был затхлым, да и под ногами было сухо."
    window hide
    scene bg int_catacombs_entrance with dissolve2
    window show
    "Постепенно зрение адаптировалось, и я понял, что мрак не был вовсе беспросветным.{w} Мы явно стояли в каком-то длинном коридоре или туннеле, с одной стороны издалека доносился слабый свет."
    show uv normal at left with dissolve2
    me "Юля, а куда коридор ведёт?"
    "Я показал в сторону невидимого отсюда источника света."
    uv "Никуда. Там дверь большая. Железная. Не открывается."
    dreamgirl "Вот тебе и возможность убить время. В худшем случае пройдёшься туда-сюда, разомнёшься."
    th "Ну да, а в лучшем - найду новых приключений на свою задницу."
    dreamgirl "Ну и сидел бы тогда в лагере, клинья к девочкам подбивал!{w} Глядишь, и обломилось бы чего."
    "Возразить Шизе было нечего. Действительно, стоя на месте много не узнаешь, и если уж я взялся искать ответы - упускать шанс получить хоть какую-то информацию было глупо."
    "К тому же здесь было ощутимо прохладнее, чем наверху, так что тонкая рубашка помогала слабо. Я зябко поёжился, с тоской вспоминая забытый тёплый свитер, и предложил:"
    me "Слушай, а пойдём, посмотрим эту твою железную дверь? Вдруг вместе получится её открыть?"
    "Юля не заставила себя долго упрашивать."
    show uv grin with dissolve
    uv "Пойдём, попробуем открыть!{w} Узнаем, что там!"
    "Она схватила меня за руку и уверенно потащила куда-то в сторону источника света."
    play music music_list["no_tresspassing"] fadein 3
    show uv smile with dissolve
    "По мере того, как становилось светлее, я смог немного осмотреться."
    "Туннель - а сейчас было уже совершенно ясно, что это никакой не коридор в подвале, а именно туннель - имел явно заметный уклон вниз."
    show uv normal with dspr
    th "Спуск в бомбоубежище? Серьёзно у них здесь всё организовано было!"
    "Вдоль бетонных стен тянулись аккуратно уложенные кабели, на потолке через равные промежутки были закреплены забранные в сетку светильники."
    "Впрочем, источник света впереди оказался таким же светильником - то ли единственным уцелевшим, то ли это просто было дежурное освещение."
    th "Надо же, сюда питание до сих пор подаётся!"
    "Запоздало удивился я."
    "В желтоватом свете пыльной лампы хорошо было видно, что стенам уже не один десяток лет, но время практически не коснулось их - ни трещин, ни потёков воды."
    th "Прочно же раньше строили!"
    "Туннель, освещённый редкими работающими светильниками, всё никак не заканчивался, плавно уводя куда-то вглубь."
    th "Что-то это выглядит слишком серьёзным для бомбоубежища в пионерском лагере где-то в глуши."
    stop music fadeout 2
    "Через несколько минут впереди показался наконец тусклый блеск металла."
    window hide
    scene int_catacombs_door_light2 with dissolve
    play music music_list["orchid"] fadein 5
    with dissolve
    window show
    "Дойдя до конца, мы оказались перед закрытой гермодверью, тускло освещаемой пыльной лампой."
    me "Ого, мощно сделано. Интересно, что там, за этой дверью?"
    show uv normal at cright with dissolve
    uv "Не знаю. Говорю же, у меня не получалось открыть."
    "Я попробовал повернуть кремальеру, но - увы, сил не хватало."
    "Немного пошарив глазами по сторонам, я заметил кусок толстой арматуры."
    th "Хмм, а что если?…"
    "Подобрав свою находку, я вставил её в колесо кремальеры."
    me "Дайте мне точку опоры, иииии…"
    "И ни хрена. Я надавил ещё сильнее, потом навалился всем телом, надеясь сдвинуть-таки заржавевший механизм. Тщетно."
    play sound breath fadein 1
    "Переводя дух, я снова огляделся. Глаза остановились на чёрной коробочке сбоку от двери - от кремальеры не дотянуться.{w} Под откидной крышкой обнаружилась кнопка без опознавательных знаков."
    stop sound fadeout 5
    "Немного подумав, я надавил кнопку, преодолевая усилие неожиданно тугой пружины…"
    "Внутри стены что-то глухо щёлкнуло."
    "Я отпустил кнопку - снова щелчок."
    th "Возможно, здесь специально проход для двоих - для сильного и для умного?"
    dreamgirl "Всегда двое их - учитель и ученик…"
    "Я не стал уточнять у Шизы смысл намёка, а вместо этого повернулся к Юле, с интересом наблюдавшей за моими манипуляциями."
    me "Поможешь мне?"
    show uv surprise at cright with dissolve
    uv "А что надо делать?"
    me "Я думаю, что дверь можно будет открыть, если держать эту кнопку нажатой. Сможешь её удержать?"
    show uv smile far at fright with dissolve
    "Несмотря на хрупкое телосложение, Юля без особого труда придавила тугую кнопку."
    "Услышав щелчок, я поспешно навалился вновь всем телом на импровизированный рычаг…{w} Это было опрометчиво."
    play sound sfx_carousel_squeak
    me "Уййй!"
    dreamgirl "Сила есть - ум не поможет."
    show uv surprise at right with dspr
    "Кремальера провернулась неожиданно легко, арматурина сорвалась, и я крепко приложил ей сам себя по правой ноге прямо над коленом."
    "Юля от неожиданности отпрыгнула на пару метров и уставилась на дверь."
    uv "Скрипит. Получилось?"
    "Шипя от боли, я выкрутил колесо до упора и потянул."
    play sound sfx_open_door_mines_metal
    "Дверь очень неторопливо провернулась на петлях."
    me "Похоже, что да."
    show uv normal with dspr
    stop music fadeout 3
    "Находясь под впечатлением от уже увиденного, я рассчитывал оказаться в тамбуре со второй гермодверью."
    window hide
    $ day_time()
    $ persistent.sprite_time = "day"
    scene bg int_catacombs_living with fade
    play music music_list["waltz_of_doubts"] fadein 5
    show uv normal at left with dissolve
    window show
    "Однако, к моему удивлению, сразу за дверью оказалось в меру просторное освещённое помещение. Двухъярусная кровать, шкафчики с ящиками, какие-то приборы - работающие, что удивительно! Похоже, именно сюда стремился Шурик."
    th "Не слишком-то похоже на бомбоубежище, скорее уж это какой-то защитный бункер для нескольких операторов, или что-то в этом роде. Кстати, операторов чего?"
    "Колено начинало ныть всё сильнее, и я, стараясь не беспокоить его, допрыгал до кровати на одной ноге."
    me "Подожди, давай передохнём. Я, похоже, сильно ногу ушиб и не смогу идти какое-то время."
    uv "Хорошо, давай."
    "Я решил посмотреть, что с ногой. На коленке красовалась небольшая ссадина поверх наливающегося синяка."
    uv "Залижи."
    me "A?"
    uv "Залижи. Я всегда так делаю. Мне очень помогает."
    show uv smile with dspr
    "Она задрала ногу и продемонстрировала, кося на меня жёлтым глазом."
    "Я усмехнулся. Ну и кошка!"
    dreamgirl "Может она и тебе коленку полижет, вдруг пройдёт?"
    "Шиза была как всегда в своём репертуаре."
    th "Отвали, не до того сейчас."
    me "Нет, мы так не делаем."
    "С некоторой гордостью сказал я."
    me "Мне бы немного льда. Да только где его тут взять?"
    uv "Вот поэтому зализывать лучше."
    "Но тут с ней не поспоришь. Наверное, в лагере найти лёд не составило бы труда, но в каком-то подземном бункере это не такая уж и лёгкая задача."
    show uv normal with dspr
    "Вставать с кровати не хотелось.{w} Слишком рано встал, слишком много беготни по лесам, руинам и туннелям, а также бесед с этим… существом, Юлей."
    "Должно быть, именно так себя чувствует чересчур разогнанный комп во время серьёзных вычислений - мой мозг практически кипел. Я вытянулся на кровати и закрыл глаза."
    show blink
    $ renpy.pause(2)
    "Толчок в плечо."
    hide blink
    show unblink
    $ renpy.pause(1.5)
    uv "Дверь закрой. А то этот, друг твой, нас найдёт."
    me "И не друг он мне вовсе!"
    "Возмутился я."
    "Но в целом - она была права, тут же ещё Шурик шляется где-то. Не хватало ещё, чтобы он меня нашёл в подземном бункере с девочкой, которая помимо хорошей фигуры имеет пару кошачьих ушей и хвост."
    "Доковыляв до двери, я закрыл её и свалился на кровать. Глаза закрылись сами."
    stop music fadeout 5
    show blink
    $ renpy.pause(2)
    "Последнее что я почувствовал - как кто-то тёплый подлез ко мне под руку и улёгся рядом…"
    window hide
    $ renpy.pause(2, hard=True)
    jump alt_day5_uvao_bunker

# Д5 - Бункер. От предхентая.
#
# Используется флаг alt_uvao_true
# Устанавливает флаг alt_uvao_D5_sh_mines.
# Устанавливает флаг alt_uvao_D5_hentai.
label alt_day5_uvao_bunker:
    window show
    "Проснулся я от ощущения какой-то тяжести на груди."
    play sound sfx_scary_sting
    "«Завалило!» - была первая паническая мысль."
    show uv smile close at center
    hide blink
    show unblink
    "Но нет, это была всего лишь Юля."
    play music music_list["reflection_on_water"] fadein 3
    "Она лежала, опираясь локтями мне на грудь, и пристально изучала меня. Какое-то время мы молча смотрели друг на друга."
    uv "Я вчера двоих ваших видела, старших. У речки. Большой, тяжёлый такой, и та, с которой ты живёшь."
    "Внезапно сообщила мне Юля."
    me "Ага… И что они делали?"
    uv "Спаривались."
    "Я вытаращил глаза."
    me "Чего-о?! {w=.4}Ольга Дмитриевна и Саныч?!"
    dreamgirl "Вот это поворот!"
    uv "Ага. Интересно было, я раньше не видела, как это. Непохоже."
    me "На что непохоже?"
    uv "На других зверей. Не как белки. Или мыши. Их тогда легко ловить."
    dreamgirl "Я же говорил, она тебе столько способов ловли мышей расскажет! Способ первый - дождись, пока папа-мышь с мамой-мышью захотят сделать мышат…"
    "Одно дело, когда у тебя в голове появляется посторонний голос - банальная шизофрения, ничего необычного. Но когда этот посторонний голос сам начинает сходить с ума…  Остаётся только вздохнуть."
    me "А на что похоже?"
    uv "Сначала они просто сидели."
    "Она взяла мои руки и положила себе на спину."
    uv "Потом вот так."
    "Она прижалась носом к моему носу. Наверное, это должно означать поцелуй."
    uv "Потом вот."
    "Чуть повернулась боком, и прижала мою ладонь к своей груди."
    dreamgirl "Чувааааак? Кажется, нам дадут!"
    th "Но она же… не человек?"
    dreamgirl "Сиська вполне человеческая и весьма упругая, кстати. Да и всё остальное, кроме хвоста и ушей - тоже вполне обычное."
    th "Да я не об этом! Если б такое устроила обычная девушка - всё ясно на сто процентов. А вот что у неё в голове происходит… я же вам не какой-нибудь контактер-уфолог!"
    dreamgirl "И улеглась она на тебя просто так, ага.{w} Поверь мне, если её перестанет устраивать происходящее, тебе об этом сообщат. В виде огромных царапин на всю щёку."
    if alt_uvao_true:
        jump alt_day5_uvao_evade_hentai_scene
    else:
        menu:
            "Попытаться сменить тему":
                jump alt_day5_uvao_evade_hentai_scene
            "Узнать поподробней":
                $ alt_uvao_D5_hentai = True
                jump alt_day5_uvao_hentai_scene

# Д5 - Сцена Хентая.
#
# Устанавливает флаг alt_uvao_D5_sh_mines.
label alt_day5_uvao_hentai_scene:
    me "Да-а-а? А потом?"
    stop music fadeout 4
    uv "А потом уже вот так!"
    "Она вскочила и ловким, по-кошачьи гибким движением избавилась от платья, под которым не было больше ничего лишнего."
    scene uv_bunker_hentai with dissolve    
    play music music_list["into_the_unknown"] fadein 3
    "Потом встала на четвереньки и повернулась ко мне, при этом хитро поглядывая через плечо, на манер избушки, так сказать. К стене передом, к Семёну задом."
    dreamgirl "Ну, убедился?"
    "Юлин хвост изогнулся эдаким приглашающим знаком вопроса. Не дождавшись от меня какой-нибудь реакции, она потопталась коленями и вопросительно наклонила голову."
    uv "М-рррр?"
    dreamgirl "Перевожу: Ну и долго тупить будем? Барыня легли и просют!"
    "Нет смысла противиться неизбежному. Не то чтоб происходящее оставило меня равнодушным, совсем наоборот, но…"
#    stop music fadeout 4
    th "Блин, это же бред какой-то! Или эротический сон."
    dreamgirl "Если сон, то тем более нечего тормозить. Короче, ты меня достал!"
#    play music music_list["that_s_our_madhouse"] fadein 3
#    play music music_list["glimmering_coals"] fadein 3
    "Меня подняло с кровати каким-то внезапным импульсом - должно быть, это тот самый пресловутый «Внутренний пендель»."
    "Только сейчас я обнаружил, что рубашка давно расстёгнута, и я лежу с полностью голым торсом."
    dreamgirl "А времени она зря не теряла. Поспал бы ты ещё полчасика - на тебе бы и шорт не осталось."
    "Руки сами собой легли на её талию, притянули к себе."
    uv "Мрррррра-а-у-у-у!"
    "Одобрительно протянула она и соблазнительно прогнулась."
    scene uv_new_hentai1 with dissolve
    th "Будь я котом, я бы просто взял её зубами за шкирку и хардкорно отымел…"
    dreamgirl "Как будто что-то плохое. Да и она вряд ли будет против."
    th "Цыц. Я пытаюсь не посрамить гордое звание Гомо Сапиенса в первом контакте с иной расой!"
    "Вместо этого я наклонился и прикоснулся губами чуть выше копчика. Хвост немедля обвил мне шею, будто какой-то пушистый удав, и затрепетал кончиком у меня на груди."
    "Дальше - цепочка поцелуев вдоль позвоночника, руки, облапившие грудь… Когда я дошёл до места между лопаток…"
    uv "Мррра-а-а-у-у-а-а-у-у!"
    "Она немного приподнялась и прижалась ягодицами ко мне."
    dreamgirl "Это означает: «Ну не тяни уже!». Ваш К.О."
    "Желание девочки - закон. А уж кошкодевочки… Я в единый миг освободился от всего, что мешало мне ниже пояса, и, прицелившись…"
    dreamgirl "На ладонь ниже хвоста. Не промахнись, Ястребиный Глаз."
    scene uv_new_hentai2 with dissolve
    uv "Мааааауууу…"
    "…единым махом скользнул внутрь."
    "Она со вздохом качнулась сначала от меня, потом обратно… И снова… И снова, вовлекая меня во всё ускоряющийся ритм движения."
    "Старая железная конструкция душераздирающе скрипела и качалась. Оставалось надеяться, что проектировщики заложили приличный запас прочности, и эта бандура не обрушится нам на головы. Впрочем, нас это мало беспокоило - мы как раз нашли подходящий ритм, и не собирались останавливаться несмотря ни на что!"
    "Юля с каждым движением старалась выгнуться всё сильнее, её кошачьи стоны заглушали скрип, а я… Я так увлёкся ролью «кота», что стал даже подпевать ей. Кажется, мы движемся к финалу…"
    "Хвост её по-прежнему обвивался вокруг меня, щекоча и вздрагивая. Я прижал его рукой к животу, и тон её «песен» внезапно повысился на октаву, вместе с громкостью. Ого! Вот это находка!"
    "Я не преминул воспользоваться новым знанием эрогенных зон кошкодевочек - обхватил ладонью хвост у основания и несильно, но властно потянул к себе. Кошечка прогнулась совершенно невероятным образом и натурально взвыла, продолжая насаживаться на меня."
    uv "А-а-а-а-х."
    "Это стало первым камешком лавины, и я совершенно потерял самоконтроль. Несколько секунд - или минут? - безумных телодвижений под дикие животные вопли, и мы оба, тяжело дыша, мокрые от пота и прочих жидкостей, повалились обратно на кровать."
    scene bg int_catacombs_living with fade2
    show uv smile close at center with dissolve
    uv "Мррррр… Да, примерно вот так и они тоже."
    "Тяжело дыша сказала она, устраиваясь головой на моём плече."
    me "Они? Кто?"
    "Мозг напрочь отказывался работать."
    uv "Тот большой и вожатая. Только у неё хвоста нет. Жалко её. С хвостом лучше."
    "Я представил Ольгу Дмитриевну с пушистым кошачьим хвостом, торчащим из-под юбки, и фыркнул. А если ещё уши, да панаму сверху… Не удержавшись, я захихикал совершенно по-дурацки."
    uv "Ты что?"
    "Удивлённо подняла голову Юля."
    me "Представил её с хвостом и ушами."
    uv "А-а-а. Тогда ладно."
    "Она взяла мою ладонь и пристроила себе на голову, негласно намекая - гладь давай, не отвлекайся!"
    hide uv with dissolve
    "Так мы и лежали, я - почёсывая ей между ушами, она - уткнувшись мне в плечо и посапывая."
    dreamgirl "Поздравляю вас, многоуважаемый коллега!"
    stop music fadeout 3
    "С Шизой говорить желания не было, однако она не оставляла попытки докопаться до меня."
    dreamgirl "Видел бы ты себя со стороны, кошачий угодник."
    menu:
        "Проигнорировать":
            $ alt_uvao_D5_sh_mines = True
            jump alt_day5_uvao_hentai_sleep
        "Побеседовать с Шизой":
            jump alt_day5_uvao_hentai_thinking

# Д5 - Сваливаем от хентая.
#
# Используется флаг alt_uvao_true.
# Устанавливает флаг alt_uvao_D5_sh_mines.
label alt_day5_uvao_evade_hentai_scene:
    "Почему-то я не был уверен, что хочу вдаваться в дальнейший сравнительный анализ коитуса грызунов и людей."
    dreamgirl "Кажется, тебе недвусмысленно предлагают перейти от болтовни к делу."
    "…"
    dreamgirl "Не тяни, девочка ждёт!"
    "Сделав над собой героическое усилие, я заставил вышедшую из-под контроля руку расслабиться и деликатно переместил её с груди Юли немного повыше."
    "Юля всё ещё лежала на мне и изучающе смотрела в лицо. Чёрт, да ведь на ней из одежды только лишь эта пародия на платье!"
    "Я вдруг очень остро ощутил сквозь два слоя тонкой ткани тепло её тела, его упругую мягкость… {w}Ноздри наполнял соблазнительный запах девичьей кожи…"
    "Рука сама собой миллиметр за миллиметром поползла обратно к груди…"
    if alt_uvao_true:
        th "Балда, Виола же особо предупреждала - никаких сексуальных контактов!"
        dreamgirl "А почему - не объяснила? Может быть, она это всё придумала, чтоб тебя заполучить?"
        dreamgirl "Ещё денёк-другой тебя промаринует в этом рассаднике подростковых грёз, а потом предложит заночевать в медпункте…{w} А ты уже на всё согласен будешь."
        th "Не, ты не просто Шиза, ты нечто наглухо психанутое. Пойди проспись!"
        "Я на несколько секунд задержал дыхание и потряс головой, пытаясь отогнать затягивающий меня сладостный дурман.{w} Бунтующее тело отчаянно отказывалось повиноваться. Ещё немного и…"
        "Впрочем, Виола мне кое-что ещё говорила. Мол, сделай снимок - и много вопросов отпадёт."
        me "Юля, А можно я тебя сфотографирую?"
        "Кажется, я нашёл наконец, как отвлечься."
        show uv surprise close at center with dspr
        "Юля навострила уши и удивлённо посмотрела на меня."
        uv "Сфотографируешь? Это как?"
        dreamgirl "… я вас сэйчас обоих сфатаграфырую!"
        me "Ну, это… Эээ…"
        th "И как ей объяснить, интересно?"
        me "Эээ, это такая штука… Делает картинку, на которой ты как настоящая."
        uv "Настоящая? Ещё одна я?"
        me "Ну… Давай я просто тебе покажу. Только сиди смирно. И отодвинься немного."
        show uv guilty at center with dissolve
        "Нехотя Юля чуть приподнялась с меня и смирно села, ожидая чего-то неизвестного для неё."
        "Я навёл камеру на Юлю, а она продолжала удивлённо смотреть на меня."
        dreamgirl "Внимание, сейчас вылетит мышка! Летучая!"
        window hide
        show frame at truecenter:
            xalign 0.5 yalign 0.5 zoom 1.0
            linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
        show cam_ui at truecenter:
            xalign 0.5 yalign 0.5 zoom 1.0
            linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
        window hide
        $ renpy.pause(2, hard=True)
        play sound snap fadein 0
        scene white with flash
        $ renpy.pause(2, hard=True)
        scene bg int_catacombs_living
        show uv surprise2 at center
        with dissolve
        window show
        "Вспышка, Юля подскочила от неожиданности, а я скорее полез в телефон посмотреть снимок."
        window hide
        play sound sfx_scary_sting
        scene uv_photo_city with dissolve
        $ renpy.pause(2, hard=True)
        scene bg int_catacombs_living with dissolve
        show uv surprise2 at center with dissolve
        window show
        "…"
        dreamgirl "…"
        "Даже словоохотливая Шиза не нашлась, как прокомментировать получившееся."
        show uv smile at center with dspr
        "Юля уже опомнилась от вспышки, и опять приняла свою любимую позу. На мне."
        uv "Получилось? Покажи ещё одну меня!"
        "Я молча повернул смарт экраном к ней."
        "Юля изучила снимок и кивнула."
        uv  "Хорошо получилось. Почти как я."
        show uv normal at center with dspr
        th "И ни капли удивления, главное."
        dreamgirl "Чувак, если таки надумаешь её сношать - попроси у Виолы полосатый презерватив. Пусть водители твой левитирующий посреди проспекта орган за жезл регулировщика принимают. {w} Извини, нервное."
        th "Задолбал со своими шутками. То есть {b}это{/b}, по твоему, нормально?"
        dreamgirl "А я и не говорю, что это нормально. Ты думаешь, Виола просто так попросила кошечку твою сфотографировать? Но, честно сказать, это для меня тоже сюрприз."
        th "Тогда - надо добиться ответов от Виолы! Она, похоже, самый информированный человек в лагере."
        dreamgirl "И обожает издеваться над пионерами, особенно мужского полу. Ты уверен, что сможешь… эээ… настоять на своем? Вдруг ей больше нравится использовать тебя «втемную»?"
        th "Ты хочешь что-то дельное предложить, или как обычно?{w} Если идей нет, тогда я просто пойду и потребую рассказать всё начистоту!"
        dreamgirl "…или можно собрать на нее компромат и шантажировать её неподобающим поведением,{w} или собрать «жучка» у кибернетиков и подкинуть ей,{w} или…"
        "Внутренний голос продолжал выдавать идеи одна другой дурнее, и я попросту от него отмахнулся."
        me "Юля, как ты думаешь, Шурик уже ушел?"
        uv "Наверное. Сюда он не приходил, я бы услышала."
        me "Значит, можно выбираться наверх. Пойдем?"
        jump alt_day5_uvao_true_back
    else:
        "Я разрывался между зовом плоти и параноидальным червячком сомнения.{w} Однако что-то во всей этой ситуации меня настораживало…"
        dreamgirl "…Или кто-то уже настолько боится женщин, что ищет повод слиться?"
        "Я не боюсь! Я опасаюсь. Тем более, что она не вполне человек."
        dreamgirl "Там где надо - вполне себе человек, причём явно выраженного женского полу. Не ври хотя бы себе. И мне."
        show uv normal close at center with dspr
        "Юля между тем заскучала и, игриво прижав ушки, придвинулась поближе, прижимаясь всем телом."
        dreamgirl "Ну, и как ты теперь планируешь слиться?"
        me "О, Юля! Я же тебе конфет принёс!"
        show uv grin close at center with dissolve
        "Юля радостно заулыбалась. Судя по её реакции, вкусная еда для неё всегда на первом месте."
        "Кажется, для меня блеснул луч надежды. Я судорожно перевёл дыхание."
        me "Ну-ка, где там мой рюкзак?"
        show uv smile close at center with dissolve
        "Я завозился, пытаясь приподняться, и с намёком посмотрел на Юлю. Та поспешила подвинуться, освобождая меня."
        "Расстегнув молнию на рюкзаке, я извлёк пакет с конфетами и вручил его несостоявшейся соблазнительнице."
        show uv laugh close at center with dspr
        "Она моментально впилась в него острыми ноготками, торопясь добраться до содержимого."
        dreamgirl "Некоторые подманивают девочек конфетами… А ты - наоборот, отвлекаешь. In Soviet Russia…"
        "На глаза мне вдруг попался спичечный коробок, лежащий на дне рюкзака вместе с остальным мелким барахлом."
        th "А что, вдруг спички пригодятся? Какой-никакой, а свет будет."
        "Недолго думая, я сунул коробок в карман шорт и покосился на Юлю. Та увлечённо боролась с конфетами, не обращая на меня особого внимания."
        "Пользуясь образовавшейся паузой, я решил получше осмотреть бункер."
        hide uv with dissolve
        "Какая-то аппаратура неясного назначения - между прочим, подключённая и работающая!" 
        th "Интересно, кто и что здесь замеряет?"
        "Я попытался разобраться в хитросплетениях проводов…{w} Безрезультатно. Некоторые провода соединяли клеммы приборов, но большая часть уходила куда-то в стену."
        "Мелькнула было мысль понажимать кнопки и повертеть ручки, что называется, «от балды», но я решил не искушать судьбу. Мало ли, ещё спалю что-нибудь."
        "В стене напротив кровати обнаружилась ещё одна гермодверь. Краска на ней местами облупилась, обнажив пятна ржавчины."
        "У другой стены выстроились шкафчики - наверное, с разными необходимыми припасами. Подойдя, я покопался в их содержимом."
        "Противогазы, аптечки с описью, инструменты… Но, кажется, ничего такого, что могло бы пригодиться мне прямо сейчас."
        stop music fadeout 3
        menu:
            "Продолжить расспросы":
                $ alt_uvao_D5_sh_mines = True
                jump alt_day5_uvao_asking
            "Задуматься":
                jump alt_day5_uvao_thinking

#Д5 - Расспросы Юли              
label alt_day5_uvao_asking:
    # TODO: Мысли между строк
    play music music_list["trapped_in_dreams"] fadein 3
    th "Поиск улик не принёс результатов. Следовательно, стоит сосредоточиться на допросе свидетелей.{w} Тем более, вот он, свидетель, шуршит конфетами."
    show uv smile close at center with dspr
    me "Юля?{w} Ты мне так и не рассказала, кто ты."
    uv "Я - это я. Не могу же я быть не мной? Тогда это уже была бы не я." 
    "Произнесла она, доедая свою сладкую добычу."
    show uv normal close with dspr
    th "Мда, так мы никуда не придём."
    me "Ну вот смотри. Я - Семён."
    uv "Да, я знаю."
    me "Но я также - человек. Пионер. Я заснул в автобусе у себя, а проснулся здесь. А ты?"
    uv "Я?{w}… Я - человек… только с ушами и хвостом.{w} Я - девочка…{w} Я всегда жила здесь…{w} Ой сколько меня много! Я так потеряюсь!"
    me "Ну ладно. А чем ты тут занимаешься вообще?"
    uv "Всем! Еду ищу, чтоб запасы делать. Хожу, смотрю. Вы, люди, интересные бываете, на вас весело смотреть. Только прятаться приходится."
    me "От меня-то ты не прячешься…"
    uv "Ну так я и говорю - давно тебя знаю! Я тебя видела, когда спала.{w} Все уезжают, и я сплю. Потом я просыпаюсь - а все уже приехали! Одной тут грустно и скучно."
    me "Погоди, как это ты меня видела?"
    uv "Когда спала, говорю же. Ты всё время в свой светящийся квадрат смотрел, и идти не хотел.{w} А что это за квадрат был? Он интересный?"
    me "Ну, это… Неважно, в общем, я тебе потом как-нибудь расскажу. Ты мне лучше скажи - я вот идти не хотел… а потом взял - и пришёл?"
    uv "Ага, не хотел-не хотел, а потом пришёл."
    me "А как это? Как это так вдруг получилось?"
    uv "Обычно. Взял - и пришёл. Приехал. Тут очутился."
    me "Ничего не понимаю. Я просто сел в автобус и заснул… А потом проснулся здесь! Да что за фигня-то?!"
    show uv upset close with dspr
    uv "Не кричи! Просто взял и пришёл. Ты давно уже должен был прийти, только всё не хотел и не хотел. Я уже даже ждать устала."
    me "Так получается, это ты мне снилась и спрашивала - «Ты пойдёшь со мной?»"
    show uv normal close with dspr
    uv "Не знаю. Это же тебе снилось, а не мне. Я просто ждала, когда ты наконец придёшь."
    me "Вот не знаю, как у тебя, а мы, обычные люди, не умеем так, чтобы заснуть зимой в городе, а проснуться летом в лагере. Может, этот лагерь какой-то особенный?"
    uv "Может быть. Я других лагерей не видела. Я же всё время здесь живу."
    me "А давно, кстати? Ну, сколько лет прошло, как ты здесь поселилась?"
    uv "Давно. Всегда. Тут ещё дети жили, маленькие, в старом доме, который наверху.{w} Потом все в новом лагере стали жить, а я здесь. Наверно, их та вожатая испугала, с которой я говорила."
    me "Вожатая? Испугала?"
    uv "Ну, я с ней поговорила. Было очень интересно, а потом она испугалась. А я только поговорить с ней хотела!"
    me "Погоди, а что с ней случилось, с вожатой-то? Чего она испугалась?"
    show uv normal close with dspr
    uv "Не знаю, я не поняла. Она почему-то испугалась и всё.{w} Бегала туда-сюда и кричала. А потом со мной говорить больше не хотела."
    uv "Потом остальные пришли. Я спряталась, а они тоже бегали и кричали.{w} Странные какие-то."
    show uv upset close with dspr
    "Юля обиженно замолчала и отвернулась, а я почувствовал холодок между лопаток."
    uv "А потом все-все ушли. Мне так грустно было, что я заснула."
    show uv smile close with dspr
    uv "А когда проснулась, все уже жили там. На новом месте. Мне там тоже больше нравится!"
    show uv normal close with dspr
    dreamgirl "Советую сменить тему…"
    "Кажется, это был тот самый случай, когда к подсознанию следовало прислушаться."
    th "Да, похоже, разговор на эту тему Юле неприятен. Не стоит мне её расстраивать, ох не стоит!"
    me "Ну ладно. Тогда расскажи лучше, что это за ходы тут такие под землёй?"
    show uv smile close with dspr
    uv "Очень интересные ходы! Длинные! Там дальше ещё дыра есть вниз, в другие ходы. Я в них живу и припасы храню. А теперь, когда ты мне дверь открыл, я буду здесь жить, здесь кровать удобная."
    me "А откуда они взялись-то, эти ходы?"
    show uv guilty close with dspr
    uv "Всегда были. Или нет… Те, нижние, всегда были. А эти, сверху - появились, давно. Я проснулась, а они есть."
    # TODO: ТЕКСТ
    "Расспросы ходили по кругу, не принося практически ничего. Сложно вытрясти информацию из существа, которое тебя понимает с пятого на десятое."
    "В конце концов мой живот напомнил мне, что пора бы и пообедать."
    show uv normal close with dspr
    me "Юля, а ты знаешь, как отсюда быстрее в лагерь попасть?"
    uv "Знаю. Надо вниз спуститься, и оттуда можно прямо к Генде выйти. Под землёй.{w} А если ты вон ту дверь тоже откроешь - то будет еще быстрее."
    me "Ну только если ты мне поможешь. Помнишь - надо на кнопку давить."
    uv "Хорошо!"
    show uv smile far at center with dissolve
    "Я закинул рюкзак за спину и подошёл к двери."
    jump alt_day5_uvao_mines_begin

#Д5 - Сон с неко-тянками, после которого идём в шахты.
label alt_day5_uvao_hentai_sleep:
    "Сил не было даже на мысленную пикировку с внутренним голосом, так что я просто проигнорировал его выпад."
    "Продолжая почёсывать Юлю за ухом, я незаметно провалился в сон."
    "…"
    window hide
    show blink
    play music music_list["sunny_day"] fadein 4
    $ renpy.pause(2, hard=True)
    scene black with fade2
    show prologue_dream
    show int_sleep_hentai_office2 behind prologue_dream with fade3
    window show
    "На офисном столе мурлыкнул селектор."
    uv "Семён Семёнович, приехали от заказчика на сегодняшнее мероприятие. Примете?"
    me "Да, Юля, проводи их в шоу-рум, и позови девочек."
    th "Крупный заказчик, высшее общество, мать его. Падкое на всяческую экзотику, которую я им, собственно, и поставляю. И с того мы все имеем кусок хлеба с маслом и икрой. Так что собираемся и идём работать."
    window hide
    show int_sleep_hentai_office behind prologue_dream with fade3
    hide int_sleep_hentai_office2
    window show
    me "Добрый вечер, господа! Сейчас, буквально через минуту, подойдут наши девочки, которых мы выбрали для вашей вечеринки. А я пока расскажу о правилах и условиях."
    voice "Короче, Склифосовский!"
    th "Шутник, блин."
    me "Если коротко - уши и хвост полностью настоящие, но не стоит за них дёргать и проверять, они этого очень не любят. Обращаться с ними нужно бережно, подчёркиваю - насилия не применять, не угрожать и не запугивать. Были случаи, закончилось всё печально, увы."
    voice "В смысле?"
    me "Представляете себе атакующую рысь или пантеру? Вот примерно в таком смысле."    
    me "Лучше действовать лаской, уговорами и подкупом - отлично работают всякие пирожные."
    "В дверь постучали."
    show sl neko veryfar behind prologue_dream at fleft with dissolve
    sl "Можно, Семён Семёнович?"
    me "Да, проходите, девочки."
    show sl neko veryfar behind prologue_dream at fleft with dissolve
    show un neko veryfar behind prologue_dream at cleft with dissolve
    show dv neko veryfar behind prologue_dream at cright with dissolve
    show mi neko veryfar behind prologue_dream at fright with dissolve
    me "Знакомьтесь, господа, это - Славя, Лена, Алиса и Мику - помесь русской голубой и ориентальной породы. Девочки, эти господа приглашают вас потанцевать и поразвлекаться, вы не возражаете?"
    voice "Позвольте глупый вопрос, а почему они в пионерской форме?"
    me "Называйте это корпоративным стилем, или рекламным ходом. Это запоминается, создаёт имидж…"
    voice "Ещё вопрос - почему только девушки?"
    me "У нас есть и мальчики, если они вас интересуют, но обычно девушки пользуются б{i}о{/i}льшим спросом. К тому же мальчики более агрессивны, стараются поддерживать доминирующую роль в стае… В общем, лучше брать их отдельно, тогда всё пройдёт без эксцессов."
    voice "Ну что же, тогда не будем терять время. Где подписать?"
    hide sl with dissolve
    hide un with dissolve
    hide dv with dissolve
    hide mi with dissolve
    show int_sleep_hentai_office2 behind prologue_dream with fade3
    hide int_sleep_hentai_office
    "Покончив с формальностями и проводив кошечек до лимузина, я вернулся в кабинет. Неплохой куш нам сегодня обломился!{w} Я подмигнул Юрию Куклачёву, грозящему мне пальцем из фоторамки на столе, вытянулся в удобном кресле и прикрыл глаза."
    stop music fadeout 3
    window hide
    show blink
    $ renpy.pause(1)
    scene bg int_catacombs_living
    show unblink
    window show
    play music music_list["reflection_on_water"] fadein 3
    "…"
    "Открыв глаза, я уже не увидел ни офиса, ни дурацкой фотографии кошачьего дрессировщика…{w} Что за…{w=1.0} Это сон?!"
    dreamgirl "Коллега. Что за херню мы только что посмотрели?!"
    "Время, наверное, подходило к обеду."
    "На мне всё в той же позе лежала Юля, тихо посапывая. Даже моя рука, которая была у неё в волосах, осталась, кажется, в таком же положении, что и перед сном."
    th "Наверное, вот оно какое, это счастье. Просыпаться с красивой девочкой. И не важно где, важно лишь с кем."
    dreamgirl "Угомонись, мечтатель. Конечно, это не отменяет того факта, что вы несколько часов назад вытворяли тут «Хау-ау», но, чувак, у неё есть кое-что, чего ты ни в одной девушке не найдёшь."
    th "Её глаза?"
    "Мозг выдал первое, что пришло в голову."
    dreamgirl "Какие глаза, придурок! У неё половина тела кошачья, тебя это вообще не смущает?"
    th "Ну, может, совсем чуть-чуть."
    "Полюбовавшись своей кошечкой ещё минут пять, я решил, что пора вставать."
    "Правой рукой я потормошил макушку Юле и немножко почесал за ушками."
    "Результат не заставил себя долго ждать, и она тотчас же проснулась."
    show uv smile close at center with dissolve
    uv "Приве-е-т."
    "Приподнявшись, она было собралась сладко потянуться, однако…"
    show uv guilty close with dspr
    uv "Ай. {w}Больно."
    "…вместо этого произошла неожиданная для кошечки встреча с верхним ярусом кровати!"
    "Состроив недовольную мордашку, она потёрла ушибленное место."
    "Вышло у неё это на удивление уморительно, да настолько, что я не выдержал и засмеялся."
    "Заметив мою реакцию, она вытаращила на меня глаза, полные непонимания."
    uv "Чего ты смеёшься, больно же!"
    "Она удивлялась моему поведению так искренне, как умеют удивляться только дети."
    "И эта её реакция пробудила во мне новую волну неконтролируемого смеха, от которого даже немного начало крутить в животе."
    "Юля продолжала смотреть на меня, как на больного."
    "На секунду в голове пронеслось, что если я не угомонюсь, то рискую сильно её обидеть."
    "Однако, в следующую секунду этим сомнениям суждено было развеяться."
    show uv smile close with dspr
    "На её лице появилась лёгкая улыбка, а потом она звонко прыснула, не выдержав - похоже, я смеялся слишком заразительно."
    "С трудом преодолевая хохот, я сбивчивым голосом кое-как из себя выдавил:"
    me "Че… чего смеёшься, бо… больно же!"
    "Кажется, я смог смутить её, за что был щедро вознаграждён несильным шлепком по носу."
    "Немного полежав на кровати и восстановив дыхание, мы наконец поднялись."
    "Тут я вновь вспомнил, что близится обеденное время. Пропускать еду совсем не хотелось, так что я поспешил подняться и поскорее одеться." 
    "Натянув шорты, я обнаружил, что на моей рубашке с удобством устроилась Юля."
    jump alt_day5_uvao_hentai_dress_up

#Д5 - Задумался, разговор с Шизой (Хентай ветка).
label alt_day5_uvao_hentai_thinking:
    "Судя по монотонному сопению девушки, та явно уснула. Всё-таки есть в ней что-то человеческое…"
    "Ко мне же сон идти не хотел. Мысли ураганом носились в голове."
    th "Чёрт, нехорошо как-то получилось. Развёл девушку…"
    dreamgirl "Развёл? Серьёзно? А не она тебя?"
    th "Ну она-то да… Но ты прекрасно понимаешь, что я имел в виду."
    dreamgirl "Опомнился, гусар. Раньше надо было думать."
    th "И всё же, было в этом что-то неправильное."
    dreamgirl "Конечно, не мне тебя воспитывать, но стоит лишь взглянуть на её уши и хвост… В общем, зоофилией попахивает, дорогой."
    th "А кто подстрекал-то?"
    dreamgirl "Я всего-навсего голос в твоей голове. Не я же тут устроил порнографию и кошачьи ласки?"
    th "Заткнись, пожалуйста. И без тебя вопросов хватает."
    dreamgirl "Пф-ф. Больно надо общаться с зоофилами."
    "Скосив глаза, я присмотрелся к голове, лежащей у меня на плече. Уши как уши, обычные кошачьи. Вот только растут из человека… И хвост. Я прикрыл глаза, вспоминая недавние события. "
    extend "Хвост не выглядел как-то фальшиво. Вполне ощущался руками как обычный кошачий хвост."
    "Я легонько прикоснулся к нему ещё раз, не вполне доверяя своей памяти. Тёплый, пушистый…{w=1.0} Определённо настоящий."
    th "Кто она вообще? Как такое возможно?"
    dreamgirl "Ну ко{w=.4}-неч{w=.4}-но,{w=.4} самое время! {w}А ДО ты спросить не мог, неукротимый ты наш?"
    th "Отстань, я сказал."
    dreamgirl "Я-то отстану. Но если вдруг через 9 месяцев окрестные леса заполонят похожие на тебя котята… Мы оба будем знать, кто из нас виноват."
    th "Да чтоб тебя! Ты вообще на чьей стороне, скотина?"
    dreamgirl "Я твой внутренний голос, говорю же. Я озвучиваю твои потаённые желания и мысли, иногда подрабатываю совестью на полставки. И вот сейчас тебе позарез требуется потерзаться!"
    th "Мне?!"
    dreamgirl "Ну не мне же. Я тут сижу и угораю, глядя на происходящее. Зацени - встретил кошкодевочку, приманил плюшками, завёл в бомбоубежище и отодрал так, что шерсть дыбом. Одно слово - зоофил!"
    "Не найдя слов для достойного ответа, я решил этот выпад проигнорировать."
    "В чем-то он прав, мой внутренний голос. Я всего лишь собирался разобраться, что за чертовщина происходит в этом лагере, выспросить у этой Юли всё, что она знает, а дальше… дальше по обстоятельствам."
    "А вместо этого - поддался животным инстинктам!"
    dreamgirl "Девочка, девочка! А пойдём со мной в подвал! Я тебе там конфет дам!"
    "Шиза считала за должное поиздеваться надо мной, но я уже не слушал её, а лежал, просто наслаждаясь моментом."
    "Юля по-прежнему сопела, лёжа на моем уже затёкшем плече. Без одежды было неуютно, но встать я не мог. Кто знает, как она отреагирует на внезапное пробуждение?"
    "Она слегка заворочалась во сне, устраиваясь поудобнее. Я взглянул на неё."
    "Каштановые волосы были немного растрёпаны, ушки то и дело подрагивали, а кончик хвоста слегка подёргивался из стороны в сторону. Ей явно что-то снилось."
    "А что ей может сниться? Охота на мышей или… "
    "Она заворочалась, и я, изловчившись, вытащил руку из-под её головы."
    "Стараясь не шуметь, я начал одеваться, нет-нет, да и поглядывая на сладко спящую обнажённую девушку…"
    "В какой-то момент я спохватился, что уже несколько минут наблюдаю, как она мило дёргает ушками, словно спящий котёнок, гоняющийся во сне за мышкой. Вот она шевельнула ногой, обнажив пикантные места…"
    dreamgirl "Хороша, чертовка…"
    th "И ведь не поспоришь."
    "Из-за двери, через которую мы пришли, донёсся какой-то приглушённый шорох."
    "Я так и застыл, стоя на одной ноге и продев вторую в шорты."
    "Шорохи были подозрительно похожи на шаги, однако вскоре они затихли…"
    play sound sfx_scary_sting
    play music music_list["no_tresspassing"] fadein 3
    extend " Но затем в дверь ударили!"
    "Кошачье ушко дёрнулось и повернулось в сторону шума, у меня же в висках застучала кровь с такой силой, что мне даже показалось, будто кто-то продолжает барабанить в дверь бункера."
    show uv surprise close at center with dissolve
    "Юля открыла один глаз, обвела взглядом комнату, взглянула на меня, потом на железную дверь, откуда секунду назад доносились шорохи. Она открыла второй глаз и потянулась, заскрипев кроватью."
    "Мне показалось, что за дверью нас прекрасно могут услышать, и я тут же замахал Юле, призывая её к тишине, но она, потупив взгляд, лишь зевнула, обнажив слегка заострённые клыки."
    "Из-за двери снова донёсся тот же равномерный шорох. Юлины зрачки сузились, а хвост тут же вытянулся по струнке."
    "Шаги удалялись.{w} Я выдохнул с облегчением."
    uv "Человек. Ушёл…"
    show uv normal close with dspr
    "Она тут же упала на кровать, сладко потягиваясь. Кровать вновь протяжно заскрипела, и я испуганно вздрогнул, однако за дверью больше ничего слышно не было, так что я понемногу успокоился."
    "Наконец шорты были застёгнуты, и я потянулся к рубашке, на которой устроилась Юля."
    jump alt_day5_uvao_hentai_dress_up

#Д5 - Общая ветка одеваний после хентая.
label alt_day5_uvao_hentai_dress_up:
    "Она смотрела на меня не мигая, её хвост подёргивался туда-сюда."
    "Было в её взгляде что-то такое… Будто смотрит на меня, как на добычу."
    me "Позволь."
    "Юля хмыкнула и села, прижав руки к обнажённой груди…"
    dreamgirl "Хэй, гусар-зоофил, не зависай."
    "Я только сейчас понял, что я пытаюсь почти вслепую завязать себе галстук, ибо мой взгляд не отрывался от её груди ни на секунду."
    "Выдав себе мысленного леща, я всё же отвернулся и попытался завязать наконец этот грёбаный красный лоскут."
    show uv laugh close with dspr
    "Юля хихикнула, наблюдая, как я в третий раз повязываю галстук, и снова неудачно."
    uv "Не понимаю, зачем вам эта красная повязка? Ужасно неудобно."
    me "Сам не понимаю. Такой у нас строгий дресс-код."
    show uv smile close with dspr
    uv "Без одежды значительно удобней."
    uv "Только ветки больно царапаются, и зимой холодно."
    show uv normal close with dspr
    "Юля тоже стала натягивать свою нехитрую одёжку."
    me "Как обратно пойдём?"
    uv "Вот ещё дверь."
    "Юля ткнула пальчиком в сторону второй железной двери."
    uv "Только она тоже не открывается."
    me "Ничего, сейчас что-нибудь придумаем."
    "За разговором мне удалось-таки повязать несчастный галстук."
    th "Хоть пальцы узлом не повязал в процессе - и то хорошо."
    "Подобрав рюкзак и закинув его на спину, я подошёл к двери."
    me "А куда она ведёт?"
    uv "В туннель… А там спуск в шахту, где я ловлю мышей."
    uv "Я была с другой стороны, но открыть так и не смогла."
    th "Что? Здесь ещё и шахты какие-то есть? Впрочем, сейчас это неважно."
    me "Надо успеть вернуться до обеда. Не хотелось бы попасть под разнос от вожатой."
    uv "Так под площадью есть выход! Прямо из шахт! Я часто там хожу, когда за едой бегаю."
    dreamgirl "Созналась, воровка! Пришло время её наказать, жестоко наказать, товарищ следователь."
    th "Оставь свои пошлые намёки. Да и к тому же, она ещё вчера днём чистосердечно во всём созналась."
    dreamgirl "Фи, какие мы серьёзные."
    uv "Так мы туда пойдём? В туннель?"
    stop music fadeout 5
    me "Ну, ладно. Пошли."
    jump alt_day5_uvao_mines_begin
    
#Д5 - Задумался, разговор с Шизой (Не хентай ветка).
label alt_day5_uvao_thinking:
    play music my_chrysalis_highwayman fadein 3
    dreamgirl "Что делать-то будем? Юля уже почти все конфеты съела, сейчас последнюю дожуёт и опять за тебя примется."
    th "Тебя брошу на съедение."
    dreamgirl "Во мне калорий мало. Вряд ли голодный хищник соблазнится бесплотным голосом в твоей голове.{w} Ей бы чего-нибудь более… плотского.{w} Мяса. На крайняк молока."
    th "Тебя Виола заразила, что ли?" 
    dreamgirl "А то! Она в этом деле мастер.{w} Кстати, ты не задумывался, а вдруг тебя сюда завезли как подопытный организм для разработки боевого вируса, передающегося половым путём?"
    th "ЧТО-О-О? Какой ещё нахрен вирус?!"
    dreamgirl "Обычный. Оборонительное биооружие.{w} Например, перед неминуемой сдачей города каждой жительнице делается специальная прививка.{w} Когда захватчики начинают вовсю пользоваться правом сильного, среди них начинается необъяснимая эпидемия."
    dreamgirl "Противостоять заражению невозможно - вирус усиливает выделение феромонов у носительницы, обычные средства контрацепции бессильны…"
    th "Ты псих."
    dreamgirl "Но тебя какая-то необъяснимая интуиция заставляет избегать близких контактов с носительницами вируса."
    dreamgirl "Первые три дня ты успешно игнорировал всех милых девушек из отряда.{w} На третий - успешно отфутболил поползновения вожатой.{w} Тебе подсунули экзотику - кошкодевочку, но и тут ты сумел расстроить зловещие планы!"
    th "Мда. Ну и что посоветуешь в таком случае?"
    dreamgirl "Объединиться с кибернетиками, занять круговую оборону в клубе и надеяться на лучшее!{w} Шурик вот тут ходил - может быть, он что-то знает?{w} Главное - продолжай успешно противостоять соблазнам, и…"
    "Шизу понесло. Я сел на кровать и постарался отключиться от потока совершенно безумных идей и советов."
    "Юля деловито разворачивала конфеты одну за другой, иногда поглядывая на меня. Интересно, конфеты она из столовой тоже тягает?"
    dreamgirl "…в чисто мужском коллективе действует аналогично гей-бомбе…{w} Нашла заначку Ульяны где-нибудь, вот и навострилась фантики разворачивать."
    th "Всё, водопад безумных идей иссяк? Какие будут {i}реальные{/i} предложения?"
    dreamgirl "Вернуться в лагерь. Как раз успеем к обеду. А то не ровен час, Ольга Дмитриевна заметит пропажу драгоценного пионера, начнется суматоха…"
    stop music fadeout 3
    play sound sfx_stomach_growl
    "В животе, как по команде, заурчало. Юля как раз расправилась с последней конфетой и уставилась на меня."
    show uv normal close at center with dissolve
    me "Юля, покажешь мне дорогу обратно?"
    show uv guilty close with dspr
    uv "А ты уже уходишь?"
    me "Да. Боюсь, если вожатая заметит, что я пропал… В общем, ничего хорошего не будет. Да и обедать пора."
    show uv smile close with dspr
    uv "Обед - это хорошо, вкусно. Принеси мне потом ещё булочек!"
    me "Принесу, принесу, сдобная душа. Ты только меня проведи обратно, чтоб никто не заметил."
    show uv normal close with dspr
    uv "Это нужно под землёй идти. Там никто не заметит! Пошли!"
    show uv normal with dissolve
    "Она направилась было к той двери, через которую мы вошли, но вдруг остановилась."
    uv "А вот эту дверь ты можешь открыть?"
    "Она указала на вторую такую же, но более заржавленную. Видно было, что ее открывают гораздо реже, чем первую… Хотелось бы знать, кто именно, и зачем?"
    me "Попробую."
    show uv normal far at center with dissolve
    "Я закинул рюкзак за спину и подошёл к двери."
    jump alt_day5_uvao_mines_begin

# Д5 - Поход наверх. Тру ветка.
#
label alt_day5_uvao_true_back:
    # TODO: звуки и музыка
    "Внутренний замок ничем не отличался от внешнего - кнопка в стене и колесо на двери. Еще одно подтверждение того, что в бункере должны находиться как минимум двое."
    dreamgirl "А если со вторым случится что-то? Возможно, у тебя просто не хватает ума догадаться, как открыть дверь в одиночку?"
    th "А мне и незачем! Второй раз я сюда лезть не собираюсь."
    "Отшив навязчивое не в меру подсознание, я кивнул Юле:"
    me "Давай, жми!"
    "Замок щелкнул, я завертел колесо, и бронированная дверь повернулась на исполинских петлях."
    play sound sfx_open_door_mines_metal
    window hide
    scene int_catacombs_door_light2 with fade
    $ night_time()
    $ persistent.sprite_time = "night"    
    show uv normal at cright with dissolve
    window show
    "За дверью никого не было, как и ожидалось. Найти тот люк в кабинете было довольно сложно, если не знать где искать.{w} Но какого черта здесь было нужно Шурику, хотел бы я знать?"
    uv "Дверь только запри. Я теперь знаю, как туда попасть."
    th "Как это она будет её открывать, в одиночку-то?"
    "Похоже, это соображение её ничуть не волновало. Ну ладно. Преодолевая инерцию нескольких центнеров металла, я вернул дверь на место и закрутил кремальеру в положение «Закр.»."
    window hide
    scene bg int_catacombs_entrance
    show uv normal at cright with dissolve
    window show
    "Без лишних разговоров мы дошли до люка с лесенкой. Юля иногда поглядывала на меня искоса, будто бы собираясь что-то спросить, но почему-то помалкивала.{w} Молчал и я. Мне было о чем подумать."
    me "Я первый полезу. Вдруг там Шурик, или еще кто-нибудь - заметит тебя, потом растреплет всем."
    show uv smile at cright with dspr
    "Почему-то это предложение развеселило её, и она радостно заулыбалась."
    uv "Хорошо! Ты первый, а я потом, если никого нет!"
    "Я пожал плечами и начал восхождение, очень скоро стукнувшись макушкой о закрытый люк."
    "Держась за лестницу одной рукой и упершись спиной в стенку шахты, я навалился на крышку и осторожно приподнял её, стараясь не скрипеть петлями."
    stop music fadeout 5
    window hide
    stop ambience fadeout 1
    $ day_time()
    $ persistent.sprite_time = "day"
    scene int_old_building_day_uvao with flash
    window show
    play ambience ambience_forest_day fadein 3
    "После темноты подземелий свет ножом резал глаза.{w} Проморгавшись, я огляделся и, стараясь не поднимать лишнего шума, откинул крышку люка."
    "Выбравшись наверх, я прислушался и выглянул в коридор.{w} Ни Шурика, ни кого-либо ещё видно не было."
    me "Вроде никого."
    uv "Ага. Я никого не чую."
    show uv normal at cright with dissolve
    "Юля ловко вылезла следом за мной."
    window hide
    scene ext_old_building_day with dissolve
    show uv normal at cright with dissolve
    window show    
    "Мы вышли на крыльцо и некоторое время просто жмурились от солнца. Сколько же времени я там проторчал, под землей?{w} Ох ёлки, уже обед скоро!"
    th "Вот за обедом и переговорю с Виолой."
    dreamgirl "Ага, разговоры на опасные темы лучше вести в людном месте. Чтобы свидетелей побольше, и живых щитов, если вдруг что-то пойдет не так…"
    me "Ладно, Юль. Мне пора бежать."
    show uv sad with dissolve
    uv "Ты уходишь?"
    dreamgirl "Так просто ты от нее не отделаешься, братец."
    me "Да. Вожатая не любит, когда я опаздываю, да и обед скоро."
    uv "А что будет, если опоздаешь?"
    show uv grin with dissolve
    "Юля игриво улыбнулась и слегка потянулась. Платье с готовностью облегло все изгибы её тела."
    dreamgirl "На больное давит, чертовка.{w} Соберись, тебе еще с Виолой беседовать."
    me "В принципе…{w} Ну…"
    "Промямлил я, глядя на аппетитные выпуклости девочки."
    show uv smile with dissolve
    "Впрочем, мне всё-таки удалось взять себя в руки."
    me "Она строгая. Чуть что – накажет.{w} И ладно, если что-то легкое, а то вон, вчера гирлянды собирать заставила, а это часа на три работы.{w} А ведь тогда не получится встретиться с тобой…"
    "Юля вздохнула."
    uv "Да? Ну ладно.{w} А что такое гирлянды?"
    me "Ну это… Огоньки такие на деревьях. Их на праздники вешают."
    show uv laugh with dissolve
    uv "А! Это которые разноцветные и светятся?{w} Я как-то хотела взять один, но он укусил меня и погас, а вместе с ним и много-много остальных…{w} Жалко. А зачем они?"
    show uv smile with dissolve
    "Я снова бросил взгляд на экран телефона, потом на Юлю."
    me "Если хочешь, можешь меня проводить. Я попробую объяснить, зачем они нужны и как устроены."
    dreamgirl "Ты спятил?"
    th "Виола сказала, чтобы… Как ее? Визуализация!{w} В общем, чтобы я поддерживал с ней контакт. На такую поблажку стоит пойти, иначе она может обидеться."
    dreamgirl "Ты уж как хочешь, но психолог из тебя, как из меня – Посол Доброй воли."
    "Я мысленно хмыкнул."
    show uv laugh with dissolve
    uv "А можно? Давай! Я как раз знаю короткую дорожку…"
    th "А вот это очень кстати."
    "Я закинул рюкзак на плечо, Юля схватила меня за руку и потащила куда-то в лес - в сторону от натоптанной тропинки."
    window hide
    scene bg ext_path2_day with dissolve
    show uv normal at cright with dissolve
    window show
    "Какое-то время мы шли молча. Я постоянно оглядывался, опасаясь появления Шурика.{w} Юля явно скучала и решила первой нарушить тишину."
    uv "Ты кого ищешь всё время?"
    me "Шурика. Не хотелось бы с ним столкнуться нос к носу."
    "Юля посмотрела наверх и пару раз шумно вздохнула."
    show uv smile with dspr
    uv "Нет, я никого не чую…{w} Мне кажется, мы его здесь не увидим."
    "Какой-то огонек блеснул в глазах Юли, хотя мне могло и показаться."
    window hide
    scene bg ext_polyana_day with flash
    show uv normal at cright with dissolve
    window show
    "На мгновение меня ослепил солнечный свет. Мы вышли в самый центр заросшей полянки, которую я до этого не видел."
    me "А где это мы?"
    show uv smile with dspr
    uv "Я люблю это место. Тут очень много грибов после дождя!"
    "Она ловко запрыгнула на поваленное дерево и заглянула под него."
    show uv laugh far with dissolve
    uv "Вот! Нашла!"
    "Лицо ее озаряла детская радостная улыбка, а в ладошке устроился маленький подберезовик."
    me "Я, конечно, понимаю, что грибы это хорошо, но лучше всё-таки поторопиться…"
    show uv normal far with dissolve
    "Улыбка Юли начала медленно сходить на нет."
    me "Но мы можем сюда потом вернуться, если хочешь."
    show uv smile far with dspr
    uv "Да! Очень хочу! Поможешь мне с запасами!"
    dreamgirl "Многоходовочкой попахивает…"
    "Она не церемонясь схватила меня за предплечье и вновь потащила в чащу."
    window hide
    scene bg ext_path2_day with fade
    show uv smile at cright with dissolve
    window show
    "Следуя за Юлей, я довольно ловко уворачивался от веток, но то и дело спотыкался о торчащие корни. Юля же ловко преодолевала все препятствия и продолжала меня тянуть дальше."
    "Создавалось впечатление, что если бы мы шли не по глухому лесу, а по аллее парка, то она вообще бежала бы вприпрыжку."
    "Задумавшись, я не заметил торчащий корень…"
    play sound2 sfx_bodyfall_1
    with vpunch
    hide uv with moveoutright
    me "Тьфу ты, черт!"
    show uv guilty close at cleft with dissolve
    uv "Ты не ушибся?"
    "Я перевернулся на спину и встретился глазами с наклонившейся надо мной Юлей. Её каштановые волосы щекотали мне лицо."
    me "Я…{w} Вроде в порядке."
    "Она протянула мне руку, но я поднялся сам, отряхнул форму и огляделся."
    show uv normal with dissolve
    uv "Точно всё хорошо?"
    me "Да ничего страшного, даже не ушибся вроде.{w} В любом случае у нас в лагере есть медсестра, уж она-то быстро на ноги поставит в случае чего…"
    "На миг передо мной появилось серьезное лицо Виолы, а в голове эхом прозвучали ее слова."
    "«…Последствия могут быть весьма тяжёлыми…»"
    "«…закончится очень печально. Для тебя - как минимум…»"
    show uv normal close with dissolve
    uv "Эй!"
    "Перед лицом промелькнула юлина ладонь."
    uv "Тогда пойдем? А то сидишь…"
    me "Да я просто задумался…"
    show uv normal far at cleft with dissolve2
    "Юля уже ушла вперед, гордо неся пушистый хвост.{w} Я поднял рюкзак, пристроил его поудобнее на плече и побежал за ней, на этот раз внимательнее смотря под ноги."
    window hide
    scene bg ext_path_day with dissolve
    show uv smile at cright with dissolve
    window show    
    "Вскоре мы вышли на тропинку. Здесь можно было уже идти рядом, и я принялся было объяснять Юле принцип работы гирлянды, но у той возникали всё новые и новые вопросы, так что занятие это было нелегким."
    "В итоге я сдался и решил не углубляться в рассказы про электричество и про то, откуда оно берется. Лес тем временем редел."
    uv "Интересно!{w} Ой! Дальше я не хочу идти. Еще слишком светло."
    me "Я тогда потороплюсь. Спасибо за всё, Юля."
    show uv laugh with dissolve
    uv "И тебе спасибо! Так интересно слушать! Про гирлянды и про это… Илитричество…{w} Приходи еще!"
    show uv normal with dissolve
    me "Всё, я побежал. Наверняка опоздал! До встречи."
    "Я постарался ей улыбнуться. Вышло как-то не особенно удачно, по крайней мере, мне так показалось, но Юля улыбнулась в ответ и махнула рукой."
    show uv smile far with dissolve
    "Выйдя из леса, я стряхнул листву с рюкзака и обобрал с одежды прицепившийся репей. Полуденное солнце приветливо встретило меня теплыми лучами."
    hide uv with dissolve2
    "Я оглянулся. Лес отсюда казался густым и темным. Юли уже не было видно."
    dreamgirl "Какое душевное прощание. Может вам стоило обняться, поцеловаться или…"
    stop ambience fadeout 3
    th "Вот тебя только не хватало!"
    window hide
    play ambience ambience_camp_center_day fadein 1
    scene bg ext_houses_day with fade
    $ renpy.pause(1)
    scene bg ext_square_day with fade
    $ renpy.pause(1)
    scene bg ext_house_of_mt_day with fade
    window show
    "Пейзаж сменялся быстро, я был поглощен раздумьями и в уме прикидывал примерный план отчета перед Виолеттой. Вскоре показался уже привычный мне треугольный домик."
    scene bg int_house_of_mt_day with dissolve
    play ambience ambience_int_cabin_day fadein 1
    play sound sfx_close_door_campus_1
    "Ольги Дмитриевны не было видно, и я вошел, закрыв за собой дверь."
    play sound eat_horn fadein 2
    "В этот же момент раздался горн, призывавший пионеров всех возрастов трапезничать."
    stop sound fadeout 2
    "Особо не церемонясь, я бросил рюкзак возле своей тумбочки и, выйдя из домика, направился к столовой."
    stop ambience fadeout 1
    jump alt_day5_uvao_lunch

# Д5 - Шахты.
#
# Используется флаг alt_uvao_D5_sh_mines.
label alt_day5_uvao_mines_begin:
    "Замок был в точности такой же, как и у первой двери. Указав Юле на кнопку, я приготовился крутить штурвал."
    "Здесь механизм шёл туже, но всё же скоро сдался. Я навалился всем телом на дверь и она неохотно повернулась на заржавевших петлях."
    play sound sfx_open_door_mines_metal
    window hide
    $ night_time()
    $ persistent.sprite_time = "night"
    scene bg int_catacombs_entrance
    show uv smile at center 
    with dissolve
    window show
    "За дверью оказался туннель, очень похожий на тот, по которому пришли сюда. Юля проворно выбралась за мной и с любопытством осмотрелась."
    uv "Да, это тот самый коридор."
    "Я закрыл дверь за собой и попробовал запереть, но кремальера повернулась лишь на несколько градусов и её тут же заело."
    "Плюнув на бессмысленное занятие, я отряхнул ладони от ржавчины и обернулся."
    uv "Ну что, пойдём?"
    "Мне ничего не оставалось, кроме как последовать за моим проводником."
    "Какое-то время мы молча шли по туннелю, освещённому редкими тусклыми светильниками. Едва различимые в полутьме стены выглядели совершенно одинаковыми, так что создавалось ощущение, что мы топчемся на месте."
    "Единственным звуком, нарушавшим царящую здесь тишину, было эхо наших шагов, так что практически полностью лишённый и зрения и слуха, я поневоле погрузился в собственные мысли, тем более, было о чём подумать."
    if alt_uvao_D5_hentai:
        "Вспомнив своеобразный рассказ Юли про Саныча и Ольгу, я невольно заулыбался."
        dreamgirl "Это же надо, Олька и Саныч. Кто бы мог подумать!"
        th "А они что, не люди что ли?"
        dreamgirl "Люди-то люди, но заниматься этим на пляже… Не каждая вожатая такое себе позволит."
        th "Ну что же, ночь, полная луна и пляж выполнили свою работу на отлично."
        dreamgirl "А пока вожатая по походам шастает, он к Виоле, наверное, ещё наведывается. С каким-нибудь вывихом или ушибом."
        "Стоило мне только подумать о Виоле, как сразу по всему телу побежали мурашки."
        th "Виола и на «{i}пионэрах{/i}» неплохо отрывается.{w} Хотя кто его знает. В конце концов, пионеры - это так, шалости…"
    # TODO: Хорошо бы вставить пару фраз-волнений про вожатую, которая бегала, а потом говорить не хотела
    if alt_uvao_D5_hentai:
        hide uv with dissolve
        "Заболтавшись с Шизой, я не сразу заметил, что Юля куда-то пропала."
    else:
        hide uv with dissolve
        "Углубившись в размышления, я не сразу заметил, что Юля куда-то пропала."
    th "Может, я отстал? Надо бы ускорится."
    dreamgirl "Эй, полегче, под ноги-то смотри!"
    window hide
    scene bg int_catacombs_hole with dissolve
    window show
    "Я поспешно притормозил, вглядываясь в практически непроницаемую здесь темноту, и обнаружил, что стою в шаге от какого-то провала в полу туннеля."
    "Я осторожно подошёл к краю, пытаясь различить хоть что-нибудь внизу, однако в темноте видно ничего не было."
    uv "Давай сюда."
    "Я вздрогнул от неожиданности, не сразу поняв, откуда доносится голос Юли."
    th "Кажется, откуда-то снизу? Наверное, пока я был занят самокопанием, она спрыгнула в этот провал."
    dreamgirl "…И я покажу тебе, насколько глубока котовья нора."
    if alt_uvao_D5_hentai:
        extend " Хотя ты в принципе уже всё, что можно, у неё разведал."
    th "Отвянь, озабоченный."
    "На всякий случай я уточнил:"
    me "Юля, ты там, внизу?"
    uv "Ну да. Прыгай!"
    "Я поднял голову. Насколько я мог разглядеть, дальше за провалом потолок туннеля обвалился, да и перепрыгнуть яму мне всё равно вряд ли бы удалось."
    "Впрочем, с учётом того, что мой Вергилий ждал меня где-то внизу, особого выбора у меня не было."
    th "Надеюсь, там не очень высоко…"
    "И я прыгнул в дыру."
    stop ambience fadeout 1
    stop music fadeout 1
    window hide
    play sound sfx_jump_into_hole_2
    play ambience ambience_catacombs_stones fadein 3
    play music music_list["door_to_nightmare"] fadein 3
    scene bg black with dissolve
    with dissolve
    window show
    "Скатившись по насыпи куда-то вниз, я кое-как поднялся на ноги. Отряхивая одежду, попытался разглядеть в темноте хоть что-нибудь, но тщетно. Здесь мрак был совершенно непроницаемым."
    if alt_uvao_D5_hentai:
        uv "Здесь темно. Давай лапу."
        me "Руку."
        uv "А?"
        me "Мы зовём это рукой."
        uv "Как хочешь. Давай сюда, свою эту, руку."
    else:
        uv "Здесь темно. Давай руку."
    "Я наугад протянул руку куда-то в темноту."
    "Нежные пальцы обвили мою ладонь и легко потащили за собой."
    "Под ноги подворачивались какие-то камни и, кажется, шпалы. Я то и дело спотыкался, но Юля, будто не обращая внимания на полную темноту, уверенно вела меня дальше."
    "Я уже начинал чувствовать себя слепым…"
    if alt_uvao_D5_hentai:
        th "Так у меня же в рюкзаке спички есть!"
        me "Юля, подожди-ка минутку."
        "Отпустив её руку, я снял рюкзак со спины и вслепую зашарил в нём. {w}Спустя пару мгновений заветный коробок был у меня в руках."
        "Достав наощупь спичку, я неловко чиркнул ей о коробок."
    else:
        th "Так у меня же спички в кармане лежат!"
        me "Юля, подожди-ка минутку."
        "Вытащив из кармана заветный коробок, я наощупь достал из него спичку." 
    window hide
    play sound match_lights
    scene bg int_mine_crossroad
    show match_lights
    show uv normal at center behind match_lights
    with dissolve
    window show
    th "Похоже, какой-то перекрёсток?"
    "Спичка догорела почти до конца, обжигая пальцы, и я поспешно отбросил её."
    scene bg black with fade2
    uv "Направо."
    me "Так я ведь ничего не вижу. Спички быстро сгорают, долго не посветишь."
    "Кажется, она усмехнулась:"
    if alt_uvao_D5_hentai:
        uv "А я тебя и дальше поведу. За ла… ой, то есть за руку."
        dreamgirl "А она быстро учится! Только смотри плохому её не научи."
        dreamgirl "Если уже не научил."
    else:
        uv "А я тебя и дальше поведу за руку."
        dreamgirl "А она сообразительная! Жаль только, что про тебя этого не скажешь."
        dreamgirl "Ведь у тебя был просто идеальный шанс с ней пообщаться {i}поближе{/i}, но и тут ты умудрился всё запороть."
        th "Отвали. Не хватало ещё, чтобы мой первый раз в лагере был с кошкодевочкой, которая в реальности может и не существует."
        dreamgirl "Зато вспомнил бы хоть для порядка, как это делается. Чтобы не облажаться перед другими пионерками в будущем, так сказать."
        "Ох уж эти разговоры с карманной шизой, никогда не упускающей случая подшутить надо мной!"
    "Я немного прибавил шагу, чтобы не отставать от Юли, и чуть сжал её руку."
    uv "Налево."
    "Я зажёг ещё одну спичку."
    window hide
    play sound match_lights
    scene bg int_mine_crossroad
    show match_lights
    show uv normal at center behind match_lights
    with dissolve
    window show
    th "Ещё один перекрёсток? Как она вообще тут ориентируется? Ещё и в кромешной темноте."
    "К сожалению, эта спичка сгорела так же быстро, как и предыдущая."
    scene bg black with fade2
    "Затушив её, я нащупал в темноте тёплую руку своей проводницы, и мы пошли дальше."
    scene int_mine_crossroad_matches
    $ renpy.music.play( (match_lights, silence), 'sound', True)
    "Так мы и ходили. То направо, то налево. Несколько раз я зажигал спички, но все перекрёстки были похожи, словно близнецы."
    "Скоро я целиком и полностью положился на свою странную провожатую - она каждый раз зачем-то озвучивала, в какую сторону мы поворачиваем."
    "Может быть, она хотела поговорить о чём-то, пока я молча плёлся за ней?"
    dreamgirl "Не льсти себе."
    "Очередной подкол Шизы я решил проигнорировать."
    if alt_uvao_D5_sh_mines:
        stop sound
        scene bg black with fade2
        uv "Стой!"
        "Вдруг резко сказала Юля."
        "Я застыл столбом в ожидании следующих указаний."
        uv "Там опять кто-то есть."
        play sound match_lights
        window hide
        scene bg int_mine_crossroad
        show match_lights
        show uv normal at center behind match_lights
        with dissolve
        window show
        th "Кажется, я догадываюсь, кто это может быть."
        uv "Снова он, в очках. Друг твой."
        th "Похоже, это означает, что я не ошибся. {w}Хотя погодите. {w=.5}«Друг»?"
        me "Да не друг он мне!"
        show uv smile at center with dspr
        "Грозно сказал я, притопнув ногой. Похоже, этот жест её очень сильно позабавил."
        th "Неужели мы и правда настолько похожи, что нас можно принять за друзей? Да ну, тупость какая-то."
        uv "Ой, да, прости."
        scene bg black with fade2
        uv "Он где-то там."
        "Наверное, Юля махнула рукой, показывая направление, вот только я-то ничего не видел."
        uv "Пошли."
        stop music fadeout 3
        jump alt_day5_uvao_mines_sh
    else:
        uv "Направо."
        "Я покорно проследовал за моей всевидящей спутницей. Впрочем, что мне оставалось? В темноте я всё равно ничего не видел, а тратить впустую спички было глупо."
        "Кроме своих указаний, в какую сторону сворачивать, Юля ничего не говорила. Да и мне не очень-то хотелось начинать разговор, так что я просто послушно плёлся следом, держась за её руку."
        stop sound
        jump alt_day5_uvao_from_mines_to_dinner

# Д5 - Идём на обед после шахт.
label alt_day5_uvao_from_mines_to_dinner:
    window hide
    scene bg black
    with dissolve
    window show
    uv "Пришли."
    "Юля отпустила мою руку и пошла куда-то вперёд."
    th "Интересно, куда же это мы «пришли»?"
    window hide
    play sound match_lights
    scene bg int_mine_door
    show uv normal at center
    with flash
    window show
    "Оказалось, что пришли мы к какой-то большой деревянной двери. По обе стороны от неё громоздился какой-то старый хлам - вёдра, лопаты… Рядом на стене висели старые фонари."
    th "Как же они называются? Кажется, в честь какого-то ночного животного…"
    dreamgirl "«Летучая мышь» такие фонари называются, гений. С такими, наверное, ещё твой дед ходил, а то и прадед."
    "Надо же, и от Шизы бывает иногда польза."
    "Спичка тем временем обожгла мне пальцы. Охнув, я выронил её и вновь воцарился мрак."
    scene bg black with fade2
    # TODO: Вставить небольшой скрип
    "Раздался негромкий скрип и впереди появился какой-то просвет - похоже, Юля открыла ту самую дверь."
    uv "Сюда."
    "Вытянув вперёд руки, я неуверенно двинулся в сторону её голоса."
    window hide
    scene bg int_mine_room
    show uv normal at center
    with dissolve
    window show
    with vpunch
    play sound sfx_bodyfall_1
    "Под ногу подвернулась бутылка, и я мешком упал на пол."
    me "Ч-чёрт!"
    show uv normal close at center with dissolve
    "Ко мне наклонилась Юля, глаза её слегка светились в полумраке."
    uv "Не ушибся?"
    me "Ничего, нормально."
    show uv normal at center with dissolve
    "Я поднялся на ноги. В помещение оказалось достаточно светло, чтобы глаза, уже привыкшие к темноте, смогли разглядеть обстановку. Какие-то трубы, краны. Изрисованные стены, бутылки, мусор по углам."
    "В дальнем углу виднелась неплотно закрытая гермодверь - именно из-за неё в помещение и проникал свет."
    uv "Дальше наверх, только там замок."
    "Я зашарил взглядом по полу и подобрал лежащий рядом кусок арматуры."
    me "Может этим?"
    uv "Не стоит." 
    uv "Я тут недавно кое-что нашла."
    show uv smile with dspr
    "Она держала что-то в руке. Присмотревшись, я понял, что это ключ."
    "Юля подошла к двери и прислушалась, потом потянула её на себя. За дверью оказалось что-то наподобие вентиляционной шахты с вмурованными в бетонную стену скобами."
    hide uv with dissolve
    "Щуря отвыкшие от света глаза, я подошёл ближе. Юля тем временем забралась по лестнице куда-то наверх, немного повозилась там, чем-то позвякивая, и спустилась обратно."
    show uv normal close at center with dissolve
    uv "Там решётка наверху. Я открыла. Вот ключ. Закрой замок за собой и брось ключ мне. Не хочу, чтобы ваши лазали сюда."
    "Я кивнул и взял из протянутой руки ключик."
    me "Эм-м… Спасибо."
    show uv smile close at center with dspr
    "Юля улыбнулась и ответила:"
    uv "Принесёшь ещё булочек? Я буду ждать."
    window hide
    scene bg int_mine_exit
    with dissolve
    window show
    "Я кивнул ей и полез наверх. Поднявшись, я дал глазам немного времени, чтобы немного привыкнуть к льющемуся сквозь решётку яркому солнечному свету, а потом выбрался наружу."
    stop music fadeout 5
    window hide
    scene bg ext_square_day with flash
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day fadein 5
    window show
    "Поворот ключа, и приваренный к решётке ржавый замок вновь защищал лаз от посторонних. Я просунул ключик между прутьями решётки и бросил его вниз, после чего выпрямился и огляделся, прикрывая заслезившиеся от света глаза рукой."
    "Как и обещала Юля, я стоял посреди площади, держась за пыльный постамент, с которого Генда всё так же надменно следил за происходящим на площади."
    "Более того, вентиляционная шахта, по которой я только что поднялся, и оказалась этим самым постаментом."
    un "Семён, где ты был?"
    play sound sfx_scary_sting
    with vpunch
    show un normal pioneer far at right with dissolve
    "От неожиданности я подпрыгнул."
    "Лена сидела на лавочке, неподалёку от того места, откуда я только что вылез."
    dreamgirl "Бегал… по шахтам… с девочкой-кошкой. Так ей и скажи."
    me "А, это ты. Ну, я…"
    un "Тебя вожатая искала."
    "Она боязливо посматривала на меня искоса, крепко обнимая книжку, будто надеялась спрятаться за ней."
    un "Почему ты такой грязный?"
    "Она обвела меня более пристальным взглядом и снова повторила вопрос:"
    un "Где ты был?"
    me "Я… Ну, это…"
    play sound eat_horn fadein 3 
    "В голове проносились тысячи отмазок, одна нелепее другой, но тут вдруг прозвучал спасительный сигнал на обед."
    th "Фух, пронесло."
    stop sound fadeout 2
    if alt_uvao_D5_hentai:
        dreamgirl "Вышло, значит, господин гусар, и рыбку съесть, и на кошечку залезть."
    me "Смотри-ка, обед уже! Давай поторопимся."
    "Я старался придать голосу небрежность, но, кажется, получилось не особо удачно. Оставив недоумевающую Лену на площади, я побежал к столовой, на ходу отряхивая пыльные шорты и рубашку."
    "Впрочем, я вовремя спохватился, что у меня на спине по-прежнему болтается изрядно опустевший рюкзак и хлопнул себя по лбу."
    th "Растяпа! Не хватало ещё в столовую с рюкзаком припереться. Вот бы знатно я спалился-то…"
    "Я поспешно изменил траекторию, чтобы забежать сначала в приютивший меня треугольный домик и избавиться от лишних улик."
    stop music fadeout 3
    jump alt_day5_uvao_lunch

# Д5 - Охота на Шурика в шахтах.    
label alt_day5_uvao_mines_sh:
    # $ routetag = "uv"
    # $ alt_chapter(5, u"Юля. Погоня")
    "Юля ускорила шаг, сжав мою ладонь чуть сильнее."
    "Вдруг она остановилась так резко, что я чуть не врезался в неё."
    play music music_list["doomed_to_be_defeated"] fadein 3
    uv "Стой. Не ходи дальше."
    me "Почему?"
    uv "Тихо."
    "Я не очень понял, что происходит, но решил подчиниться."
    "Юля некоторое время стояла, почти не шевелясь. Потом она прошептала:"
    uv "Он там, дальше."
    "Первым делом я решил осмотреться, куда же это Шурик нас притащил."
    window hide
    play sound match_lights
    scene bg int_mine_halt
    show match_lights   
    show uv dontlike at center behind match_lights
    with dissolve
    window show
    "Брошенные инструменты, какие-то разбитые ящики. Рельсы заворачивали вправо, и на повороте у стены лежала опрокинутая вагонетка."
    th "Наверно, слетела с рельсов на вираже. Катались они на ней, что ли?"
    "Я бросил спичку, чтобы не обжечь пальцы, и всё погрузилось во тьму…"
    window hide
    scene int_mines_halt_spotlight with fade2
    show uv black silhouette at cleft
    with dissolve
    window show
    "…но не полностью! Я заметил какой-то тусклый свет из-за поворота."
    window hide
    hide uv
    scene int_mines_halt_spotlight:
        zoom 1.0 xalign 0.5 yalign 0.4
        linear 2.5 zoom 1.7 xalign 0.8 yalign 0.4
    with dissolve
    window show
    th "Это Шурик там? Больше некому, вроде. Но какого черта ему здесь надо?"
    dreamgirl "Может, плутоний добывает?"
    th "Ка…какой плутоний?"
    dreamgirl "Оружейный, балда! Кирку взял и копает! Подкрадись и посмотри, чем он там занят, вместо того, чтобы гадать!"
    "Я последовал совету и осторожно двинулся вперёд, изо всех сил стараясь не споткнуться и не налететь на что-нибудь в темноте. Судя по тихому, но азартному посапыванию где-то в районе моего плеча, Юля держалась рядом."
    "Вскоре мы подобрались достаточно близко, чтобы выглянуть из-за угла."
    window hide
    scene cg d4_sh_sit with fade
    window show
    "Шурик действительно был там. С фонариком, с какой-то сумкой через плечо - он явно подготовился к походу в катакомбы."
    "Впрочем, выглядел он совершенно не как храбрый исследователь подземелий."
    "Скорчившись в какой-то нише в стене, он раскачивался и монотонно бормотал:"
    sh "…Пришёл… {w=1}А тут пусто… {w=1}Они говорили - налево… направо…{w=1} Куда вели?…{w=1} Я правильно шёл…{w=1} И никого…{w=1} Надо искать…{w=1} Она где-то здесь…{w=1} Тогда поверят…"
    "Его поведение меня довольно сильно испугало."
    th "Да у него совсем крыша съехала… Чёрт, и что с ним теперь делать? Нельзя же его тут оставить!"
    dreamgirl "А я думаю - можно. Лучше сначала самому выбраться, а за ним вернуться потом, с санитарами и успокоительным."
    th "Да хрен его потом тут найдёшь, в шахтах-то. Надо как-то увести его отсюда. Но как?"
    dreamgirl"Может, оглушить его? А потом как мешок картошки вынести."
    dreamgirl "Хлороформ бы для этого идеально подошёл. Подкрался бы сейчас к нему со спины, спросил бы, как пройти в библиотеку…"
    th "Ну ты-то хоть с ума не сходи.{w} Да и вообще, даже если удастся его оглушить, как я его понесу потом? Он же явно потяжелее мешка картошки будет."
    dreamgirl "Моё дело предложить. Попробуй с ним поговорить тогда, вдруг он не совсем псих?"
    window hide
    scene bg int_mine_coalface
    with dissolve
    window show
    "Я выпрямился и, изобразив дружелюбную улыбку, медленно вступил в круг света."
    me "Шурик! Привет! А ты что здесь делаешь?"
    show sh scared pioneer far
    "Шурик вздрогнул и вскочил, вжимаясь в стену."
    sh "Семён?… Это ты?…"
    me "Я, я. Не видишь что ли?"
    sh "А где… она?"
    me "Она? Кто - она?{w} Шурик, ты себя нормально чувствуешь?"
    show sh rage pioneer far
    sh "Не притворяйся! Я всё про тебя знаю! Ты - ненастоящий!"
    me "Шурик, подожди… Давай успокоимся…"
    sh "Не подходи! Говори, где она?!"
    me "Да кто она-то?"
    th "Он что, Юлю сюда пришёл искать?!"
    sh "Ты знаешь!…{w} Ты должен мне сказать…{w} Говори, где она!"
    show sh angry bar3 with dspr
    "Он угрожающе шагнул ко мне, сжимая в руках неведомо откуда взявшуюся железяку."
    me "Эй, эй! Ты что творишь, псих?! Это же я, Семён!"
    "Я попятился, а он продолжал приближаться, поднимая свое оружие."
    show sh angry bar with dspr
    sh "Ты…{w} мне…" 
    show sh mad bar2 close with dspr
    extend " всё…{w} расскажешь…"
    dreamgirl "Берегись!"
    window hide
    show cg d4_sh_stay
    window show
    "Он ринулся вперёд, целя мне в голову."
    "Я рефлекторно отпрыгнул назад,{w} но оступился на камнях и рухнул на спину."
    with vpunch
    window hide
    scene bg int_mine_coalface
    show sh angry bar3 far at cright
    show uv rage at cleft
    with dissolve
    window show
    "Внезапно с каким-то странным яростным мявом из-за камней выпрыгнула Юля и встала между нами, как бы защищая меня."
    show sh scared pioneer far at cright with dspr
    sh "А… а…{w=1.0} Она здесь?…{w=1.0} Я не…{w=1.0} нет…"
    "Он забормотал что-то неразборчивое, отступая в темноту."
    hide sh with easeoutright
    "Потом внезапно развернулся и со всех ног бросился прочь - туда, откуда мы пришли."
    show uv guilty close with dissolve
    "Юля наклонилась ко мне."
    uv "Вставай. Ты не ушибся?"
    me "Вроде бы нет."
    "Я поднялся и отряхнулся."
    show uv normal with dissolve
    th "И что теперь с этим полудурком очкастым делать? Искать его по всей шахте?"
    dreamgirl "Тебе железяки в голову показалось мало? Охота ещё лопатой по шее получить?"
    th "Ну блин, явно же не в себе человек! Если его бросить - навернётся, сломает себе что-нибудь, да так и сгинет.{w} А потом сюда пригонят спасателей, они тут всё перероют… И Юлю уже точно найдут!"
    me "Юля, ты не знаешь, куда он побежал?"
    uv "Обратно. Туда, откуда мы пришли."
    me "Проведёшь? Надо его отсюда выгнать, вернуть заблудшую душу в коллектив, так сказать."
    show uv smile with dspr
    uv "Хорошо, пошли. Я его буду выгонять."
    th "Как это она его будет выгонять?"
    dreamgirl "А ты заметил, что как только она выпрыгнула - Шурик сразу весь боевой пыл растерял и драпанул? Не так уж она проста, наша кошечка…"
    "Я подобрал фонарик Шурика - трофей, взятый в бою! Теперь не придётся спотыкаться в темноте и жечь спички."
    th "Он, получается, без света убежал? Тогда мы его быстро нагоним."
    dreamgirl "У него в сумке мог быть запасной. Он, похоже, изначально собирался лезть в подземелья, вот и подготовился."
    me "Хорошо, тогда идём скорее!"
    "Не медля, Юля взяла меня за руку и решительно потянула за собой."
    window hide
    play sound_loop sfx_slavya_run fadein 2
    scene bg int_mine_crossroad with fade2:
        linear 0.1 pos (5,3)
        linear 0.1 pos (5,0)
        linear 0.1 pos (-5,5)
        linear 0.1 pos (-5,0)
        repeat
    window show
    "Идти с фонарём было гораздо быстрее, чем плестись в полной темноте, спотыкаясь на каждом шагу. Почти бегом мы пересекли несколько перекрёстков - каждый раз Юля без тени сомнения выбирала направление."
    window hide
    scene bg int_mine_crossroad with fade2:
        linear 0.1 pos (5,3)
        linear 0.1 pos (5,0)
        linear 0.1 pos (-5,5)
        linear 0.1 pos (-5,0)
        repeat
    window show
    th "Интересно, это она на слух определяет, куда идти, или идёт по следу?"
    stop sound_loop fadeout 2
    window hide
    scene bg int_mine with fade2:
        linear 0.1 pos (5,3)
        linear 0.1 pos (5,0)
        linear 0.1 pos (-5,5)
        linear 0.1 pos (-5,0)
        repeat        
    show uv dontlike at cright
    with dissolve
    window show
    "Кажется, мы его нагоняли - мне иногда чудился какой-то свет впереди. Всё-таки у него нашёлся запасной фонарь."
    scene bg int_mine
    show uv dontlike at cright
    with dissolve
    "За очередным поворотом распахнулся длинный прямой штрек, в потолке которого зияла осыпавшаяся дыра."
    me "Это мы здесь спускались?"
    "Спросил я, пытаясь не сбиться с дыхания."
    uv "Нет, это другой вход. Тот там."
    "Она махнула рукой куда-то вправо. Там - значит там, сейчас это без разницы."
    uv "Он наверх вылез."
    me "Значит и мы за ним!"
    "Нас обоих разбирал азарт погони. Я с трудом представлял, что я буду делать, когда догоню его, но это всё казалось неважным - впереди добыча! И она уходит!"
    hide uv with dissolve
    "Юля с лёгкостью забралась наверх. Впрочем, тут же выяснилось, что самостоятельно последовать за ней я не могу, так что ей пришлось помогать мне забраться наверх. Не похоже, что её это очень уж обрадовало, но деваться ей было некуда."
    window hide
    scene bg int_catacombs_entrance
    with dissolve
    window show
    show uv upset close
    "Наверху оказался туннель - может быть, тот самый, который вёл из старого корпуса, а может и нет - мне они все казались слишком похожими."
    me "Это тот, первый туннель, в который мы через люк спустились?"
    uv "Да. Лестница там, а он побежал туда. Там тоже выход есть."
    "Она махнула вперёд, где брезжил какой-то свет."
#    hide uv
    window hide
    scene int_catacombs_entrance_light with fade
    "Мы продолжили преследование, и через пару минут увидели тот самый выход - обвалившийся потолок туннеля, выводящий на поверхность где-то в лесу, судя по виднеющимся наверху веткам деревьев. Я прикинул высоту - до поверхности где-то метра три."
    show uv dontlike at cright
    with dissolve
    window show
    uv "Он наверх вылез."
    "Констатировала Юля и, похоже, приготовилась взбираться по торчащим из стены крюкам, по которым были проложены кабели."
    me "Эй, погоди! Я тут не влезу! Как тут Шурик вообще мог вскарабкаться?"
    th "Вроде бы навыков ниндзя я за ним не замечал…"
    dreamgirl "Когда за тобой гонится крупный хищник из рода кошачьих, навыки ниндзя проявляются у всех."
    uv "Не знаю. Но он точно здесь вылез. И спускался тоже здесь."
    th "Может быть, он по верёвке спустился, а потом поднялся сам, и верёвку поднял?"
    me "Пошли по лесенке тогда поднимемся, как спускались."
    "Мы развернулись и побежали обратно."
    window hide
    scene bg int_catacombs_entrance
    show uv normal at cright
    with dissolve
    window show
    "Я первым заметил лестницу, вскарабкался по ней и откинул люк. Свет, бьющий через щели в забитом досками окне, ослепил меня и заставил зажмуриться."
    window hide
    stop ambience
label alt_day5_uvao_mines_sh_capture_debug: #Хомутаем Шурика и отлаживаем спрайты
    $ meet('mt', 'Голос')
    $ day_time()
    $ persistent.sprite_time = "day"
    scene int_old_building_day_uvao with flash
    play ambience ambience_forest_day fadein 3
    window show
    show uv shocked with dissolve
    "Судя по всему, свет ослепил не меня одного. Хоть тут с ней никаких сюрпризов."
    "Мы понемногу привыкали к солнечному свету, как вдруг снаружи раздался чей-то крик."
    show uv surprise with dspr
    mt "ШУ-У-У-УРИ-И-И-ИК!"
    th "Голос знакомый. Ольга Дмитриевна?"
    dreamgirl "Не мы одни тут на психованных изобретателей охотимся."
    "Мы с Юлей подобрались к окну и одновременно выглянули в щель между досками."
    window hide
    $ meet('mt','Ольга Дмитриевна')
    scene bg ext_old_building_day
    show mt pioneer normal veryfar:
        xalign 0.3 yalign 0.999 zoom 0.7
    show sl pioneer normal veryfar:
        xalign 0.4 yalign 0.999 zoom 0.7
    show el pioneer normal veryfar:
        xalign 0.5 yalign 0.999 zoom 0.7
    show un pioneer normal veryfar:
        xalign 0.6 yalign 0.999 zoom 0.7
    with dissolve
    window show
    "Спасательная команда в составе Ольги Дмитриевны, Слави, Лены и Электроника стояла посреди заросшего бурьяном двора и озиралась."
    th "И как они теперь будут его искать? Ещё нас заметят, чего доброго. Наверное, придётся нам снова прятаться в туннелях, или в бункере…"
    if alt_uvao_D5_hentai:
        dreamgirl "Ага-ага, спрятаться, и ещё разок объездить кошечку. Ну, чтоб время скоротать."
        "Я отмахнулся и от лезущей в голову похабщины, и от моего назойливого альтер эго."
    "Пионеры тем временем продолжали звать потерянного товарища на все лады."
    mt "Шу-у-у-у-ури-и-и-ик! Шу-у-у-ури-и-и-ик!"
    sl "Ау-у-у-у, Шу-у-ури-и-и-ик!"
    el "Эге-ге-е-ей, Шу-у-ури-и-ик!"
    th "Что-то я сомневаюсь, что он сам добровольно выйдет…"
    show sh angry bar3 veryfar behind mt with dissolve:
        xalign 0.99 yalign 0.999 zoom 0.55
    show mt pioneer surprise veryfar:
        xalign 0.3 yalign 0.999 zoom 0.7
    show sl pioneer surprise veryfar behind mt:
        xalign 0.4 yalign 0.999 zoom 0.7
    show el pioneer surprise veryfar:
        xalign 0.5 yalign 0.999 zoom 0.7
    show un pioneer surprise veryfar:
        xalign 0.6 yalign 0.999 zoom 0.7
    with dissolve
    "И в этот момент из зарослей появился сам виновник торжества. Чумазый, ободранный, и всё с той же приснопамятной железякой в руке."
    show sh angry bar veryfar with dissolve:
        xalign 0.9 yalign 0.999 zoom 0.7
    show mt pioneer scared3 veryfar:
        xalign 0.3 yalign 0.999 zoom 0.7
    show sl pioneer scared veryfar:
        xalign 0.4 yalign 0.999 zoom 0.7
    show el pioneer scared veryfar:
        xalign 0.5 yalign 0.999 zoom 0.7
    show un pioneer scared veryfar:
        xalign 0.6 yalign 0.999 zoom 0.7
    with dissolve
    "Не тратя времени на разговоры, он размахнулся своим рубилом и кинулся на замерших в испуге спасателей."
    show sh angry bar veryfar with dissolve:
        xalign 0.85 yalign 0.999 zoom 0.7
    th "Он сейчас точно кому-нибудь голову проломит!"
    hide sl
    hide mt
    hide el
    with moveoutleft
    "Отряд тем временем бросился врассыпную, кто куда - все, кроме Лены. Та в каком-то ступоре замерла неподвижно на месте."
    show sh angry bar veryfar:
        xalign 0.8 yalign 0.999 zoom 0.7
    with dspr
    "Я уже собрался было выскочить на подмогу, отлично понимая уже, что не успею…{w} Но в последний момент Лена пришла в себя и отскочила в сторону."
    show sh angry bar veryfar:
        xalign 0.68 yalign 0.999 zoom 0.7
    show un pioneer serious veryfar behind sh:
        xalign 0.6 yalign 0.999 zoom 0.7
    with dspr
    "Дальше время для меня словно бы замедлилось. Рука девочки легла на плечо Шурика, следом резкий удар по ногам…"
    show sh angry bar veryfar:
        yanchor 1.0 xanchor 1.0 xalign 0.68 yalign 0.999 zoom 0.7 transform_anchor True
        easeout 0.5 yanchor 1.0 xanchor 1.0 rotate -90.0
    with None
    show un pioneer surprise veryfar:
        xalign 0.6 yalign 0.999 zoom 0.7
    with dissolve
    "Очкарик по инерции пролетел вперёд и мешком рухнул в траву лицом вниз."
    hide sh
    show un pioneer surprise veryfar:
        xalign 0.75 yalign 0.999 zoom 0.7  
    with dissolve 
    dreamgirl "Круто! Как по учебнику провернула!"
    show sh angry bar3 veryfar:
        xalign 0.4 yalign 0.999 zoom 0.7
    show sl pioneer serious veryfar behind sh:
        xalign 0.47 yalign 0.999 zoom 0.7
    with dissolve
    "Шурик попытался было подняться, но подскочившая Славя вцепилась в его правое запястье, с неожиданной силой выкручивая за спину руку, всё ещё сжимающую импровизированное мачете."
    show sh pioneer rage veryfar:
        xalign 0.4 yalign 0.999 zoom 0.7
    show el pioneer sad veryfar behind sh:
        xalign 0.33 yalign 0.999 zoom 0.7
        xalign 0.33 yalign 0.999 zoom 0.7
    with dissolve
    "Наконец-то подоспевший на поле битвы Эл пришёл к ней на помощь. Вдвоём они обезоружили Арматур-Батыра и снова повалили его на траву лицом вниз."
    show un pioneer shy veryfar:
        xalign 0.75 yalign 0.999 zoom 0.7
    with dissolve
    "Я перевёл взгляд обратно на Лену. Она столбом стояла всё на том же месте, сама, кажется, пребывая в полном шоке от содеянного."
    show mt pioneer angry veryfar behind sh:
        xalign 0.2 yalign 0.999 zoom 0.7
    with dissolve
    mt "Ну-ка, переверните его!"
    "Выполняя отданное командным голосом распоряжение Ольги, Эл со Славей совместными усилиями перевернули брыкающегося и что-то неразборчиво хрипящего Шурика на спину."
    "Вожатая наклонилась над ним{w}… и отвесила несколько смачных оплеух!"
    stop music fadeout 5
    play sound sfx_face_slap
    $ renpy.pause(0.8)
    play sound sfx_face_slap
    $ renpy.pause(0.8)
    play sound sfx_face_slap
    show sh pioneer scared veryfar:
        xalign 0.4 yalign 0.999 zoom 0.7
    show sl pioneer normal veryfar behind sh:
        xalign 0.47 yalign 0.999 zoom 0.7
    show el pioneer normal veryfar behind sh:
        xalign 0.33 yalign 0.999 zoom 0.7
    with dissolve
    "Кажется, это подействовало - Шурик внезапно обмяк и перестал вырываться."
    dreamgirl "Вот что значит советская педагогика! А если б ногами - вообще был бы как шёлковый!"
    "Вожатая о чем-то спрашивала у Шурика, но тот явно не мог ничего объяснить, только растерянно озирался по сторонам."
    "Вскоре вся поисковая партия двинулась по направлению к лагерю. Шурика шатало туда-сюда, и шёл он только благодаря поддержке Эла и Слави."
    hide un
    hide sl
    hide sh
    hide el
    show mt pioneer normal veryfar:
        xalign 0.2 yalign 0.999 zoom 0.7
    with dissolve
    "Напоследок Ольга обернулась и пристально обвела взглядом поляну. По-видимому, не найдя ничего подозрительного, она повернулась и скрылась за деревьями вслед за остальными."
    hide mt with dissolve
    jump alt_day5_parking_back

# Д5 - Возвращение в лагерь через стоянку с палевом.
#
# Используется флаг alt_uvao_D4_viola_morning.
# Используется флаг alt_uvao_D5_hentai.
label alt_day5_parking_back:
    window hide
    $ routetag = "uv"
    $ alt_chapter(5, u"Юля. Пикник")
    scene int_old_building_day_uvao
    show uv grin
    with dissolve
    window show
    "Я с облегчением перевёл дыхание и покосился на Юлю. Та стояла донельзя довольная, разве что не подпрыгивая на месте."
    th "Ну да, она же говорила, что за нами, людьми, интересно наблюдать. А тут целый спектакль разыграли, как по заказу!"
    play sound sfx_stomach_growl
    "Однако тут желудок изволил напомнить о себе, и мои мысли приобрели совершенно другое направление:"
    th "Ё-моё, время-то уже к полднику, а я сегодня даже и не завтракал!"
    dreamgirl "Вот они, вредные привычки холостяцкой жизни. Нельзя же так не любить свой организм.{w} И вообще, жениться тебе надо, барин!"
    "Похоже, Юля правильно поняла ситуацию."
    play music music_list["went_fishing_caught_a_girl"] fadein 3
    show uv laugh with dspr
    uv "А, так ты есть хочешь! Угадала?"
    "Я смущённо пожал плечами и честно признался:"
    me "Ну да… Со вчерашнего вечера ничего не ел!"
    show uv normal with dissolve
    uv "Это плохо. Есть надо обязательно! Мне вот очень не нравится, когда я голодная."
    "Юля сразу посерьёзнела - видимо, в отличие от меня, о питании своего организма она заботилась своевременно."
    "Впрочем, почти тут же она снова заулыбалась:"
    show uv smile with dspr
    uv "А давай пойдём, поедим?"
    dreamgirl "Конечно-конечно. Чем там у нас кошки питаются? Как насчёт свежепойманной мышки? Вку-у-усная! Или белку предпочитаете?"
    "Моё альтер эго как в воду глядело, потому что следом Юля предложила:"
    uv "Здесь мыши знаешь какие вкусные. Пойдём наловим."
    "Юля облизнулась, обнажив острые зубки."
    dreamgirl "Соглашайся, соглашайся! Когда у тебя будет ещё шанс попробовать такой чудный деликатес!"
    me "Да как бы, понимаешь… Мы-то, люди, мышей обычно не едим…"
    "Я замялся, боясь обидеть Юлю отказом."
    show uv surprise with dissolve
    uv "Чудные вы. Вкусно же!"
    "Кажется, на лице Юли было написано искреннее недоумение: не любить мышей, слыханное ли дело?"
    uv "А чем тогда тебя покормить? Орехи будешь? Ягоды есть. И грибы ещё."
    "Услышав про более привычную еду я несколько воспрял духом. Ну да, она же говорила, что припасы делает.{w} Хоть что-то из этого должно оказаться съедобным!"
    me "Во-во! Ягоды с орехами подойдут."
    show uv normal with dissolve
    uv "Ну пошли тогда."
    "К некоторому моему удивлению, Юля направилась не к люку в туннель, а к выходу из здания. Заметив мой недоумённый взгляд, она пояснила:"
    uv "Пойдём здесь, так ближе!"
    hide uv with dissolve
    play sound sfx_open_metal_hatch
    "Я прикрыл на всякий случай крышку люка и поспешил за ней."
    window hide
    scene bg ext_old_building_day
    show uv normal
    with dissolve
    window show
    "Выйдя на поляну, Юля уверенно двинулась по направлению к тропинке, ведущей к лагерю."
    th "Надеюсь, она знает, что делает. Не хватало ещё столкнуться со всей этой компанией."
    dreamgirl "Правильно, совершенно это незачем! Мало того, что с расспросами привяжутся, так ещё и заставят Шурика носить, как пить дать!"
    stop ambience fadeout 3
    window hide
    scene bg ext_path2_day with dissolve
    show uv normal with dissolve
    play ambience ambience_forest_day fadein 3
    window show
    "Впрочем, очень скоро Юля уверенно свернула с тропинки"
    hide uv with easeoutleft
    extend " и нырнула в кажущиеся отсюда непроходимыми заросли кустарника."
    "Стараясь не отставать, я бросился следом"
    show uv normal close with dissolve
    extend "… И чуть не сбил Юлю с ног."
    "Она стояла на краю какой-то ямы, казавшейся отсюда бездонной - всё внутри скрывал густой мрак."
    me "Э-э… Нам сюда что ли?"
    "Я опасливо кивнул на оскаленную пасть провала.{w} К сожалению, Юля подтвердила мои опасения:"
    uv "Ну да, здесь спустимся."
    hide uv with easeoutbottom
    "Не успел я глазом моргнуть, как она присела на корточки и ловко спрыгнула куда-то вниз, в темноту."
    "Впрочем, снизу тут же послышался её жизнерадостный голос:"
    uv "Прыгай сюда, здесь неглубоко!"
    "Мне совсем не улыбалось снова лезть в эти проглятые подземелья с риском сгинуть там навсегда, но есть хотелось сильнее, чем жить."
    "Вздохнув, я бросил прощальный взгляд на море живой зелени вокруг и сиганул вниз."
    stop music fadeout 2
    stop ambience fadeout 3
    window hide
    scene int_catacombs_entrance_light with dissolve
    with vpunch
    play ambience ambience_catacombs fadein 3
    $ night_time()
    $ persistent.sprite_time = "night"
    window show
    "Однако всё оказалось не так уж плохо - пролетев от силы метра два или три, я довольно удачно приземлился на кучу земли вперемешку с камнями."
    "На ногах, впрочем, мне удержаться не удалось. Поднявшись и пытаясь отряхнуться, я осмотрелся."
    play music music_list["no_tresspassing"] fadein 3
    th "Опять туннель… Кажется, это тот самый провал, через который от нас Шурик сбежал.{w} Или другой? Здесь всё такое похожее."
    show uv normal with dissolve
    "Юля стояла рядом. В рассеянном свете, пробивавшемся через дыру в потолке, её глаза таинственно поблёскивали."
    show uv smile with dspr
    uv "Пойдём!"
    "Она уверенно двинулась куда-то в темноту. Мне не оставалось ничего другого, как последовать за ней."
    "После яркого дневного света в туннеле мне показалось ещё темнее, чем раньше.{w} Буквально через несколько шагов я перестал что-либо различать вокруг."
    window hide
    scene bg black with dissolve2
    window show
    "Решив не дожидаться, пока напорюсь на какую-нибудь железяку, я достал трофейный фонарь."
    window hide
    scene bg int_catacombs_entrance
    show uv upset
    with fade
    window show
    uv "Не свети в глаза."
    "Юля недовольно сощурилась, и я поспешил отвести фонарь в сторону."
    uv "Не понимаю, зачем вам эти светящие штуки. Здесь и так всё хорошо видно."
    me "Ну, наверное, у людей не такое хорошее зрение."
    window hide
    scene bg black with fade
    window show
    "Тем не менее я выключил фонарь и постоял несколько секунд, слепо тараща глаза."
    me "Тогда ты веди меня за руку, пока я к темноте не привыкну, хорошо?"
    uv "Хорошо!"
    "Впрочем, на этот раз Юле совсем недолго пришлось вести меня за собой - очень скоро она остановилась и, подумав немного, заявила:"
    uv "Ладно, включай эту штуку… Фонарь, вот. А то ты так еду не увидишь."
    stop music fadeout 5
    window hide
    scene bg int_catacombs_entrance
    show uv normal at left
    with flash
    window show
    "Я огляделся. Мы стояли всё в том же туннеле, но однообразная непрерывность стен в этом месте нарушалась нешироким проходом явно рукотворного происхождения."
    "За ним оказалось какое-то небольшое помещение."
    th "Наверное, это какая-то насосная или ещё что-нибудь такое."
    "Впрочем, первоначальное назначение этой каморки сейчас угадать было непросто.{w} Всё доступное пространство занимала теперь {i}еда{/i}."
    "Связки грибов, свисающие с потолка, ровные кучки орехов и ягод… Да, запасов здесь действительно хватило бы на всю зиму."
    "Однако, очень быстро мой энтузиазм насчёт предстоящей кормёжки дал трещину. Похоже, большая часть припасов лежала здесь уже не один год…"
    me "И ты здесь все свои запасы хранишь?"
    "Поинтересовался я больше для того, чтобы прервать наступившее молчание."
    show uv grin with dissolve
    uv "Нет, что ты! У меня ещё несколько таких мест есть. Я там тоже еду запасаю."
    "Она вручила мне горсть прошлогодних орехов."
    show uv smile with dissolve
    uv "Угощайся!"
    "И сама принялась хрумкать ими, выплёвывая скорлупки."
    th "Что же, орехи - выбор настоящих укротителей кошкодевочек…"
    "Положив фонарик на пол, я обречённо принялся бороться с орехами, пытаясь не сломать зубы. Ссохшиеся за год ядра по твёрдости также мало уступали скорлупе."
    show uv surprise with dissolve
    "Вдруг Юля замерла, широко раскрыв глаза."
    me "Что такое? Что-то случилось? Ты кого-то почуяла?"
    uv "Слу-у-ушай! Раз ты придумал, как ту дверь железную открыть…"
    hide uv with easeoutleft
    "Она бросилась в угол, принявшись что-то там ожесточённо ворошить, и через несколько секунд вернулась назад."
    show uv smile at left with easeinleft
    uv "Вот! Я это в столовой взяла. Я точно знаю, что там еда внутри!"
    show uv guilty with dissolve
    uv "Вот только достать не могу…"
    "Я уставился на консервные банки в её руках."
    show uv normal with dissolve
    uv "Смотри, они тоже железные, как дверь! Открой, а?"
    "Я аккуратно положил остатки орехов обратно в кучу на полу."
    play music music_list["went_fishing_caught_a_girl"] fadein 3
    me "Знаешь, нам сказочно повезло! Я как раз крупный специалист в этой области! Разумеется, я их открою!"
    show uv laugh with dissolve
    "Тут я обратил наконец внимание, что свет от фонаря совсем пожелтел и на глазах становится всё слабее."
    show uv smile with dissolve
    me "Вот только давай отнесём их куда-нибудь, где есть свет - фонарик скоро погаснет совсем, а в темноте их открыть не получится."
    me "Пойдём к тому месту, где мы в туннель спустились? Там светло."
    "Юля согласно кивнула, и мы, сложив банки в рюкзак, отправились обратно."
    dreamgirl "А потом окажется, что консервы эти времён войны. Какие там первые симптомы ботулизма?"
    th "Тьфу на тебя! Не смей портить настроение!"
    window hide
    scene int_catacombs_entrance_light with fade
    window show
    show uv smile with dissolve
    "К моей радости, инсинуации Шизы оказались беспочвенными - у консервов даже не кончился срок годности."
    "Я кое-как открыл перочинным ножом сардины в масле для Юли, и вступил в схватку с банкой, таящей в своих недрах тушёную говядину, если верить этикетке."
    show uv normal with dspr
    "Юля с опаской принюхалась к содержимому банки."
    uv "А это точно можно есть? Ты уверен?"
    "Я пожал плечами и, подцепив ножом кусочек рыбы, отправил его в рот."
    me "Нормальные консервы. Если тебе не понравится - я их сам с удовольствие съем."
    "Глядя на меня, Юля наконец тоже решилась."
    show uv grin with dissolve
    uv "Умм! Вкусно!"
    "Я ожесточённо заработал ножом, и следующие несколько минут мы оба были страшно заняты, уплетая содержимое всех банок подряд."
    show uv smile with dissolve
    stop music fadeout 3
    "Наконец с едой было покончено и я сыто привалился к стене туннеля."
    th "Ну вот, поели – теперь можно и поспать…"
    dreamgirl "Ты что, сдурел? Тебя уже давно в лагере хватились! Пора ноги делать.{w} И кстати, насчёт ног…"
    "Действительно, давешний синяк набух и почернел, а нога снова начала ныть. Надо будет к медсестре сходить, наверное."
    "Со страшной силой навалилась усталость, накопившаяся за день. Впрочем, Юля выглядела бодрой как огурчик."
    me "Юль, а здесь есть какой-нибудь короткий путь в лагерь?"
    show uv normal with dspr
    uv "Да. Пойдём."
    window hide
    scene bg black with fade
    window show
    "И снова тёмные туннели и штольни, которые мне уже осточертели за этот день."
    "Я уже давно перестал ориентироваться и просто послушно шагал за своей проводницей в полной темноте, не решаясь лишний раз включить севший фонарик."
    window hide
    scene bg int_catacombs_entrance
    show uv normal at left
    with fade2
    window show
    "После невообразимого количества развилок мы наконец оказались в каком-то пусть чуть-чуть, но освещённом туннеле."
    "Довольно быстро я понял, что свет проникает сюда через какое-то отверстие в потолке. Прямо под ним на стене обнаружились металлические скобы."
    uv "Здесь наверху есть люк. Только он на замок заперт. У меня есть ключ, но замок всё равно не открывается. Я уже пробовала."
    me "Ключ? У тебя есть ключ?"
    uv "Да, я в туннеле недалеко нашла. Давно ещё."
    dreamgirl "Ключ-с, моя прелесть. Мы нашли его в тёмных туннелях-с. Мерзские Семены-с хотят отобрать у нас нашу прелесть-с."
    uv "Но ты ведь откроешь, да? Он ведь тоже железный!"
    th "Кажется, Юля свято уверовала в мою способность открывать всё железное… Забавно."
    me "Давай я попробую."
    window hide
    scene int_mine_exit_day with dissolve
    window show
    "Поднявшись по узкой шахте, я оказался перед металлической решёткой с вваренным в неё замком."
    "Как ни странно, но ключ подошёл, хотя провернуть его до конца мне долго не удавалось."
    "Стоя на металлической скобе и упёршись спиной в стенку шахты, я принялся крутить ключ туда-сюда, одновременно тряся решётку."
    th "Эх, масла бы сюда какого-нибудь! Да хоть бы даже от тех сардин, всё равно!"
    play sound sfx_open_metal_hatch
    "Наконец замок сдался, и решётка заскрипела, поворачиваясь на ржавых петлях. Не похоже, чтобы этим ходом регулярно пользовались."
    stop ambience fadeout 3
    window hide
    scene bg ext_camp_entrance_sunset with dissolve2
    play ambience ambience_camp_entrance_evening fadein 3
    $ day_time()
    $ persistent.sprite_time = "sunset"
    window show
    th "Интересно, где это я?"
    "Выбравшись наружу, я обернулся…{w} И уткнулся носом в гипсовые ноги какой-то скульптуры. Похоже, оголовок вентиляцтонной шахты служил ей постаментом."
    "Я обалдело завертел головой.{w} Подозрительно знакомые ворота, ещё одна скульптура…"
    th "Да это же выход из лагеря! Ничего себе… Интересно, много ли ещё здесь таких ходов?"
    show uv smile far at fright with dissolve
    "Юля вылезла вслед за мной и теперь потягивалась в свете вечернего уже солнца."
    th "Я что, провёл в шахтах весь день?! Славно прогулялся, ничего не скажешь."
    "Заперев решётку, мы отошли к воротам."
    show uv normal at cright with dissolve
    me "Ну, я пойду?"
    uv "Конечно."
    me "Эмм… Спасибо тебе."
    show uv smile with dspr
    "Юля улыбнулась."
    uv "Приходи ещё. Было весело."
    show uv smile close at cright with dissolve
    "Она поднялась на цыпочки и неловко потёрлась носом о мою щёку."
    th "Кажется, это ещё один поцелуй? Наверное, пора всё-таки провести небольшой урок…"
    me "Смотри, это чуть-чуть иначе делается. Давай я тебе покажу."
    show uv surprise close with dissolve
    "Я мягко прикоснулся губами к её щеке."
    if alt_uvao_D5_hentai:
        extend " Потом ещё раз, в уголок губ…{w} Смешно, мы такое устроили в бункере, а банально поцеловаться - забыли!"
        "Руки сами обвили тонкую талию, я притянул ее к себе, наши губы соединились…"
        th "Запоздалый первый поцелуй. Ну лучше уж поздно, чем никогда."
        "Не удержавшись, я провел рукой по пушистому, удивленно поднятому хвосту."
    show uv smile close with dissolve
    uv "Мм-м-р-р… Мне нравится. Подожди, я тоже попробую!"
    "Юля оказалась способной ученицей, и я уже собирался перейти к следующей части урока…"
    dreamgirl "Послушай, все эти поцелуйчики очень трогательны и романтичны, но не лучше ли заняться этим в каком-нибудь месте поспокойнее?{w} И вообще, ты куда-то там собирался идти, или я ошибаюсь?"
    "Я со вздохом остранился от Юли."
    if alt_uvao_D5_hentai:
        show uv guilty with dissolve
        me "Мне тоже очень нравится, но я боюсь, что нас кто-нибудь здесь увидит."
        uv "Жалко. А потом можно будет ещё так сделать?"
        me "Ну конечно же можно! Даже нужно!"
    else:
        me "Ну вот как-то так у нас принято."
        uv "И всё? Жалко."
    show uv normal with dspr
    uv "Принесёшь мне ещё булочек? Я буду ждать."
    th "Да, сначала булочки, а потом уже Семён..."
    me "Обязательно. Я приду к тебе завтра."
    show uv smile with dspr
    "Внезапно мне в голову пришла одна безумная идея. Решив не откладывать её на потом, я попросил Юлю:"
    me "Не двигайся. Хочу тебя сфотографировать."
    show uv surprise with dissolve
    "Юля навострила уши и удивлённо посмотрела на меня."
    uv "Сфотографировать? Это как?"
    dreamgirl "…Я вас сэйчас обоих сфатаграфырую!"
    me "Ну, это… Эээ…"
    th "И как ей объяснить, интересно?"
    me "Эээ, это такая штука… Делает картинку, на которой ты как настоящая."
    uv "Настоящая? Ещё одна я?"
    me "Ну… Давай я просто тебе покажу. Только стой смирно."
    show uv guilty at center with dissolve
    "Я навёл камеру на Юлю, а она продолжала удивлённо смотреть на меня."
    dreamgirl "Внимание, сейчас вылетит мышка! Летучая!"
    window hide
    show frame at truecenter:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
    show cam_ui at truecenter:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 0.8 zoom 1.0 xalign 0.5 yalign 0.5
    window hide
    $ renpy.pause(2, hard=True)
    play sound snap fadein 0
    scene white with flash
    $ renpy.pause(2, hard=True)
    scene bg ext_camp_entrance_sunset
    show uv surprise2 at center
    with dissolve
    window show
    "Вспышка, Юля подскочила от неожиданности, а я скорее полез в телефон посмотреть снимок."
    window hide
    play sound sfx_scary_sting
    scene uv_photo_galaxy with dissolve
    $ renpy.pause(2, hard=True)
    scene bg ext_camp_entrance_sunset
    show uv surprise2 at center
    with dissolve
    window show
    "…"
    dreamgirl "…"
    "Даже словоохотливое обычно альтер эго не нашлось, как прокомментировать получившийся результат."
    show uv smile with dissolve
    "Юля уже опомнилась от вспышки и с любопытством заглядывала мне через плечо."
    uv "Получилось? Покажи ещё одну меня!"
    "Я молча повернул смарт экраном к ней."
    "Юля изучила снимок и кивнула."
    show uv grin with dissolve
    uv  "Хорошо получилось. Почти как я."
    show uv smile with dissolve
    th "И ни капли удивления, главное."
    dreamgirl "Ну и что это такое было?"
    th "Вообще-то я рассчитывал, что это ты мне что-нибудь подскажешь!"
    dreamgirl "Извини, но сейчас я понимаю не больше твоего. {w} Разве что тень на силуэте кажется смутно знакомой… Нейронная сеть?{w=0.8} Разряды плазмы?{w=0.8} Звёздное небо?"
    th "Ну да, скажи ещё - карта Вселенной!"
    play music music_list["doomed_to_be_defeated"] fadein 0
    show mt rage panama pioneer far at fleft with dspr
    with hpunch
    show uv rage at right with ease
    mt "Семён!"
    "Внезапное появление вожатой стало для нас полной неожиданностью."
    hide uv with easeoutright
    play sound_loop sfx_tree_branches
    show mt rage panama pioneer at left with dissolve
    "Как и вчера, Юля мгновенно метнулась в кусты, но в этот раз она даже не пыталась сделать это тихо - сквозь кусты словно бы тигр ломился, не разбирая дороги."
    stop sound_loop
    "Наконец всё стихло. Стиснув зубы, я приготовился к обороне."
    show mt angry panama pioneer at center with dissolve
    mt "Семён! Что это такое?! Я требую объяснений!"
    me "Вы о чём, Ольга Дмитриевна?"
    "Вежливо поинтересовался я."
    mt "Не придуривайся! Я всё видела! Ты с этой кошкой!…"
    "От возмущения вожатая даже начала задыхаться."
    "Однако, теперь уже злиться начал и я тоже."
    if alt_uvao_D4_viola_morning:
        me "Вот я как знал, вчера не стал к Вам с этим подходить!"
        me "Увидел её издали, хотел было посоветоваться, да подумал, что ничего кроме истерики не дождусь!{w} Вижу, верно угадал!"
    else:
        me "Значит, когда я вчера вам рассказывал о своём видении, это была чушь и попытка отвильнуть от зарядки, а сегодня выясняется, что вы про неё знаете!"
        me "Нехорошо, Ольга Дмитриевна, нехорошо."
    show mt rage panama pioneer with dspr
    mt "Не смей так разговаривать со старшими!"
    "Лицо вожатой приобрело нездоровый лиловый цвет, а глаза готовы были выскочить из орбит."
    "На секунду мне показалось, что её сейчас удар хватит, но Ольга судорожно перевела дыхание и кое-как взяла себя в руки."
    show mt angry panama pioneer with dspr
    mt "Поговорим об этом позже, а сейчас марш на ужин!"
    "Тяжело дыша от злости, я гордо задрал голову и прошествовал мимо разъярённой вожатой в ворота.{w} Та пошла следом за мной, намереваясь, судя по всему, конвоировать до самой столовой."
    stop music fadeout 5
    window hide
    scene bg ext_square_sunset with fade
    show mt angry panama pioneer at fleft
    with dissolve
    play ambience ambience_camp_center_evening fadein 1
    window show
    play sound eat_horn fadein 2
    "Сигнал к ужину настиг нас на площади. К этому времени я уже немного поостыл."
    stop sound fadeout 2
    th "Вообще, если подумать, то надо сначала поужинать, а потом уже ругаться с Ольгой, или с кем там ещё придётся."
    dreamgirl "Ну наконец-то ты за ум взялся! Мало ли как оно может обернуться - вдруг тебя в карцер решат посадить, так хоть наешься напоследок."
    "Под сомнительные напутствия подсознания и недовольное пыхтение вожатой за спиной я направился было в сторону столовой."
    "Однако Ольга, похоже, к этому времени тоже немного успокоилась, потому что заявила вдруг относительно вменяемым голосом:"
    mt "Семён, погоди-ка! Зайдём к умывальникам сначала. Не хочу, чтобы ты весь отряд распугал."
    "Я окинул взглядом свою одежду и руки."
    th "Мда-а… Надо думать, лицо у меня примерно такое же чистое. Так не только отряд, так весь лагерь распугать можно."
    dreamgirl "Не понимаешь ты своего счастья! Вся столовая была бы в твоём распоряжении!"
    th "Кто-то только что мне про карцер втирал, кажется? Сгинь!"
    window hide
    scene ext_washstand_sunset with fade
    show mt angry panama pioneer at fleft
    with dissolve
    play sound sfx_open_water_sink
    $ renpy.pause(1)
    play sound_loop sfx_water_sink_stream
    window show
    "Под конвоем вожатой я дошёл до умывальников и наскоро умылся."
    stop sound_loop
    play sound sfx_close_water_sink
    pause(1)
    "Вообще, после сегодняшней беготни я с удовольствием ополоснулся бы до пояса, пусть и ледяной водой, но устраивать для вожатой сеанс стриптиза не собирался. Просто из принципа.{w} Не заслужила."
    "Отыскав в кармане носовой платок, я кое-как вытерся им и вопросительно посмотрел на Ольгу.{w} Та критически оглядела меня и поморщилась, но ничего не сказала, лишь кивнула в направлении столовой."
    window hide
    scene bg ext_dining_hall_near_sunset with fade
    show mt angry panama pioneer at fleft
    with dissolve
    window show
    "Уже подойдя к столовой, я подумал, что помимо умывания можно было бы и рюкзак отнести домой, но связываться с  вожатой не хотелось."
    "Под любопытные взгляды пионеров я вошёл в столовую."
    jump alt_day5_uvao_evening

# Д5-Обед
#
# Используется флаг выхода на тру-энд alt_uvao_true.
label alt_day5_uvao_lunch:
    window hide
    $ alt_chapter(5, u"Юля. Обед")
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_day with fade
    window show
    "В столовой как всегда было не протолкнуться. Я осмотрелся."
    show mi normal pioneer far at fright with dissolve
    "Мику сидела и одиноко ковыряла ложкой перловую кашу."
    show dv smile pioneer far at right
    show us laugh pioneer far at cright
    with dissolve
    "Алиса с интересом слушала хохочущую Ульяну."
    show el sad pioneer far at fleft
    show sl normal pioneer far at left
    show mt sad pioneer far at cleft
    with dissolve
    "Электроник взволнованно объяснял что-то Славе и Ольге Дмитриевне."
    show cs normal far at center with dissolve
    "Сидящая чуть в стороне медсестра явно прислушивалась к речам пионера."
    if alt_uvao_true:
        "Заметив, что я вошёл в столовую, она одарила меня пристальным взглядом и слегка кивнула на свободное место напротив."
        hide dv
        hide us
        hide mi
        with dissolve
        "Так же сдержанно кивнув в ответ, я направился за подносом."
        dreamgirl "Прямо-таки конспиративная встреча Тульева и Исаева!{w} Надеюсь, ты уже выбрал пути отхода на случай провала? Капсула с ядом наготове?"
        "Ольга косо глянула на меня, но ничего не сказала, продолжив вполголоса что-то обсуждать со Славей и Элом."
        hide sl with easeoutleft
        "Впрочем, Славя почти сразу поднялась со своего места и куда-то убежала."
        show cs normal2 close at center
        hide el
        hide mt
        with dissolve
        "Стоило мне сесть напротив Виолы, как она тут же громко поинтересовалась у меня:"
        show cs smile2 close at center with dspr
        cs "Ну что, пионер, как самочувствие? Давай рассказывай! Предписания все выполнял?"
        dreamgirl "Знаем мы, какие предписания она имеет в виду! Ничего, ты можешь собой гордиться, непокобелимый ты наш!"
        "Почувствовав, что краснею, я замямлил:"
        me "Да я… Спасибо, лучше стало…"
        show cs normal2 close at center with dspr
        "Впрочем, следовало признать, что прикрытие Виолетта придумала довольно удобное."
        "Пристроив на лице выражение «мне неловко обсуждать такое при всех» и говоря вполголоса, я кратко отчитался об утренней экспедиции, попутно стараясь не забывать про суп."
        "Услышав про Шурика, Виола нахмурилась:"
        cs "Что?! Он там наедине с визуализацией остался?"
        me "Что такого-то? Юля ему всё равно на глаза не покажется."
        cs "Ей и необязательно показываться. Если твоя Юля решит, что он ей как-то угрожает…"
        "Она бросила взгляд на Ольгу и продолжила:"
        cs "Семён, необходимо как можно скорее его найти и увести оттуда."
        cs "Одного тебя отправлять глупо, да это и не понадобится.{w} Его ещё за завтраком хватились, так что сейчас начнут искать уже всерьёз."
        cs "Присоединишься к поисковой партии и проследишь, чтобы там всё прошло гладко, без ненужных контактов… пионер."
        me "Но…"
        "Не дожидаясь моего ответа, Виола обратилась к вожатой:"
        show cs normal close at center with dspr
        cs "Оль, тут вот пионер говорит, что видел сегодня Александра за пределами лагеря."
        th "Нет, ты подумай, какая наглость! Снова без меня меня женили!"
        show el sad pioneer at fleft
        show mt sad pioneer at cright
        show cs normal2 at fright
        with dissolve
    else:
        "Я решил сесть рядом с Мику."
        hide dv
        hide us
        with dissolve
        th "Лучше уж потерять душевное равновесие, чем физическое здоровье, сев с рыжими."
        "Однако только я двинулся было в ту сторону, как меня окликнули:"
        mt "Семен! Подойди сюда."
        hide sl with easeoutleft
        "Ничего не оставалось кроме как подчиниться, тем более Славя уже ушла.{w} Я сел на освободившееся место."
        hide dv
        hide us
        hide mi
        show el sad pioneer at fleft
        show mt sad pioneer at cright
        show cs normal2 at fright
        with dissolve
        mt "Семен, ты не… Почему не в форме?"
        show mt angry pioneer at cright with dspr
        "Она кивнула на незавязанный галстук."
        me "Ну, я…"
        "Придумывая отмазку, я закашлялся, делая вид, что поперхнулся супом, но внезапно на помощь ко мне пришла Виолетта:"
        cs "Господи, Оль, сейчас не это важно!"
        me "Что-то случилось?"
        show mt sad pioneer at cright with dspr
        "Не успела Ольга Дмитриевна открыть рот, как Электроник воскликнул:"
        el "Шурик пропал!"
        me "Как пропал? Куда?"
        mt "Вот мы и решили поинтересоваться у тебя. Ты его видел сегодня?"
        me "Кстати, видел."
        show el surprise pioneer at fleft
        show mt surprise pioneer at cright
        with dspr
        "Электроник и Ольга Дмитриевна выпучили глаза. Даже тонкая бровь Виолы поползла вверх."
    mt "И где же?"
    me "В лесу. Он выглядел как-то потрепанно…"
    show el normal pioneer at fleft
    show mt normal pioneer at cright
    with dspr
    mt "Что он там делал?"
    if alt_uvao_true:
        "Она покосилась на Эла и добавила:"
        mt "…И кстати, что ты, Семён, там делал?"
    else:
        extend " Что ты там делал?"
    me "Ну, я гулял… Думал о своем…"
    if alt_uvao_true:
        show cs normal at fright with dspr
        cs "У Семёна лёгкая э-э… гипостеническая неврастения, так что я прописала ему прогулки на свежем воздухе в одиночестве."
        "Вмешалась Виолетта, обращаясь больше к Электронику, чем к Ольге Дмитриевне."
        show cs normal2 at fright with dspr
    me "Да не важно ведь! Важно, что {i}он{/i} там делал?"
    el "В лесу же ничего интересного! Хотя…"
    "Теперь все смотрели на Электроника."
    el "Может он ушел в старый лагерь?"
    dreamgirl "Не спались! Лицо проще!"
    me "А что он там мог забыть?"
    el "Я не знаю… Говорят, там были не то подвалы какие-то, не то бомбоубежище со старым оборудованием."
    "Я покрылся холодным потом."
    if not alt_uvao_true:
        cs "С тобой всё в порядке, пионер? Какой-то ты бледный."
        "Я не сразу понял, что обращаются ко мне."
        me "Да, я просто… Проголодался."
        "Я с остервенением начал проталкивать в себя пресную перловку, как бы подтверждая свои слова.{w} Было противно, но я ощущал три пары наблюдающих за мной глаз."
    "Обстановку разрядила прибежавшая Славя."
    show sl sad pioneer at cleft with dissolve
    sl "Девочки из младшего отряда видели, как Шурик выходил со склада. Сказали, что он шёл в лес."
    mt "Что-нибудь еще?"
    sl "Да, они говорят, что он выскочил, ничего с собой не взяв. А Женя ещё сказала, что он спрашивал у нее батарейки для фонарика. И что она-то и отправила его на склад."
    show el surprise pioneer at fleft with dspr
    el "И она ничего не заподозрила?"
    me "А чего здесь подозревать? Руководитель технического кружка и батарейки - всё логично."
    show el normal pioneer at fleft with dspr
    "Ольга Дмитриевна отмахнулась от меня:"
    mt "Это дело десятое. Спасибо, Славя. Теперь мы знаем, что Шурик ушел в лес с фонариком."
    el "Это как раз подтверждает теорию о том, что он направился в старый корпус."
    el "Сами посудите, если бы он решил прогуляться в лесу, то он бы взял спальник или еще что-то. В конце концов, у него же аналитический ум, он привык всё продумывать…"
    mt "Что ж, теперь на повестке дня вопрос: что делать дальше?"
    el "Как что? Искать его надо!"
    "Электроник демонстративно ударил по столу кулаком."
    mt "А кто искать пойдет?"
    show cs normal at fright with dspr
    cs "От меня помощи не ждите, у меня половина четвёртого отряда животами мается. Наелись вчера зелёных ягод, как дети малые."
    show cs normal2 at fright with dspr
    show sl normal pioneer at cleft with dspr
    sl "Борис Александрович сказал, что у него инвентаризация. Говорит, приказ директора лагеря."
    "Я подивился осведомленностью Слави. Впрочем, от нее другого ожидать не приходилось."
    "Ольга Дмитриевна не отрываясь смотрела на меня."
    mt "Отлично, сформируем поисковый отряд. Выступаем после обеда. Нас четверо, думаю хватит."
    sl "Простите, но я не могу. Я же предупреждала…"
    cs "Да-да, прости Ольга, но сегодня она моя пациентка."
    show sl shy pioneer at cleft with dspr
    "Славя покраснела."
    "На лице Электроника, впрочем как и на моем, красовалось недоумение."
    "Виола и Славя покинули столик и направились к выходу."
    hide sl
    hide cs
    with dissolve
    if alt_uvao_true:
        th "Если уж я собираюсь идти на поиски, как просила Виола, то сейчас самое время прояснить этот вопрос."
        me "Э-э… Судя по всему, мне тоже придётся идти?"
        mt "Ты должен, Семен! Это твой пионерский долг. Где это видано, чтобы пионер бросал товарища в беде?"
        "Я возможно небрежнее пожал плечами:"
        me "Да нет, в принципе-то я не против…"
        mt "Вот и отлично!"
        "Отрезала Ольга Дмитриевна."
    else:
        "И тут меня осенило, к чему клонит вожатая."
        me "Э-э, нет!"
        mt "Ты должен, Семен! Это твой пионерский долг. Где это видано, чтобы пионер бросал товарища в беде?"
        me "Но…"
        show mt angry pioneer at cright with dspr
    mt "Тем более ты последний, кто его видел. Сыроежкин тоже пойдет с нами, ты ведь знаешь дорогу туда?"
    "Она уже обращалась к Электронику."
    el "Ну… В теории…"
    show mt normal pioneer at cright with dspr
    mt "Еще одного человека в группу я, так и быть, найду. И сама пойду с вами для контроля. Встретимся на площади, там всё обсудим."
    "Она встала из-за стола и направилась к выходу."
    hide mt with dissolve
    el "Я пойду, подготовлюсь."
    "Электроник тут же засуетился и чуть ли не бегом покинул столовую."
    hide el with dissolve
    dreamgirl "С Жужелицей попрощаться пошел, не иначе…"
    "Я мысленно кивнул Шизе."
    if alt_uvao_true:
        "Аппетита больше не было. Да и какой сейчас аппетит?{w} Если Юлю обнаружат…"
    else:
        "От перловки было противно, и я залпом выпил компот, стараясь перебить вкус."
        "Аппетита больше не было. Да и какой сейчас аппетит?{w} Юлю обнаружат, потом разбор полетов, а затем…"
    "Я встал, убрал посуду с так и не начатой рыбной котлетой и направился к выходу."
    stop ambience fadeout 1
    jump alt_day5_capture_sh_together

# Д5-Совместная охота на дикого Шурика
#
# Используется флаг lt_uvao_true.
# Используется флаг alt_uvao_D4_lunch_un.
label alt_day5_capture_sh_together:
    window hide
    $ routetag = "uv"
    $ alt_chapter(5, u"Юля. Поиски Шурика")
    $ persistent.sprite_time = "day"
    $ day_time()
    play ambience ambience_camp_center_day fadein 3
    play sound_loop ambience_medium_crowd_outdoors fadein 2
    scene bg ext_dining_hall_away_day with fade
    window show
    # TODO: Рюкзак
    "Выйдя из столовой, я машинально направился было к своему домику за рюкзаком, но быстро спохватился."
    th "Зачем он мне? Идём мы ненадолго, вряд ли что-то из него может понадобиться.{w} Только таскать зря."
    "Так что я развернулся и зашагал в сторону площади."
    th "Интересно, кого Ольга в отряд четвёртым припашет?{w} Что-то мне подсказывает, что кроме Слави никто добровольно на это не подписался бы…"
    window hide
    scene bg ext_square_day with dissolve
    $ renpy.pause(1)
    window show
    "На площади никого из остальных участников спасательной экспедиции ещё не было."
    "К этому времени я успел успокоиться насчёт возможной поимки Юли и последующих неприятностей."
    th "В конце-то концов, до сих пор её не поймали - может и сейчас всё обойдётся. Тем более, я смогу вмешаться, если что."
    "Я развалился на скамеечке возле вечно поправляющего очки Генды, лениво размышляя о вынужденных добровольцах, таинственной болезни Слави…{w} И о том, что зря я не доел свой обед."
    th "И понесла же этого Шурика нелёгкая шляться где не надо!"
    th "Интересно, случайно он в старый корпус потащился именно сегодня, или всё-таки следил за мной?{w} А если следил - то зачем?"
    show un normal pioneer at center with dissolve
    un "Привет…"
    play music music_list["i_dont_blame_you"] fadein 3
    "Я вздрогнул, вырванный из потока своих размышлений незаметно подошедшей Леной."
    me "Привет…"
    un "Мне Ольга велела на площадь прийти…"
    "Надо думать, мой вгляд наглядно демонстрировал полное отсутствие мыслительных процессов, потому что она добавила:"
    un "Сказала, что я с вами пойду Шурика искать…"
    show un shy pioneer with dspr
    "Тут Лена порозовела и уставилась куда-то себе под ноги."
    me "Э-э-э… Ну да, ясно…"
    "Я наконец-то вышел из ступора и поспешно подвинулся, освобождая на скамейке место:"
    me "Садись, подождём вместе.{w} Если ты не против, конечно…"
    show un normal pioneer close at fleft with dissolve2
    "Я не удивился бы, окажись Лена очень даже против, но она неожиданно спокойно кивнула и присела на краешек скамейки."
    th "Ну конечно, кого ещё припахать в поисковую партию?{w} Правильно - того, кто не сумеет отвертеться!"
    if alt_uvao_D4_lunch_un:
        "Мне вдруг вспомнился вчерашний обед."
        th "Как бы моя любезность мне же не вышла боком.{w} Что прикажете делать, если она сейчас отморозится, как вчера, или решит довести до конца своё признание, о чём бы оно ни было?"
        th "Только этих хлопот мне сейчас не хватает! Сбежать от неё что ли, пока не поздно?"
        dreamgirl "Ты лучше подумай, что станешь делать, если решишь-таки сбежать, а она возьмёт и за тобой погонится!"
        th "Кажется, самое разумное, что можно сделать сейчас - это держать рот на замке."
    else:
        me "Э-э…"
        "На этом моё красноречие иссякло."
    "С минуту мы молчали.{w} Впрочем, Лену, кажется, это не слишком тяготило."
    show un smile pioneer close with dspr
    "Внимательно посмотрев на мою смущённую физиономию, она вдруг слегка улыбнулась, поёрзала, устраиваясь поудобнее, и мечтательно уставилась в небо, откинувшись на спинку скамейки."
    th "Кажется, мы поменялись ролями. Теперь моя очередь отмораживаться, а Лена радуется жизни…"
    "Я растерянно перевёл взгляд вверх. По выцветшему от жары послеобеденному небу лениво ползли редкие облака.{w} Где-то в кустах неподалёку перекликались птицы."
    "Мимо вальяжно прошествовали к своим домикам несколько пионеров из младших - переваривать обед."
    "Так мы и сидели молча посреди сонного лагеря - Лена с едва заметной улыбкой на губах, спокойно разглядывающая облака, и я, растерянно переводящий взгляд с девочки на небо и обратно."
    "Насмешливо взирающий на нас Генда явно понимал в происходящем больше моего, но делиться секретами не спешил."
    stop music fadeout 3
    show mt normal panama pioneer far with dissolve
    mt "Ага, вы уже здесь, это хорошо!"
    show mt angry panama pioneer
    show un normal pioneer close
    with dissolve
    extend " А где Сыроежкин? Его почему нет?!"
    "Не могу сказать, что я так уж рад был видеть Ольгу Дмитриевну, а слышать - и того меньше."
    show mt normal panama pioneer with dissolve
    "С другой стороны, наедине с Леной сидеть было ещё хуже.{w} Трудно находиться рядом с человеком, чьи действия не можешь предугадать."
    "Немного жаль только было видеть, как с появлением вожатой девочка снова спряталась, словно улитка в раковину, за свою обычную маску и теперь любовалась уже не небом, а плитами под ногами."
    th "Это не девочка, а какая-то шкатулка фокусника! Каждый раз что-то новое, и каждый раз - неожиданное."
    th "Похоже, она и вправду расслабилась в моём присутствии. С чего бы это?"
    dreamgirl "А тебе бы только клювом щёлкать!"
    dreamgirl "Девочка тебе редкое доверие оказала, позволила наблюдать себя, можно сказать, в естественной среде обитания, а ты даже поговорить с ней не мог!"
    th "Да о чём с ней говорить-то?{w} А главное - она, кажется, и без моей болтовни выглядела вполне довольной жизнью."
    if alt_uvao_D4_lunch_un:
        th "Помнится, кто-то мне впаривал вчера про «загрузит проблемами» и про «оно тебе надо?». Не ты ли?"
        dreamgirl "Да мало ли что я тебе наговорю, а ты и рад слушать, развесил уши!"
    play sound sfx_slavya_run
    "Начинающуюся пикировку с невидимым собеседником весьма кстати прервал вбежавший на площадь запыхавшийся Электроник."
    show el pioneer slap at fright with dissolve
    "В глаза бросалась некоторая ассиметрия его раскрасневшегося лица - одна щека была заметно румянее другой."
    dreamgirl "Вот кто у нас времени не теряет, так это Эл!"
    dreamgirl "Хотя непохоже, что ему от этого много радости - Жужелица явно перешла от слов к методам физического воздействия."
    show mt angry panama pioneer with dissolve
    el "Извините!{w=0.8} Что заставил!{w=0.8} Ждать!"
    "Выдохнул Электроник."
    "Вожатая одарила его кислым взглядом, но выговаривать ему не стала."
    play music music_list["always_ready"] fadein 3
    "Оглядев наше грозное войско (два дрища-интеллектуала и девочка-стесняша), она спросила командирским голосом:"
    show mt normal panama pioneer with dissolve
    mt "Ну что, все готовы?"
    show un normal pioneer with dissolve
    "Не знаю, как остальные, но лично я себя чувствовал готовым разве что к послеобеденному сну."
    "Впрочем, Ольга не стала дожидаться ответа, скомандовав:"
    mt "Тогда пошли!"
    "Развернувшись, она уверенно пошла в сторону дорожки мимо домиков, в конце которой пряталась калитка в заборе."
    hide mt with dissolve
    "Переглянувшись, мы поплелись следом за ней."
    stop sound_loop fadeout 3
    stop ambience fadeout 3
    window hide
    scene bg ext_path_day with dissolve
    play ambience ambience_forest_day fadein 3
    $ renpy.pause(1)
    window show
    show mt normal panama pioneer at center
    show el sad pioneer at right
    show un normal pioneer at left
    with dissolve
    "Довольно скоро я понял, что по утреннему холодку ходить по лесу намного приятнее."
    "День давно уже перевалил за середину, под деревьями было душно. Слабый ветерок, едва слышно шевеливший листья наверху, до тропинки не доставал."
    "Несмотря на жару, Ольга с самого начала задала быстрый темп, так что буквально через пару минут пот уже заливал мне глаза, а рубашка прилипла к телу."
    "Судя по взглядам, которые бросали Эл с Леной на вожатую, и по их тяжёлому дыханию, они тоже были не в восторге от выбранной скорости."
    th "И разумеется, никто из нас, включая Ольгу, не догадался захватить с собой воды. Красавцы!"
    if alt_uvao_true:
        th "Конечно, хорошо бы поскорее добраться до старого корпуса и найти Шурика, пока он там не начал гоняться за Юлей…"
        dreamgirl "…Или пока Юля не завялила Шурика в запасы на зиму - или на какие там угрозы для окружающих намекала Виолетта?…"
        th "…Однако, если мы не сбавим скорость, то долго я по такой жаре не протяну!"
        dreamgirl "Ну и метаболизм у вожатки! Утром ещё с похмелья зелёная была, а сейчас полюбуйтесь на неё!{w} Спортсменка, комсомолка!"
        th "Может, она потому и бодрая такая, что от вчерашнего не отошла?"
        dreamgirl "Бодрая, потому что с похмелья?! Ты вообще соображаешь, что несёшь?"
        th "Нет, не соображаю…{w} Жарко мне, отстань…"
    window hide
    scene bg ext_path2_day with dissolve
    window show
    show mt normal panama pioneer at center
    show el sad pioneer at right
    show un normal pioneer at left
    with dissolve
    "По мере того, как тропинка становилась уже, а за ноги всё чаще цеплялись корни деревьев, моя решимость кого-то спасать стремительно убывала."
    "В какой-то момент я решился было плюнуть на самолюбие и запросить пощады, но тут мы вышли наконец к опушке возле старого корпуса."
    "Тут мне пришло в голову, что если мы и дальше попрёмся всей толпой, то вполне можем нарваться на Юлю, а это не нужно ни ей, ни нам."
    me "Ольга Дмитриевна, как Вы думаете, может быть нам не стоит идти сразу всем вместе? Мало ли, вдруг Шурик испугается…"
    show el surprise pioneer
    show mt surprise panama pioneer
    show un surprise pioneer
    with dspr
    mt "Семён, что ты несёшь?! Почему это он должен нас пугаться?"
    "На это возразить мне было нечего, тем более, что Электроник с Леной тоже посмотрели на меня с недоумением."
    show el sad pioneer
    show mt normal panama pioneer
    show un normal pioneer
    with dspr
    "Остановившись, Ольга несколько раз крикнула:"
    mt "Шурик!!! Шурик!!!"
    "Ей никто не ответил, и мы пошли дальше."
    th "Что же, по крайней мере, хоть шум подняли. Вряд ли Юля могла не услышать вожатую."
    th "Надеюсь, у неё хватит сообразительности куда-нибудь уйти или спрятаться."
    dreamgirl "А ты не боишься, что у Шурик тоже услышал вопли вашей предводительницы, и у него тоже хватит смекалки не попадаться вам на глаза?"
    "Преодолев последние метры между редеющими кустами, мы вышли на поляну перед знакомым уже мне зданием старого корпуса."
    window hide
    scene bg ext_old_building_day with dissolve
    window show
    show el sad pioneer at left
    show un normal pioneer at fleft
    show mt normal panama pioneer at center
    with dissolve
    "Здесь Ольга Дмитриевна остановилась."
    mt "Шурик!!!"
    "Снова никакого ответа.{w} Вожатая обернулась к нам."
    mt "Может быть, Александра здесь и нет, иначе бы он уже отозвался. Но всё равно, сначала нам надо осмотреть здание."
    mt "Думаю, если мы разделимся…"
    stop music fadeout 2
    play sound sfx_tree_branches
    show sh angry bar3 far at fright with dissolve
    show el surprise pioneer at left
    show un surprise pioneer at fleft
    show mt surprise panama pioneer at center
    with dissolve
    play music music_list["doomed_to_be_defeated"] fadein 3
    "Её прервал треск веток позади нас, и из кустов выбрался наш драгоценный Шурик."
    "Видок у него был тот ещё. Его словно таскали волоком по земле - весь в какой-то грязи, одежда местами порвана, на руках и ногах свежие ссадины, волосы растрёпаны."
    show sh angry bar far at fright
    show el scared pioneer at left
    show un scared pioneer at fleft
    show mt scared3 panama pioneer at center
    with dissolve
    "Тут стоящая рядом Лена испуганно пискнула и спряталась мне за спину."
    hide un with easeoutleft
    "Через секунду и до меня дошло, чего она так испугалась - в руке Шурик сжимал увесистый на вид кусок арматуры.{w} В сочетании с перекошенным от гнева лицом и криво сидящими очками зрелище и в самом деле было устрашающее."
    show el scared pioneer at fleft
    show mt scared3 panama pioneer at cleft
    with ease
    "Мельком глянув на Ольгу с Элом, я убедился, что они тоже не больно-то рвутся обнимать от радости вновь обретённого товарища."
    "В другой ситуации я бы и сам с удовольствием попраздновал труса, но наличие за спиной дрожащей от страха девочки странным образом в корне всё изменило."
    hide mt
    hide el
    show sh angry bar3 far at right
    with dissolve
    "Я расправил плечи и медленно двинулся навстречу Шурику, стараясь улыбаться пошире."
    show sh angry bar at center with dissolve
    sh "Не подходи!"
    "Заверещал тот, выкатив налитые кровью глаза и выставляя перед собой арматурину, словно меч."
    me "Шурик, ты же пионер, успокойся!"
    me "Брось арматурину, это не наш метод.{w} Где гуманизм? Где человек человеку?"
    sh "Гуманизм? Да вы же вообще не люди! Вы - голоса в моей голове.{w} Хватит меня за нос водить! Вас нет.{w=.8} ВАС НЕТ!{w=.8} ВЫ - ГОЛОСА!{w=.8} УХОДИТЕ!"
    $ renpy.music.set_volume(0.5, delay=5, channel='music')
    "Мне очень не понравился нездоровый блеск в его глазах, а ещё меньше понравилось то, что в такт последним словам Шурик принялся размахивать железякой перед собой."
    th "Да что он, под кайфом?!"
    $ renpy.music.set_volume(0.3, delay=1.5, channel='music')
    me "Шурик? Может, не надо?"
    window hide
    stop music fadeout 0
    $ renpy.music.set_volume(1.0, delay=0, channel='music')
    scene cg d4_sh_stay
    with dissolve
    play music music_list["pile"] fadein 0
    window show
    "Вместо ответа он ринулся на меня с бессвязным воплем, замахиваясь куском арматуры."
    "От хилого на вид очкарика я никак не ожидал такой прыти…"
    dreamgirl "Не зевай!"
    with hpunch
    "В последний момент я сумел каким-то чудом увернуться от удара, но потерял равновесие и чуть не упал на спину."
    window hide
    scene bg ext_old_building_day
    show sh angry bar3 far at left
    with dissolve
    window show
    "Прежде чем я успел опомниться, Шурик уже снова пёр на меня, замахиваясь своим импровизированным мачете."
    show sh angry bar at center with dspr
    "Непроизвольно я вскинул руку, уже сознавая, что это бесполезная жертва и следующим ударом он всё равно раскроит мне череп…"
    show sh angry bar2 close at center with dspr
    "…Как вдруг увидел краем глаза какое-то движение сбоку. Шурика крутануло в сторону, и он, пролетев мимо меня, мешком рухнул на землю лицом вниз."
    hide sh with easeoutleft
    "На этот раз я не терял время даром и, растянувшись в прыжке словно вратарь, пытающийся взять идущий низом мяч, со всего маху приземлился на спину Шурику."
    with vpunch
    show sh rage pioneer close at center with easeinbottom
    "Тот сдавленно крякнул, а я вцепился что было сил в его правое запястье, выкручивая руку с несостоявшимся орудием убийства."
    show el upset pioneer close at right behind sh with dissolve
    "Тут ко мне на помощь подоспел Сыроежкин."
    with hpunch
    "Вдвоём мы сумели заломать Шурику руки за спину.{w} Тот хрипел что-то неразборчиво в землю, отчаянно пытаясь освободиться."
    with vpunch
    "Что делать дальше, я не имел ни малейшего представления."
    th "Отпустить его нельзя. Связать бы, но чем?"
    dreamgirl "Свяжите его полотенцами, как того поэта!"
    with vpunch
    "От неожиданности я ослабил было хватку, так что Шурик чуть не вырвался."
    show mt angry panama pioneer at fleft with dissolve
    mt "Ну-ка, переверните его!"
    "Раздался откуда-то сверху неожиданно спокойный голос вожатой."
    "Пыхтя от натуги, мы с Элом кое-как выполнили её распоряжение."
    with hpunch
    "Ольга нагнулась… И залепила Шурику звонкую оплеуху!"
    play sound sfx_face_slap
    show sh upset pioneer close with dspr
    sh "Ай!"
#    play sound_loop sfx_face_slap
    play sound sfx_face_slap
    "Последовала целая серия деловитых пощёчин, у бедняги только голова моталась туда-сюда."
    play sound sfx_face_slap
    $ renpy.pause(0.8)
    play sound sfx_face_slap
    $ renpy.pause(0.8)
    play sound sfx_face_slap
#    stop sound_loop fadeout 1
    dreamgirl "Браво! Брависсимо!"
    if alt_uvao_true:
        extend " Вот это я понимаю, настоящий специалист по адаптации! Прирождённый психолух!"
    else:
        extend " Во даёт вожатка! Так ему!{w} Слева! Справа!"
    "Наконец Шурик завопил:"
    show sh scared pioneer close with dspr
    sh "Ай! Да перестаньте же! За что?!"
    sh "Перестаньте меня бить! Отпустите!"
    stop music fadeout 9
    "Он как-то разом обмяк в наших руках, переводя испуганный взгляд с меня на Эла."
    show mt normal panama pioneer with dspr
    "Ольга вгляделась в его лицо и удовлетворённо кивнула."
    mt "Александр, как ты себя чувствуешь?"
    "Спросила она как ни в чём ни бывало."
    show sh normal pioneer close with dissolve
    sh "Н-нормально… А… А где это мы?"
    "Он ошарашенно завертел головой, оглядываясь. Увидев здание неподалёку, он явно удивился."
    show sh surprise pioneer close with dspr
    sh "Что вообще здесь происходит?"
    me "Что происходит?!"
    "Не выдержав, я перешёл на крик:"
    me "Ты только что мне голову чуть не пробил, вот что происходит!"
    show sh scared pioneer close with dspr
    sh "Я?!"
    mt "Семён, подожди!"
    "Вмешалась Ольга Дмитриевна."
    mt "Шурик, ты что последнее помнишь перед тем, как я… В общем, перед тем, как ты пришёл в себя здесь?"
    show sh upset pioneer close with dspr
    sh "Я… Я утром собирался пойти в старый корпус…"
    "Он бросил на меня вороватый взгляд и поспешно добавил:"
    sh "…За радиодеталями! Я слышал, там старое бомбоубежище внизу есть!"
    sh "Я вышел из лагеря, пошёл по тропинке сюда и потом…"
    sh "Я… Я не помню… Голова кружится…"
    "Лицо Шурика побледнело, он болезненно скривился:"
    show sh scared pioneer close with dspr
    sh "Да отпустите вы меня, ведь больно же!"
    mt "Действительно, ребята, отпустите его.{w} Думаю, он никуда теперь не убежит и вообще будет себя хорошо вести.{w} Правда, Шурик?"
    "Тот слабо кивнул.{w} Поняв, что всё ещё продолжаю выкручивать ему руки, я разжал захват."
    show sh upset pioneer close with dspr
    "Отчаянно заныло колено, пораненное утром при попытке открыть дверь в бункер.{w} Похоже, я снова здорово им приложился, когда прыгал на Шурика."
    th "Надеюсь, этому психу я тоже что-нибудь отшиб!"
    "Уложив совместными с вожатой усилиями Шурика на траву, Электроник принялся шарить вокруг в поисках очков, слетевших с того при падении."
    hide el
    hide sh
    with dissolve
    mt "Семён, ты как?"
    "Негромко спросила Ольга Дмитриевна."
    me "Всё в порядке."
    "Кивнул я. Меня порядком трясло, но показывать этого не хотелось."
    "Подождав, пока дрожь немного утихнет, я, кое-как встав сначала на четвереньки, а потом и на ноги, огляделся по сторонам."
    window hide
    hide mt
    with dissolve
    $ renpy.pause(1)
    show un normal pioneer far with dissolve
    window show
    "Лена тихонько стояла в нескольких шагах от нас, не вмешиваясь в допрос."
    "Поймав мой взгляд, она явно смутилась."
    dreamgirl "Знаешь, есть у меня одна идея…"
    th "Кажется, у меня тоже."
    show un normal pioneer with dissolve
    "Я подошёл к девочке и негромко спросил:"
    me "Лена, скажи, а ты не в курсе, как вышло, что Шурик вдруг так вовремя упал?"
    show un shy pioneer with dspr
    "Она засмущалась ещё больше и ничего не ответила."
    me "Это ведь ты ему помогла, да? Ловко у тебя вышло! Как по учебнику."
    "Я не видел, как там всё было на самом деле, но решил, что сейчас тот самый случай, когда лучше перехвалить."
    "В конце концов, нужного результата она достигла, а как именно - это уже детали."
    "Лена отчаянно покраснела и пролепетала еле слышно:"
    un "Я боялась, у меня не получится…{w} Мне папа показывал… На всякий случай, если…{w} Но я никогда раньше…"
    show un cry_smile pioneer with dissolve
    if alt_uvao_D4_lunch_un:
        "Она подняла взгляд и продолжила, гипнотизируя меня своими зелёными глазищами точь-в-точь, как вчера за обедом:"
    else:
        "Она вдруг посмотрела мне прямо в глаза."
    un "Сама не знаю, что на меня нашло, но я так за тебя испугалась, когда…"
    "Тут она окончательно смутилась и отвернулась в сторону."
    show un shy pioneer with dissolve
    "Чувствуя, что и сам краснею, я сделал несколько глубоких вдохов и выдохов."
    "Потом сказал, стараясь, чтобы голос звучал твёрдо:"
    me "Если бы не ты, я сейчас лежал бы с проломленным черепом."
    "Лена вздрогнула, но промолчала."
    me "Спасибо тебе большое. Кажется, я теперь твой должник."
    me "И да, если вдруг когда-нибудь снова захочешь спасти меня от неминуемой смерти - прошу, не отказывай себе в этом маленьком удовольствии."
    show un smile pioneer with dissolve
    "Лена в ответ смущённо хихикнула и, кажется, немного расслабилась."
    dreamgirl "А теперь самое время обменяться кольцами и поцеловать друг друга!"
    dreamgirl "Или нет, пусть лучше она объявит себя твоим рыцарем и поклянётся оберегать и защищать тебя до самой смерти!"
    mt "Лена, Семён, идите сюда!"
    show un normal pioneer with dspr
    "Голос вожатой весьма вовремя отвлёк меня от мысли подобрать ту арматурину и довести до конца затеянную Шуриком трепанацию моей черепной коробки, дабы успокоить одного навязчивого внутреннего придурка."
    show un sad pioneer far at fleft
    show el sad pioneer at right
    show sh cry pioneer at center
    show mt sad panama pioneer at left
    with dissolve
    "Мы подошли к остальным.{w} Шурик теперь уже не лежал, а сидел на траве, но выглядел всё так же неважно."
    dreamgirl "Лицо бледное, взгляд мутный. Похоже, наш Рэмбо порядком подустал.{w} Перенапрягся, пока по кустам бегал да железяками размахивал, вот головка и бо-бо."
    th "Ты-то веселишься. А я вот уже догадываюсь, кто его сейчас потащит на себе."
    "К сожалению, вожатая подтвердила мои опасения."
    play music music_list["always_ready"] fadein 5
    mt "Надо бы поскорее доставить Александра в медпункт."
    mt "Давайте попробуем его поднять - может быть, он сможет идти, если вы будете его поддерживать с двух сторон."
    "Ухватив Шурика под руки, мы с Элом кое-как подняли его и даже сумели сделать несколько шагов, после чего ноги у болящего подогнулись и вся наша троица полетела на землю."
    play sound sfx_bodyfall_1
    with vpunch
    "И разумеется, меня в третий раз угораздило приложиться больной ногой!{w} С трудом сдерживая рвущиеся с языка ругательства я поднялся."
    mt "Кажется, придётся кому-то бежать в лагерь за помощью. Без носилок не обойтись."
    show sh upset pioneer with dissolve
    "Тут я сообразил, что за всей этой вознёй я совсем забыл про Юлю."
    th "Оставаться здесь сейчас совсем не с руки."
    if alt_uvao_true:
        dreamgirl "И вообще, не ушастая ли ему помогла крышу потерять? Может, Виола на что-то такое и намекала?"
    me "Эл, ты на «замке» когда-нибудь человека носил?"
    show el surprise pioneer with dspr
    "До Сыроежкина довольно быстро дошло, что я имею в виду."
    show el sad pioneer with dspr
    el "Нет, нам рассказывали в школе, но применить на практике как-то никогда не приходилось."
    me "Ну вот и попрактикуемся сейчас.{w} Как думаешь, осилишь?"
    "Эл вздохнул:"
    show el normal pioneer with dspr
    el "Во всяком случае, мы должны попробовать. Не можем же мы бросить друга в беде!"
    "Сдержавшись, я не стал развивать тему своих дружеских чувств к Шурику, вместо этого просто кивнув."
    "С третьей попытки мы с Элом сообразили, как правильно сложить руки в замок."
    "Ольга Дмитриевна с Леной помогли Шурику усеться на получившееся сиденье, он ухватился за наши плечи, и мы с Элом потащили его в лагерь."
    window hide
    scene bg ext_path2_day with dissolve
    window show
    "Обратная дорога запомнилась преимущественно как множество веток, норовящих ударить по лицу, и корней, упорно цепляющих за ноги."
    "Идти по узкой тропинке вдвоём, да ещё и с увесистым грузом, было дьявольски неудобно."
    "Прохлады в лесу ничуть не прибавилось, так что очень скоро мою одежду можно было выжимать."
    window hide
    scene bg ext_path_day with dissolve
    window show
    "Постепенно тропинка расширилась и идти стало немного легче, но к этому времени мы с Электроником успели изрядно вымотаться."
    play sound_loop breath fadein 5
    "Потные ладони упорно пытались соскользнуть с запястий, уставшие пальцы норовили разжаться, спину ломило."
    "Рука Шурика давила мне на шею. Оттягивающее наши руки седалище Шурика тоже не пробуждало бурной радости."
    dreamgirl "Лучше бы вы девочку какую несли! Славю, скажем. Или ту же Лену - её, кстати, есть за что - она вам, балбесам, жизнь спасла…{w} Да хоть вожатую - всё приятнее было бы!"
    th "Вожатую мы бы далеко не унесли…{w} Хотя и Шурик мог бы быть немного полегче. Раскормили, блин! Все руки мне оттянул!"
    "Впереди уже показалась ограда лагеря, когда Электроник пропыхтел:"
    show el sad pioneer close at right
    show sh upset pioneer close at center
    with dissolve
    el "Может…{w=0.8} Устроим…{w=0.8} Привал?"
    me "Если мы сейчас остановимся{w=0.8}, то ещё раз его уже не поднимем.{w} Давай уже просто донесём поскорее{w=0.8}, немного осталось!"
    sh "Ребята, мне так неловко…"
    "Слабым голосом пробормотал Шурик."
    me "А ты лучше молчи.{w} Лично я предпочёл бы тебя не нести{w=0.8}, а по земле перекатывать{w=0.8} всю дорогу. Так что{w=0.8} просто{w=0.8} молчи!"
    window hide
    scene bg ext_houses_day with dissolve
    play ambience ambience_camp_center_day fadein 3
    window show
    "Вскоре мы протиснулись в калитку и затопали по мощёной плитами дорожке лагеря."
    "Встречные пионеры смотрели на нас, разинув рот, но помогать никто не рвался."
    window hide
    scene bg ext_square_day with fade
    window show
    "Всё ускоряясь, мы миновали площадь…"
    window hide
    scene bg ext_aidpost_day with fade
    window show
    "По дорожке к медпункту мы с Элом уже практически бежали.{w} Бросившаяся вперёд Лена распахнула перед нами дверь…"
    play sound sfx_open_door_clubs fadein 0
    window hide
    stop music fadeout 5
    scene bg int_aidpost_day with fade
    play ambience ambience_camp_center_day fadein 3
    show un sad pioneer far at fleft
    show mt sad panama pioneer far at left
    show el sad pioneer at fright
    show sh upset pioneer at cright
    show cs normal glasses far at cleft
    with dissolve
    window show
    "И мы ввалились внутрь."
    "Не обращая внимания на изумлённую Виолетту, я скомандовал:"
    me "На кушетку!"
    "Последние несколько шагов, и мы с Электроником свалили свою ношу на кровать и без сил опустились на пол рядом."
    hide sh with dissolve
    "Кажется, Ольга Дмитриевна что-то объясняла медсестре, та тоже что-то говорила, но мне было всё равно."
    stop sound_loop fadeout 5
    hide un
    hide mt
    hide el
    hide sh
    hide cs
    scene bg black
    with dissolve2
    "Весь мир сейчас для меня сжался до надёжного прочного пола, на котором можно было сидеть, и края кушетки, на которую можно было опереться спиной."
    "Единственное неудобство доставляли только руки, вытянувшиеся, судя по ощущениям, до пятиметровой длины как минимум."
    th "Странно, что они вообще помещаются в медпункте, а не высовываются на крыльцо."
    dreamgirl "А ты расскажи о своих ощущениях Виоле. Пусть сразу и тебе что-нибудь от галлюцинаций вколет заодно с Шуриком, зачем ей два раза возиться?"
    "Наконец, в голове перестали грохотать кузнечными молотами, а пальцы рук начали ощущаться как часть тела, а не как отдельно лежащая на полу субстанция."
    window hide
    scene bg int_aidpost_day
    show mt normal panama pioneer far at left
    show el sad pioneer at right
    show cs normal glasses at center
    with dissolve2
    window show
    "Сидевший рядом Эл тоже завозился, пытаясь подняться."
    cs "Я смотрю, пионеры, вы уже пришли в себя."
    "Покопавшись в холодильнике, Виолетта извлекла оттуда бутылку «Боржоми».{w} Достав из шкафа пару стаканов, она нацедила в них минералки и всучила один Электронику, а другой мне:"
    cs "Пейте маленькими глотками."
    "Я честно старался делать, как велено, но весь стакан почему-то уместился в три глотка."
    cs "Всё с вами ясно."
    "Вздохнула Виола, отбирая стакан."
    cs "А теперь поднимайтесь и оба марш отсюда.{w} Мне ещё этому пионеру ссадины обработать надо и ввести противостолбнячную сыворотку."
    "Она кивнула на Шурика который уже вовсю спал, вытянувшись на кушетке.{w} Похоже, что-то она ему уже успела вколоть для расслабления духа."
    show cs smile glasses with dspr
    cs "Конечно, если вы тоже хотите стать моими {i}пациентами{/i}, то можете задержаться."
    show cs badgirl2 glasses with dspr
    cs "В конце концов, надо бы провести тщательный {i}осмотр{/i}, мало ли какие травмы вы могли получить…"
    show el scared pioneer with dspr
    "Голос медсестры, ставший медовым, произвёл на Электроника прямо-таки волшебное действие - вскочив, словно и не сидел только что на полу без сил, он залепетал что-то и устремился к двери."
    hide el with easeoutright
    show cs smile glasses with dspr
    show mt laugh panama pioneer far with dspr
    "Ольга Дмитриевна тихонько хихикнула по-девчоночьи, но ничего не сказала."
    "Кое-как подобрав свои руки-ноги, я поднялся и тоже направился на выход."
    if alt_uvao_true:
        show cs normal glasses
        show mt normal panama pioneer far
        with dspr
        cs "Семён, на ужине мне не забудь рассказать, как твоя неврастения себя ведёт."
        "Кивнув Виолетте, я вышел на крыльцо вслед за Сыроежкиным."
    window hide
    stop ambience fadeout 2
    scene bg ext_aidpost_sunset with dissolve
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    play ambience ambience_camp_center_evening fadein 3
    play sound sfx_close_door_1
    show un normal pioneer at left
    show el sad pioneer at right
    with dissolve
    window show
    "Лена ждала нас снаружи. Похоже, её успели вытурить раньше, пока мы с Элом расслаблялись."
    "Судя по её усталому лицу, сегодняшняя прогулка тоже далась ей не просто так, хотя она и не участвовала в переноске тяжелораненных с поля боя."
    play sound sfx_close_door_1
    show mt smile panama pioneer at center with dissolve
    "Вышедшая через минуту Ольга Дмитриевна, напротив, была до неприличия бодра."
    dreamgirl "После сегодняшнего рассказа Юли сдаётся мне, что неприличная бодрость нашей вожатой имеет вполне себе неприличные же причины!"
    dreamgirl "Зарядилась ночью от Саныча, вот и…"
    th "Да по мне - хоть от Шурика! Уймись!{w} Вон, кажется, она сказать что-то хочет."
    mt "Виолетта говорит, что Шурик отлежится немного и всё с ним будет нормально.{w} Наверняка будет ясно завтра, когда он проснётся - она ввела ему снотворное."
    "Тут Ольга приосанилась и произнесла торжественным тоном:"
    play music music_list["always_ready"] fadein 3
    show mt normal panama pioneer with dspr
    mt "Я рада, что все вы показали себя сегодня надёжными товарищами и не посрамили высокое звание пионера!"
    mt "Вы, ребята, продемонстрировали настоящее мужество и достойную подражания выдержку, оказывая помощь пострадавшему."
    show el normal pioneer with dissolve
    "Она посмотрела на на нас с Электроником с гордостью, словно лично занималась нашим воспитанием на протяжении последних лет, и продолжила:"
    mt "Особенно я хочу отметить участие Лены."
    mt "Мало того, что она сегодня не растерялась в критической ситуации и э-э… пришла на помощь оказавшемуся в опасности Семёну."
    show un shy pioneer with dspr
    mt "Лена ещё и {i}добровольно{/i} приняла участие в поисках пропавшего товарища, по своей инициативе вызвавшись пойти с отрядом!{w} Вот пример, на который все должны равняться!"
    "Лицо Лены к этому моменту сделалось пунцовым. Кажется, она с трудом сдерживалась, чтобы не броситься бежать куда глаза глядят."
    "Ольга же, раздав всем сёстрам по серьге, продолжила уже вполне будничным тоном:"
    mt "Сейчас всем отдыхать, только далеко не разбредайтесь - ужин уже скоро."
    "Она критически оглядела нас с Элом."
    show mt angry panama pioneer with dspr
    mt "И приведите себя в порядок! Подвиги подвигами, но в таком виде в столовую я вас не пущу."
    dreamgirl "Полагаю, этого человека не спасти. Занудство уже поработило его мозг."
    stop music fadeout 3
    "Ольга сошла с крыльца и неспешно направилась в сторону площади."
    hide mt with dissolve
    "Куда исчезла Лена я не мог с уверенностью сказать, но через секунду в пределах видимости её уже не было."
    hide un with easeoutleft
    "Мы с Электроником переглянулись и поплелись к умывальникам."
    hide el with dissolve
    play music music_list["my_daily_life"] fadein 5
    dreamgirl "А ведь ты, похоже, так ничего и не заметил?"
    th "А что именно я должен был заметить? Что Ольга умеет толкнуть речь к месту и не к месту?"
    dreamgirl "Что стесняша потащилась в эту вашу нелепую вылазку добровольно, идиот!"
    dreamgirl "Причём напомню на случай, если ты ещё и склерозом страдаешь - пару часов назад она тебе сказала, что её Ольга припахала."
    dreamgirl "А теперь внимание, правильный вопрос!{w} То, что она пошла добровольно, это ещё полбеды, но зачем она тебе соврала?"
    th "Слушай, кто из нас интуицией заведует? Ты! Вот и подскажи мне."
    dreamgirl "Интуиция здесь не при чём, здесь аналитику надо задействовать. Так что постарайся сам подумать."
    "Ни стараться, ни думать мне сейчас отчаянно не хотелось."
    window hide
    scene bg ext_washstand2_day
    with dissolve
    window show
    play sound sfx_open_water_sink
    $ renpy.pause(1)
    play sound_loop sfx_water_sink_stream
    "Поэтому я ограничился тем, что дойдя до умывальников, кое-как умылся привычно уже ледяной водой и почистил вымазанную землёй и травой одежду, как смог."
    "Закончив прихорашиваться, я прикинул, что ходить туда-сюда по лагерю мне не с руки и побрёл сразу в сторону столовой."
    play sound sfx_close_water_sink
    stop sound_loop
    window hide
    stop music fadeout 5
    play sound_loop ambience_medium_crowd_outdoors fadein 2
    scene bg ext_dining_hall_away_sunset with dissolve2
    window show
    "Плюхнувшись на скамейку недалеко от входа, я откинулся на спинку и наконец-то расслабился."
    play sound eat_horn fadein 2
    "Впрочем, долго отдыхать мне было не суждено - через несколько минут с небес прогремели трубы, зовущие пионеров вкусить вечернюю трапезу."
    stop sound fadeout 2
    "Вздохнув, я поднялся и повёл свой организм питаться."
    stop ambience fadeout 3
    stop sound_loop fadeout 3
    jump alt_day5_uvao_evening

# Д5-вечер
#
# Используется флаг alt_uvao_true
# Используется флаг alt_uvao_D5_sh_mines.
# Используется флаг alt_uvao_D4_concert.
# Используется флаг alt_uvao_D5_hentai.
# Используется флаг alt_uvao_D5_evening_sl.
# Используется флаг alt_uvao_D5_evening_dv_un.
label alt_day5_uvao_evening:
    $ alt_uvao_D5_evening_dv_un = False
    window hide
    $ routetag = "uv"
    $ alt_chapter(5, u"Юля. Вечер")
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    scene bg int_dining_hall_people_sunset
    with dissolve
    play ambience ambience_dining_hall_full fadein 3
    window show
    if (alt_uvao_D5_sh_mines):
        "К сожалению, кажущееся спокойствие вожатой оказалось временным."
        play music music_list["awakening_power"] fadein 2
        show mt angry panama pioneer at left with dissolve
        "Войдя следом за мной в столовую, Ольга ухватила меня за плечо и прямиком потащила к столику в дальнем углу, за которым восседала Виолетта наедине со своим ужином."
        if alt_uvao_D4_viola_morning:
            th "Так… Кажется, мне сейчас предстоит объясняться, почему я больше ни разу не зашёл проведать доктора Виолу…"
            dreamgirl "Да ладно тебе! Скажешь ей честно, что женщин боишься."
            dreamgirl "А если серьёзно, то мне кажется, что Виола не просто медсестра. Очень уж она в тот раз заинтересовалась твоим рассказом."
            dreamgirl "Не удивлюсь, если она решит посадить тебя в карантин - поближе к телу, так сказать."
        else:
            th "Странно… При чём здесь медсестра? Я не ранен, не считая синяка на колене, а раз Ольга сама видела Юлю, то понимает, что с головой у меня всё в порядке…"
            dreamgirl "Какая завидная самоуверенность! Человек, регулярно общающийся с внутренним голосом, объявляет себя психически здоровым!"
            dreamgirl "Хотя, если серьёзно, то мне тоже непонятно, при чём здесь Виола. Может быть, в карантин тебя хотят посадить?"
        "Давно заметившая наше приближение Виола смотрела на нас с явным любопытством."
        show cs smile2 at right
        show mt angry panama pioneer at left
        with dissolve
        "Дотащив меня до столика, Ольга отпустила наконец моё плечо и, наклонившись к разглядывающей меня медсестре, что-то принялась быстрым шёпотом ей объяснять, жестикулируя."
        "До меня долетали только отдельные слова: «…стоянке… контактировал… территории… эвакуация…»"
        th "Э-э-э… Не понял? Эвакуация? Это что, тоже из-за меня?"
        dreamgirl "Кажется, карантин для тебя стал бы меньшим из зол… Боюсь, что препарируют тебя, милый друг, для пользы науки."
        th "Надеешься отсидеться? Извини, боюсь, что не получится. Препарируют обоих."
        "Тем временем Виола нахмурилась и посмотрела на вожатую."
        show cs normal with dissolve
        cs "Оля, остановись пожалуйста. Я думаю, пока что вполне достаточно. Будь добра, сядь и постарайся успокоиться."
        stop music fadeout 3
        show mt normal panama pioneer with dspr
        show cs normal2 with dissolve
        "Хотя Виола говорила спокойным голосом, Ольга как-то сразу сникла и послушно уселась с ней рядом. Виола снова посмотрела на меня."
        cs "А ты, пионер, сходи-ка на раздачу и принеси две порции - себе и вашей вожатой."
        cs "И рюкзак свой положи - никуда он не денется."
        "Идея носить еду всяким истеричным девицам мне совсем не понравилась, но возражать я не решился."
        th "В конце концов, Виола производит впечатление довольно адекватного человека, несмотря на её милые шалости. Да и вообще, надо разобраться сначала, что она на самом деле за птица, а поссориться с ней я всегда успею!"
        hide mt
        hide cs
        with dissolve
        "Сунув рюкзак под стол, я направился за едой."
        "На ужин сегодня снова были картофельное пюре с зелёным горошком и котлеты."
        "Однако даже запах горячей еды не улучшил моего настроения, слишком беспокоил меня предстоящий разговор - а я не сомневался, что он будет непростым."
        th "Они обе явно знают гораздо больше, чем пытаются показать. Ольга ведь нисколько не удивилась, увидев Юлю. Любые эмоции, кроме удивления. Да и Виола явно не выглядит изумлённой происходящим."
        show cs normal2 far at right
        show mt normal pioneer far at left
        with dissolve
        "Балансируя сразу двумя подносами, я начал продвигаться к столику, от которого за мной пристально следили две пары глаз - одна зелёная, другая разноцветная."
        th "Интересно, чем всё это может для меня закончиться?"
        dreamgirl "Известно чем - заспиртуют тебя в большой банке, а может и того хуже - из пионеров исключат!"
        th "В таком случае, я бы, пожалуй, предпочёл худший из этих двух вариантов - быть исключённым."
        dreamgirl "Тогда всё отрицай и ничего не подписывай!"
        th "От тебя никакой пользы!"
        show cs normal2 at right
        show mt normal pioneer at left
        with dissolve
        "Когда я подошёл к столику, негромко переговаривавшиеся о чём-то Ольга и Виолетта замолчали и снова уставились на меня."
        show cs normal2 close at right
        show mt normal pioneer close at left
        with dissolve
        "Поставив подносы, я подавил возникшее внезапно желание бросив всё, дать дёру, и уселся вместо этого напротив."
        cs "Ты ешь, пионер, ешь. Здоровое питание прежде всего."
        "Виола приглашающе показала на тарелку."
        "Я вспомнил съеденную сегодня банку тушёнки. Это было так давно…{w} Кажется, столетия назад…"
        "Почувствовав, как рот внезапно наполнился слюной, я схватил вилку."
        dreamgirl "Надо же, а ведь только что аппетита не было. Какой же ты непостоянный."
        cs "Ешь и одновременно рассказывай. Я понимаю, что разговаривать с набитым ртом - это вредно для здоровья, но сейчас, как ты сам понимаешь, случай исключительный."
        me "А про что, собственно, рассказывать?"
        "Принимаясь за котлету я покосился на Ольгу. Та сидела молча, но сдерживала себя с явным трудом.{w} Виола, напротив, оставалась совершенно спокойной."
        cs "Как про что? Про нашу местную легенду. Тебя же видели вместе с ней. И у меня есть определённые основания полагать, что ты с ней не за пять минут до этого познакомился."
        if alt_uvao_D4_viola_morning:
            cs "Кстати, очень жаль, что ты не зашёл ко мне поделиться информацией. Самостоятельные исследования - это прекрасно, но уверяю тебя, у меня нашлось бы, что рассказать тебе взамен."
        cs "А исходя из того, что тебя никто нигде не мог найти…"
        "Тут вожатая не выдержала."
        show mt angry pioneer close with dspr
        mt "Вот именно! Тебя с утра все ищут, я с ног сбилась, а он шляется неизвестно где… Нет, Виола, дай мне сказать!"
        "Попытавшаяся было вмешаться Виола с достоинством откинулась на спинку стула, а Ольга продолжила, переключившись внезапно на неё:"
        mt "Ты что, беды хочешь? У нас уже пострадавшие! Мало тебе Александра? Может быть, ты хочешь, чтобы вышло как с Ветровым, а?!"
        "Негромкий хлопок Виолы ладонью по столу прозвучал в притихшей столовой как пистолетный выстрел."
        show mt normal pioneer close with dspr
        "Ольга осеклась на полуслове и уставилась на поднос с нетронутым ужином, тяжело дыша."
        "Выждав немного и убедившись, что большинство присутствующих снова вернулось к ужину, медсестра произнесла всё таким же спокойным, как и раньше, тоном:"
        show cs normal close with dissolve
        cs "Оля, не говори ерунды. Никаких серьёзных неприятностей мы не допустим. В конце концов, это наша работа."
        cs "И не надо преувеличивать. Ничего особенного с Трофимовым не случилось - отоспится и дело с концом.{w} Неужели ты думаешь, я оставила бы его одного и отправилась ужинать, будь с ним что-то серьёзное?"
        show cs normal2 close with dissolve #Наверное, было замечено уже, что моя самопальная эмоция чуть ниже чем стандартная?
        "Ольга смущённо потупилась и ничего не ответила. Убедившись, что бунт на корабле подавлен, Виола как ни в чём не бывало вновь обратилась ко мне:"
        cs "Так вот, как я уже говорила, у меня есть определённые основания полагать, что значительную часть сегодняшнего дня ты провёл с визуа… гм… с этим необычным существом."
        cs "Кстати, у неё есть имя? Как-то ты её называешь?"
        me "Она сказала, что её зовут Юля."
        "Кажется, лицо Виолы дрогнуло, когда я произнёс имя, но уже через секунду уверенности в этом у меня не было."
        cs "Очень хорошо. Если мы предположим на минуточку, что ты довольно долго находился с Юлей, то не мог ли ты {i}случайно{/i} видеть, что именно произошло с Александром?"
        "Сквозь ледяное спокойствие на её лице вдруг проступил явный интерес юного натуралиста, которому вот-вот откроется тайна мирового значения - как же всё-таки размножаются ёжики."
        cs "Скажем так, эти данные могут не только помочь твоему товарищу, но и вообще… оказаться весьма полезными.{w} С соответствующими результатами для тебя."
        th "Собственно говоря, а что я теряю? Всё равно мне играть приходится в тёмную, а Виола явно владеет ситуацией."
        th "Раз уж они и без меня знают о существовании Юли, то и вправду, не рассказать ли им немного про сегодняшний поход?"
        if alt_uvao_D5_hentai:
            th "Разумеется, с некоторыми купюрами. Виолу-то вряд ли будет так просто пронять интимными подробностями, а вот Оленька может смутиться…"
            dreamgirl "Шутник! Ты смотри, и в самом деле {i}об этом{/i} не сболтни сдуру, пусть даже и Виоле! Хотя бы разберись сначала, что здесь к чему, а то и сам подставишься, и хвостатую…"
            th "Да ладно тебе, не учи учёного. Вряд ли именно {i}это{/i} интересует их в первую очередь."
        if alt_uvao_D4_viola_morning:
            th "И вообще, с учётом намёков Виолы про обмен информацией…"
        "Я открыл было рот, но моим благим намерениям не суждено было исполниться."
        "Сидевшая последние пару минут молча, Ольга внезапно словно взорвалась:"
        play music music_list["awakening_power"] fadein 2
        show mt angry pioneer close with dspr
        mt "Виола, да ты посмотри на него! Кого ты просишь?! Посмотри на этого безответственного типа! Думаешь он порядочный пионер? Да ему же наплевать на всех!"
        mt "Мало того, что всё время он отлынивал от любых общественных мероприятий, так сегодня вообще весь день шлялся неизвестно где, никого не предупредив!"
        "Растерявшись от такого напора, я хотел было что-то сказать в своё оправдание, но вожатая только отмахнулась и продолжила, не давая мне вставить ни слова."
        mt "А сегодня на стоянке он знаешь что делал? Я увидела - и чуть с ума не сошла! Он с {i}ней целовался{/i}! Целовался, понимаешь?!"
        "В глазах у меня потемнело от ярости. Возникла короткая пауза, которую не преминули заполнить:"
        if alt_uvao_D5_hentai:
            dreamgirl "Ах, жалко она вас {i}там{/i} не видела, вместе! Как ты Юленьку-то за хвост держал! Обзавидовалась бы до смерти!"
        else:
            dreamgirl "Я видел это, и разум мой был помрачен! Не под покровом ночи! Не за глухими стенами и заборами! А с белой простыни! Дщерь человеческая! Даровала мужчине! Свой поцелуй!"
        "Я не стал размениватся на препирательства с Шизой и не стал орать на вожатую, хотя мне этого очень хотелось - и плевал я на последствия!"
        "Ухватившись за край стола так, что пальцы заболели, и слегка наклонившись, я поинтересовался вкрадчивым голосом:"
        me "Как же я могу стать порядочным пионером, если мне не с кого брать пример? Ведь некоторые вожатые, не будем показывать пальцем, тоже не являют собой образец ответственности и нравственности."
        mt "Что ты несёшь?!"
        "Возмутилась Ольга Дмитриевна."
        me "Да я даже не знаю, с чего собственно начать. С вашей «активной» педагогически-воспитательной работы в виде лежания в шезлонге с книгой и загорания на пляже, в то время как пионеры выполняют ваши бессмысленные поручения?"
        show mt rage pioneer close with dspr
        me "…Или, может быть, с ваших вечерних посиделок с физруком?"
        mt "Я не знаю, что ты там себе напридумывал, но…"
        "Ольга побагровела не то от злости, не то от смущения, но теперь уже я решил отыграться по полной и добил:"
        me "Ах да, извините, оговорился! Не посиделок, а полежанок!{w} На пляже."
        stop music fadeout 3
        show mt shocked pioneer close
        show cs badgirl2 close
        with dspr
        "Я разжал наконец сведённые судорогой пальцы и удовлетворённо откинулся на спинку стула, наслаждаясь результатом."
        "Последняя фраза пришлась не в бровь, а в глаз. Ольга разом сдулась, словно проколотый воздушный шар и, ничего не отвечая, молча смотрела на меня."
        "Я перевёл взгляд на Виолу. Та, похоже, была не слишком-то удивлена. Скромно опустив глаза, она разглядывала стакан с остатками компота, но на её лице явственно проступала ехидная полуулыбка."
        "Снова посмотрев на вожатую, я даже испытал лёгкий укол совести - настолько она на себя была не похожа.{w} Может быть, не стоило сюда Саныча приплетать? Ольга, конечно, та ещё заноза, но её ведь теперь за аморалку и из комсомола исключить могут."
        stop ambience fadeout 6
        show cs normal2 close with dspr
        "Над столом повисло напряжённое молчание. За увлекательными разговорами я даже не заметил, что все пионеры давно уже закончили есть и разошлись."
        "Столовая опустела. Дежурные, косясь на нас, подметали пол, протирали столики и поднимали стулья."
        "Эйфория от лёгкой победы быстро улетучивалась, уступая место раскаянию."
        dreamgirl "Да, лихо ты вожатку уел. Ничего, не переживай - ей это только на пользу пойдёт. Иногда людей надо избавлять от излишней самоуверенности."
        "Я сердито засопел и принялся доедать ужин, пока меня и в самом деле не посадили в карцер."
        "Наконец Виола прервала затянувшуюся паузу."
        cs "Семён. Мне кажется мы, как взрослые люди, могли бы найти разумный компромисс."
        show cs normal close with dissolve
        cs "Прежде всего, я хочу предупредить тебя о возможной опасности со стороны этого… этой… Юли."
        cs "Мы примерно представляем, {b}что{/b} это такое, и {b}чего{/b} можно от него ожидать. Случались уже э-э-э… прецеденты."
        show cs normal2 close with dissolve
        cs "Конечно, ты можешь не обращать внимания на мои предостережения, но я надеюсь на твою рассудительность."
        "Пока я переваривал услышанное, она повернулась к вожатой, всё ещё пребывающей в состоянии лёгкого шока."
        cs "Оля, я думаю, можно предоставить {i}пионеру{/i} некоторую свободу действий и сделать его пребывание в лагере… более комфортным. В пределах разумного, разумеется."
        show mt normal pioneer close with dissolve
        "Ольга молча кивнула. Виола пристально посмотрела на меня."
        show cs normal close with dissolve
        cs "Только не думай, пионер, что это подействовал твой детский шантаж.{w} Просто одна из наших первоочередных задач - следить за психологическим климатом в лагере. На то есть свои причины."
        cs "Надеюсь, ты с умом распорядишься представившимися возможностями. Предложение сотрудничества, если ты не понял, по-прежнему в силе, так что подумай. Обещаю, разочарован ты не будешь."
        show cs normal2 close with dissolve
        "Против обыкновения, последние слова она произнесла серьёзным тоном, безо всяких там игривых интонаций."
        "Убедившись, что высказаться желающих больше нет, она резюмировала:"
        cs "А сейчас, я думаю, нам всем пора идти. Тебе, пионер, отдыхать, а нам с тобой, Оля, надо обсудить пару рабочих вопросов."
        "Не глядя ни на кого, я подхватил рюкзак и чуть ли не бегом бросился вон из столовой."
        play music music_list["my_daily_life"] fadein 4
    else:
        "Впрочем, идти было очень недалеко, так что в столовую я, впервые за пять дней, попал одним из первых."
        "Получив свою порцию гречневой каши с чем-то, смутно напоминающим гуляш, и стакан неизбежного компота, я замер было в нерешительности с подносом в руках…"
        if not alt_uvao_true:
            "Но потом решительно направился к пустующему пока что столику в углу."
            "Я с облегчением поставил поднос - руки всё ещё здорово ныли после переноски очкарика - и уселся за стол."
            if not alt_uvao_D4_concert:
                extend " Кажется, именно здесь я вчера и обедал."
                th "А кстати, вчерашний обед очень даже спокойно прошёл! Надо воспользоваться отработанной техникой."
                "Я снова пристроил на лицо зверскую гримасу серийного убийцы."
            th "В конце концов, я сегодня не только не пообедал толком, так ещё и полдник пропустил за всей этой беготнёй!{w} Непорядок, однако! Так и похудеть можно."
            dreamgirl "Можно подумать, последние несколько лет у тебя было исключительно пятиразовое питание ресторанного уровня, а здесь наоборот, голодом морить начали…"
            "Проигнорировав надменное фырканье Шизы, я перемешал содержимое тарелки в однородную массу и принялся за ужин."
            play music music_list["my_daily_life"] fadein 4
            "Не знаю, еда ли оказалась на вкус лучше, чем на вид, или причиной были физические упражнения на свежем воздухе, но я успел слопать ужин прежде, чем понял, что именно ем.{w} Впрочем, может оно и к лучшему. Главное - желудок наполнился и не слал больше в мозг ультиматумов."
            "Сыто поковырявшись в зубах, я лениво поднялся и не спеша пошёл к выходу."
        else:
            "Но потом решительно направился к ставшему уже привычному за последние два дня столику, за которым обычно кормилась Виола."
            dreamgirl "Ты в столовую уже как на работу в офис начинаешь ходить.{w} Вернее, как в штаб-квартиру какой-нибудь {i}конторы{/i} из дешёвого триллера."
            dreamgirl "Пришёл, доложился начальству, получил задание - поскакал выполнять."
            th "Знаешь, как учат те же триллеры, пока наше сотрудничество является взаимовыгодным - не стоит от него отказываться."
            "Я с облегчением поставил поднос - руки всё ещё здорово ныли после переноски очкарика - и уселся за стол."
            dreamgirl "Взаимовыгодным? Ну интерес медсестры, или кто там она на самом деле, мне вполне ясен - ты для неё бесплатный полевой агент и объект для троллинга в одном лице."
            dreamgirl "Твой-то интерес здесь какой? Что-то я не вполне улавливаю!"
            th "Информация.{w} Я надеюсь, что она либо сама ей будет делиться, либо проболтается о чём-нибудь случайно."
            th "Кстати, даже если отбросить невнятные объяснения про параллельные миры, её подсказка сфотографировать Юлю была очень даже ничего."
            "Пользуясь тем, что мой внутренний оппонент промедлил с ответом, я взялся наконец за вилку."
            th "В конце концов, я сегодня не только не пообедал толком, так ещё и полдник пропустил за всей этой беготнёй!{w} Непорядок, однако! Так и похудеть можно."
            dreamgirl "Можно подумать, последние несколько лет у тебя было исключительно пятиразовое питание ресторанного уровня, а здесь наоборот, голодом морить начали…"
            "Проигнорировав ехидное фырканье Шизы, я перемешал содержимое тарелки в однородную массу и принялся за ужин."
            play music music_list["my_daily_life"] fadein 4
            "Не знаю, еда ли оказалась на вкус лучше, чем на вид, или причиной были физические упражнения на свежем воздухе, но когда через пару минут напротив уселась Виолетта, моя порция успела уменьшиться как минимум наполовину."
            show cs normal2 close with dissolve
            "Сделав, подобно Портосу, над собой героическое усилие, я разом проглотил всё, что было у меня во рту."
            me "Приятного аппетита, Виолетта Церновна!"
            show cs smile2 close with dspr
            cs "Спасибо, пионер, спасибо…{w} Ну а у тебя, как я вижу, с аппетитом и так всё в порядке."
            "Слегка смутившись, я наконец взял себя в руки и начал всё-таки пережёвывать пищу перед тем как проглотить. Надеюсь, общество оценит мою помощь!"
            show cs normal2 close with dspr
            cs "Итак?"
            "Коротко бросила Виола, принимаясь за ужин. Мысленно вздохнув, я положил вилку и начал вполголоса:"
            me "Да не о чем особенно рассказывать-то. Не успели дойти до старого корпуса, как на нас откуда-то из кустов выскочил этот… Гм… Шурик."
            me "Весь грязный и ободранный, вопил что-то дикое, размахивал железками."
            me "В общем, мы с Сыроежкиным его скрутили, а потом Ольга… э-э… довольно быстро привела Шурика в чувство."
            me "Но она же Вам рассказывала, наверное?"
            show cs smile2 close with dspr
            "Виолетта неопределённо повела бровями. Я предпочёл считать это за знак согласия и продолжил:"
            me "Ну а потом мы его сразу в медпункт потащили, сам идти он не мог."
            "Я немного подумал и закончил решительно."
            show cs normal2 close with dspr
            me "Юля нам на глаза не попадалась, никаких признаков её присутствия замечено не было. Шурик о контактах с ней тоже не упоминал."
            me "Потерь среди личного состава нет, у меня всё."
            "Здесь я позволил себе улыбнуться, но Виолетта моего веселья не разделила."
            show cs normal close with dissolve
            cs "Зря смеёшься, Семён. Очень хорошо, что потерь нет."
            "Видимо, на моём лице что-то такое отразилось, потому что она поспешила добавить:"
            show cs smile2 close with dissolve
            cs "Ой, да не волнуйся ты так. Если бы у меня действительно были опасения насчёт вашей безопасности, никуда бы вы не пошли."
            cs "Во всяком случае, я была вполне уверена, что {i}в твоём присутствии{/i} всё пройдёт нормально, даже если вы столкнётесь с визуализацией. Пардон, с Юлей."
            show cs normal2 close with dissolve
            cs "Кстати, ты сказал, что Шурик кричал что-то. Не помнишь, что именно? Может быть, потом рассказывал что-нибудь?"
            me "Ну… Кажется, сначала он кричал про голоса в голове и что мы не люди. Я как-то больше беспокоился, чтобы он кого-нибудь не укокошил ненароком."
            me "А потом, когда очнулся, он вообще не помнил как туда попал."
            "Медсестра вздохнула:"
            cs "Жаль. Есть у меня некоторые подозрения насчёт того, что с ним могло произойти, но пока что ничем не обоснованные, к сожалению.{w} Что же, делать нечего, подождём до завтра - может и вспомнит что-нибудь."
            "Виола задумалась, покачивая машинально рукой, как и вчера, стакан из-под компота. Тут я обнаружил, что она между делом уже успела как-то незаметно для меня съесть свой ужин и заторопился, навёрстывая упущенное."
            th "А Виола сейчас не слишком-то похожа на какую-то там медсестру из провинциального пионерлагеря - слишком уж интеллектуальное у неё выражение лица."
            dreamgirl "Ага, и про шуточки свои совсем позабыла. Даже неинтересно!"
            "Виолетта тем временем завершила свои размышления и снова обратила на меня внимание:"
            cs "Ладно, шёл бы ты, Семён, отдыхать. Завтра об остальном поговорим."
            "И, словно подслушав Шизу, продолжила с ехидной ухмылкой:"
            show cs smile2 close with dspr
            cs "И знаешь, заходи-ка ты завтра с утра ко мне в медпункт."
            "Голос её словно стал ниже на целую октаву, а от появившейся в нём бархатистой хрипотцы у меня мурашки побежали по спине."
            "Она заговощически наклонилась ко мне и продолжила жарким шёпотом:"
            show cs badgirl2 close with dissolve:
                xalign 0.5 yalign 0.5 zoom 1.0
                linear 2.0 xalign 0.5 yalign 0.1 zoom 1.3
            stop music fadeout 6
            cs "В столовой толком не поговоришь - только и ждёшь, что подслушает кто-нибудь.{w} Да и реноме надо поддерживать."
            show cs badgirl2 close with dissolve:
                linear 1.5 xalign 0.5 yalign 0.5 zoom 1.0
            stop ambience fadeout 6
            "Отстранившись, она неспеша оправила воротник своего белого халата. Пальцы коснулись верхней пуговицы, на мгновенье задержавшись…"
            show cs smile2 with dissolve2
            "Поднявшись из-за стола, она томно подмигнула мне, взяла свой поднос и, слегка качнув бёдрами, уплыла к окошку посудомойки."
            hide cs with dissolve
            dreamgirl "Да-а… Был неправ. Вспылил. Но теперь считаю свою критику безобразной ошибкой.{w} Кстати, слюни подбери."
            "Я вернулся к реальности. Кажется, половина столовой перестала есть, замерев на месте."
            play ambience ambience_dining_hall_full fadein 4
            play music music_list["my_daily_life"] fadein 4
            "Впрочем, самое большее через минуту всё вернулось на круги своя - ужинающие занялись едой и обычными разговорами, а я наконец смог подняться из-за стола, не шокируя окружающих внешним видом."
            th "И ей это с рук сходит? Похоже, она действительно не последний человек в лагере."
            show dv scared pioneer far at right with dissolve
            "Тут я встретился глазами с Алисой. Та смотрела на меня с неприкрытым ужасом."
            th "Да в самом деле, какого чёрта!"
            dreamgirl "А вот теперь самое время позаботиться и о собственном реноме. Плечи расправить, грудь колесом, на лицо ухмылку прожжёного донжуана, и не спеша иди мимо…"
            "…Вздохнув, я смущённо пожал плечами и, ссутулившись, поплёлся относить грязную посуду."
            hide dv with dissolve

label alt_day5_uvao_evening_dining_hall_exit:
    stop ambience fadeout 3
    window hide
    $ renpy.pause(1)
    scene bg ext_dining_hall_near_sunset with fade
    play ambience ambience_camp_center_evening fadein 3
    play sound sfx_close_door_1
    play sound_loop ambience_medium_crowd_outdoors fadein 2
    "До отбоя оставалась ещё уйма времени, следовало решить, куда идти дальше."
#
# Безнадёжная карта 
#
    $ disable_all_zones()
    $ disable_all_chibi()
    $ set_zone('music_club', 'alt_day5_uvao_evening_map_music_club')
    $ set_zone('clubs', 'alt_day5_uvao_evening_dv_un_transit')
    $ set_zone('camp_entrance', 'alt_day5_uvao_evening_dv_un_transit')
    $ set_zone('sport_area', 'alt_day5_uvao_evening_map_sport_area')
    $ set_zone('library', 'alt_day5_uvao_evening_map_strange')
    $ set_zone('medic_house', 'alt_day5_uvao_evening_medic_house')
    $ set_zone('square', 'alt_day5_uvao_evening_dv_un')
    $ set_zone('beach', 'alt_day5_uvao_ev_beach_event')
    $ set_zone('boat_station', 'alt_day5_uvao_evening_boat_station')
    $ set_zone('me_mt_house', 'alt_day5_uvao_evening_go_house')
label alt_day5_uvao_evening_map_show:
    play sound sfx_paper_bag
    $ show_map()
    window show

label alt_day5_uvao_evening_map_music_club:
    scene bg ext_dining_hall_away_sunset with fade
    th "Зачем я туда пойду? Вряд ли меня там кто-то ждёт."
    $ disable_current_zone()
    jump alt_day5_uvao_evening_map_show
    
label alt_day5_uvao_evening_medic_house:
    scene bg ext_dining_hall_away_sunset with fade
    th "Э-э, нет! Там мне сейчас делать нечего. С Виолой я на сегодня пообщался вполне достаточно."
    $ disable_current_zone()
    jump alt_day5_uvao_evening_map_show
    
label alt_day5_uvao_evening_map_sport_area:
    if alt_uvao_D5_sh_mines:
        scene bg ext_dining_hall_away_sunset with fade
        dreamgirl "Да ты что, с ума сошёл?! Вдруг Саныч уже в курсе сегодняшних разборок в столовой?{w} Чего доброго, уронит на нас штангу нечаянно…"
        $ disable_current_zone()
        jump alt_day5_uvao_evening_map_show
    else:
        jump alt_day5_uvao_evening_map_strange    

# Идём на пристань, но сваливаем на пляж.
label alt_day5_uvao_evening_boat_station:
    hide window
    scene bg ext_boathouse_sunset with fade
    play ambience ambience_boat_station_day fadein 1
    window show
    "Выйдя на пристань, я уже собирался разуться и пристроиться где-нибудь с краю, свесив ноги в воду, но тут в голову пришла мысль получше."
    stop music fadeout 3
    th "А стоит ли после такого напряжённого дня ограничиваться только болтанием ногами в воде? Я ведь за всё это время так и не искупался ни разу - вот и совмещу приятное с полезным."
    jump alt_day5_uvao_ev_beach_event_direct

# Идём мимо площади и нарываемся
label alt_day5_uvao_evening_dv_un_transit:
    scene bg ext_dining_hall_away_sunset with fade
    th "Идея странная, но почему бы и нет? Тоже способ убить время…"
    "По пути я решил зайти на площадь, проведать Генду - мало ли, вдруг его украли, пока я ужинал?"
    jump alt_day5_uvao_evening_dv_un_square

# Д5-вечер - наблюдаем на площади ДваЧе и Унылку
#
# Используется флаг alt_uvao_true
# Используется флаг alt_uvao_D5_sh_mines (Видели Шурика в шахтах/не обедали/спалились на стоянке)
# Используется флаг обеда в Д4 с Леной alt_uvao_D4_lunch_un
# Устанавливает флаг встречи с ДваЧе и Унылкой alt_uvao_D5_evening_dv_un
label alt_day5_uvao_evening_dv_un:
    scene bg ext_dining_hall_away_sunset with fade
    th "Не думаю, что на площади за это время появилось что-то интересное, но почему бы и нет? Убить время можно и там."
    th "Посижу после еды на скамеечке, подумаю. Вряд ли кроме меня там кто-то будет."
    dreamgirl "А ты не боишься, что если ты начнёшь считать ворон в самом центре лагеря, то тебя мигом привлекут к каким-нибудь общественным работам?"
    if alt_uvao_D5_sh_mines:
        th "Пусть только попробуют! К тому же, мне ведь ясно пообещали «более комфортные» условия пребывания - вот и проверим, чего стоят эти слова."
    else:
        th "Пусть только попробуют! На сегодня я свой общественный долг уже исполнил с лихвой - до сих пор не могу рук поднять."
        if alt_uvao_true:
            th "И вообще, я при исполнении! Мысленный эксперимент буду ставить!"
label alt_day5_uvao_evening_dv_un_square:
    window hide
    scene bg ext_square_sunset with dissolve
    $ renpy.pause(1)
    window show
    stop music fadeout 3
    "До площади оставалась пара шагов, когда я обнаружил, что ближайшая лавочка уже занята - на ней сидели голова к голове две девочки, занятые негромким разговором."
    "В лучах закатного солнца рыжие волосы одной из них пылали красной медью, а смешные хвостики на голове второй казались сейчас сиреневыми."
    play music music_list["trapped_in_dreams"] fadein 5
    show un normal pioneer far at cright
    show dv normal pioneer far at fright
    with dissolve
    th "Вот уж не ожидал увидеть Алису и Лену вместе! Что у них может быть общего?"
    dreamgirl "А вот интересно, Лена волосы красит в фиолетовый цвет, или они у неё такие от природы?"
    th "Фиолетовых волос не бывает…"
    dreamgirl "Это в {i}том{/i} мире не бывает фиолетовых волос, точно так же, как и девочек с кошачьими ушами и хвостом, кстати говоря. Только вот ты-то не там, а {i}здесь{/i}."
    "Занятые разговором девочки пока что не замечали моего появления, но продолжаться бесконечно это не могло."
    th "В «том» я мире, или не в «том», но у меня такое ощущение, что лучше бы мне уйти подобру-поздорову, пока они меня не увидели. Решат ещё, что подслушиваю."
    if alt_uvao_true:
        th "Тем более, Лена достаточно странно себя вела сегодня. Не вышло бы сейчас какого-нибудь продолжения."
    elif alt_uvao_D4_lunch_un:
        th "Тем более, Лена вчера достаточно странно себя вела. Не вышло бы сейчас какого-нибудь продолжения."
    "Я сделал было осторожный шаг назад, но тут Лена подняла голову и заметила меня."
    stop music fadeout 3
    show un shocked pioneer far with dissolve
    "Пару секунд она смотрела на меня широко раскрытыми глазами, словно кролик на удава."
    show dv angry pioneer far with dissolve
    extend " А потом вскочила на ноги, как будто её за ниточки дёрнули."
    un "Я… {w=0.8}Я… {w=0.8}Ядолжнаидтиуменядела!"
    "Всхлипнув что-то нечленораздельное, она опрометью бросилась бежать."
    hide un with easeoutright
    play music music_list["pile"] fadein 3
    show dv rage pioneer far with dissolve
    "Алиса резко поднялась на ноги. Я не успел и глазом моргнуть, как она уже подступала ко мне, сжав кулаки."
    show dv rage pioneer at cright with dissolve
    dv "Ты… Ты… Ты подслушивал, гад!"
    "Ошеломлённый, я невольно попятился назад от разгневанной рыжей."
    me "Алиса, ты что? Ничего я не подслушивал, я вообще только что подошёл!"
    show dv rage pioneer close at center with dissolve
    "Пятясь, я выставил перед собой руки, не то в примиряющем жесте, не то готовясь прикрывать лицо от ударов продолжающей наступать на меня Двачевской."
    me "Я вас не сразу заметил, а как увидел - сразу хотел уйти, чтобы не мешать! Честное пионерское…"
    show dv angry pioneer close at center with dissolve
    "Алиса остановилась, по-прежнему глядя на меня исподлобья и тяжело дыша. Я поспешно добавил:"
    me "И вообще, даром не нужны мне ваши секреты, охота была подслушивать!" # однозначно "охота былА"
    "Её реакция оказалась совершенно неожиданной."
    show dv sad pioneer close at center with dissolve
    dv "Дурак!"
    "Крикнула внезапно тонким голосом Алиса."
    stop music fadeout 3
    hide dv with dissolve2
    "Не говоря больше ни слова, она повернулась и зашагала прочь, печатая каждый шаг так, словно мечтала пригвоздить меня подошвой к бетонным плитам площади."
    stop music fadeout 3
    "Открыв рот, я какое-то время смотрел ей вслед."
    th "Да что они здесь, с ума все посходили, что ли? Не пионерский лагерь, а какой-то дурдом на выезде! Ничего не понимаю."
    dreamgirl "Только меня ни о чём не спрашивай. Я беру отгул за свой счёт, извини."
    "Я перевёл дыхание и вытер ладонью вспотевший лоб. Кажется, лучшее, что мне оставалось сейчас сделать - это отправиться немедленно к своему домику и постараться больше не влипнуть сегодня ни в какую историю."
    $ alt_uvao_D5_evening_dv_un = True
    jump alt_day5_uvao_evening_headlong

#
# Д5-вечер - разговариваем на пляже со Славей
#
# Используется флаг alt_uvao_true
# Устанавливает флаг встречи со Славей alt_uvao_D5_evening_sl
label alt_day5_uvao_ev_beach_event:
    scene bg ext_dining_hall_away_sunset with fade
    "После такого напряжённого дня мысль искупаться казалась весьма здравой."
label alt_day5_uvao_ev_beach_event_direct:
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    stop sound_loop fadeout 3
    scene bg ext_beach_sunset with dissolve
    "Я не спеша дошёл до вотчины Ольги Дмитриевны - хвала рандому, сейчас её тут не было, и некому было устроить мне внеочередную выволочку за… да за что угодно!"
    stop music fadeout 3
    "Я нашёл подходящий пятачок земли и уже начал развязывать осточертевший галстук, как вдруг заметил кого-то в дальнем углу пляжа.{w} Знакомый силуэт… Славя?"
    play music music_list["trapped_in_dreams"] fadein 5
    "Она сидела, уткнувшись лицом в колени, косы безвольно стелились по земле."
    show sl sad pioneer far at fleft with dissolve
    "Такой Славю я ещё не видел. Она что, плачет?"
    dreamgirl "И ты, как рыцарь на белом коне, должен немедленно утешить печальную деву. Преисполненная благодарности, она падёт тебе на широкую грудь и доверит все свои тайны."
    th "А почему бы и нет? Не проходить же мимо. Я от неё ничего, кроме хорошего, не видел. Может, смогу помочь чем-то?"
    show sl sad pioneer far at cleft with dissolve
    "Чувствуя себя донельзя неловко, я направился к ней, старательно шурша ногами по песку - чтобы она не подумала, будто я подкрадываюсь."
    show sl sad pioneer at center with dissolve
    me "Эм… Славя?"
    sl "Семён. Что тебе?"
    "Она так и не подняла голову, голос её звучал глухо и неприветливо."
    me "Да в общем-то ничего… Я просто искупаться собирался…"
    sl "Одному нельзя. Не положено. Надо, чтоб кто-то из вожатых присматривал."
    me "Вот как… Жаль…"
    show sl sad pioneer close with dissolve
    "Я сел рядом. Не зная чем занять руки, я сорвал какую-то травинку и стал обрывать с неё семена. Славя так и сидела, не поднимая лица и не шевелясь."
    dreamgirl "Кажется, с утешением и падением на грудь возникли проблемы. Может быть, она сценарий не читала?"
    me "Славя… У тебя всё в порядке?"
    sl "Нет. Но это неважно."
    "Что-то мне подсказывает, что я тут нежеланный гость."
    me "Ты извини, что я навязываюсь, но… Может, я смогу помочь?"
    sl "Вряд ли…"
    "Да, так и есть. Наверное, сейчас стоит встать и уйти… Да, вот сейчас… Сейчас встану, извинюсь и пойду… "
    sl "Помнишь день, когда ты приехал, Семён?"
    "Славя произнесла это, по-прежнему не поднимая головы."
    sl "Ты ещё спрашивал, какой сейчас год."
    me "Да, а ты так и не ответила."
    sl "Тысяча девятьсот восемьдесят девятый. Сегодня двадцать второе июля."
    me "Спасибо, но… Почему ты вдруг это вспомнила?"
    "Славя подняла наконец голову и провела ладонями по лицу, стирая слезы… "
    th "Действительно плакала!"
    show sl normal pioneer close at center with dissolve
    "…потом повернулась ко мне, нацепив своё обычное оптимистичное выражение лица."
    sl "Да так, ерунда.{w} Странное место этот лагерь, правда ведь?"
    th "Да не то слово!"
    me "Странное? О чем ты?"
    sl "Здесь кажется, будто возможно всё.{w} Будто бы тут можно исполнить любую мечту - или, по крайней мере, сделать первый шаг к ней…{w} Так ведь?"
    "Вопрос застал меня врасплох. Впрочем, с момента появления здесь я и впрямь чувствовал себя так, будто получил второй шанс."
    me "Ну… наверное. Да."
    sl "А чего бы ты хотел? Какую мечту исполнить?"
    th "Разобраться во всех этих тайнах, это уж точно. Докопаться до сути происходящего!"
    me "Так сразу сложно сказать…{w} Не знаю. Стать примерным пионером?"
    show sl smile pioneer close at center with dissolve
    "Она фыркнула."
    sl "Это у Ольги Дмитриевны такая мечта - чтобы все стали примерными пионерами. {w} Ну не хочешь говорить - как хочешь."
    show sl normal pioneer close at center with dissolve
    sl "Просто интересно стало…{w} зачем ты сюда приехал."
    th "Как будто меня кто-то спросил…"
    me "Не знаю… Чем занимаются в лагере?{w} Приезжают, отдыхают, заводят друзей, набираются впечатлений."
    sl "И уезжают."
    me "Ну… да. А как иначе?"
    sl "Значит, еще два дня - и всё закончится?"
    me "Два дня?! Почему?"
    sl "Так смена же заканчивается послезавтра. Завтра вечером итоговый концерт и прощальная дискотека. Ты что, забыл?"
    th "Да я и не знал никогда… Два дня! А я так ничего и не узнал!"
    me "Да, как-то вылетело из головы. Очень уж быстро смена закончилась…"
    sl "А ты еще позже всех приехал. Ничего не успел, наверное?"
    "Разговор уходил куда-то в непонятные дебри. Славя, похоже, преследовала своими туманными расспросами какую-то цель, но вот какую?"
    me "Ну почему… Кое-что успел."
    "Впрочем, выкладывать ей всё начистоту я не собирался."    
    if alt_uvao_true:
        extend " Достаточно того, что Виола в курсе, что я «не от мира сего»."
    me "Ладно, пойду я, пожалуй. Доброй ночи!"
    show sl sad pioneer at center with dissolve
    sl "Доброй…"
    show sl sad pioneer far at cleft with dissolve
    "Уходя с пляжа, я оглянулся. Славя так и сидела неподвижно у кромки воды."
    th "Странная она сегодня какая-то…"
    stop ambience fadeout 3
    "Не-е-ет, всё, хватит на сегодня тайн. Пора в кроватку, утро вечера мудренее."
    $ alt_uvao_D5_evening_sl = True
    play ambience ambience_camp_center_evening fadein 3
    jump alt_day5_uvao_evening_headlong  
  
label alt_day5_uvao_evening_map_strange:
    scene bg ext_dining_hall_away_sunset with fade
    "Мягко говоря, идея странная, но почему бы и нет? Тоже способ убить время…"
    jump alt_day5_uvao_evening_miss
    
# Ошибаемся и внезапно приходим к домику. Обалдеваем и остаёмся там.
label alt_day5_uvao_evening_miss:
    "Я побрёл нога за ногу к намеченной цели, наслаждаясь тем, что вместе с уходящим солнцем дневная жара понемногу начала сдавать свои позиции."
    window hide
    scene black with dissolve2
    $ renpy.pause(2)
    window show
    "Впрочем, ноги оказались куда практичнее головы…"
    window hide
    scene bg ext_house_of_mt_sunset with dissolve2
    window show
    stop music fadeout 3
    "…Так что через несколько минут я с некоторым удивлением обнаружил себя прямо перед приютившим меня треугольным домиком."
    th "Минуточку!{w} Разве я сюда шёл?"
    dreamgirl "Можно подумать, ты всё ещё помнишь, куда собирался идти.{w} Неужели за день не набегался? Если уж тело столь бесцеремонно берёт инициативу в свои руки - самое время к нему прислушаться."
    "Немного подумав, я решительно махнул на всё рукой."
    th "В конце концов, здесь ничем не хуже, чем в любом другом месте.{w} Спокойно, никто не шляется туда-сюда."
    jump alt_day5_uvao_evening_headlong_already_here

label alt_day5_uvao_evening_go_house:
    scene bg ext_dining_hall_away_sunset with fade
    th "Действительно, что тут думать? Как будто я за день недостаточно набегался.{w} Пойду-ка я домой, посижу там спокойно до отбоя."

label alt_day5_uvao_evening_headlong:
    window hide
    $ renpy.pause(1)
    scene bg ext_house_of_mt_sunset with dissolve2
    window show
label alt_day5_uvao_evening_headlong_already_here:
    stop sound_loop fadeout 7
    stop music fadeout 5
    "Впрочем, особого желания сидеть в прогревшемся за день домике у меня не было."
    if (alt_uvao_D5_sh_mines):
        "Вместо этого я с удовольствием развалился на стоящем у входа шезлонге и уставился в наливающееся закатным румянцем небо."
    else:
        "Вместо этого я с удовольствием развалился на стоящем у входа шезлонге и, с лёгким смущением вспомнив послеобеденную сцену на площади, уставился в наливающееся закатным румянцем небо."
    "Ласковый ветерок чуть шевелил над головой ветви уже отцветающей, но всё ещё душистой сирени."
    th "Странно, лето уже явно за середину перевалило, а сирень всё ещё цветёт."
    "Лениво подумал я, бездумно пытаясь дотянуться рукой до ближайшей ветки, не отрывая при этом спины от шезлонга."
    dreamgirl "Тебе-то какая разница? Тоже мне, ботаник-любитель выискался."
    "Разницы особой и в самом деле не было, так что я просто добавил ещё один пункт к длинному списку местных странностей."
    "Впечатлений за день и так набралось предостаточно."
    "Утренний поход к старому корпусу и попытка открыть дверь в конце туннеля…{w} Я осторожно ощупал колено и удовлетворённо отметил, что дела явно идут на поправку."
    if alt_uvao_D5_hentai:
        "Короткий отдых в бункере и последовавшая за ним сцена, от воспоминания о которой я мечтательно улыбнулся."
    elif alt_uvao_true:
        "Короткий отдых в бункере и ошеломившая меня фотография на экране смартфона."
    else:
        "Короткий отдых в бункере…"
    if alt_uvao_D5_sh_mines:
        "Блуждание вслепую по тёмным шахтам и беготня за спятившим Шуриком…"
    else:
        "Чудом не приключившаяся со мной трепанация черепа… И странное поведение Лены, кстати говоря.{w} Ещё и сама на поиски напросилась, оказывается."
    if alt_uvao_D5_evening_dv_un:
        "Вообще, сцена на площади сегодня вечером тоже не давала мне покоя. Почему девочки так переполошились? Про меня говорили, что ли?"
    elif alt_uvao_D5_evening_sl:
        "Вообще, странный разговор со Славей тоже не давал мне покоя.{w} {i}Очень{/i} странный разговор, если уж на то пошло."
    "За всеми этими мыслями я и не заметил, как роскошный закатный пожар в небе подёрнулся пеплом, и вечер по-южному быстро уступил место ночи."
    stop ambience fadeout 2
    window hide
    scene bg ext_house_of_mt_night with dissolve2
    play ambience ambience_camp_center_night fadein 3
    $ persistent.sprite_time = "night"
    $ night_time()
    window show
    "Глаза уже безнадёжно слипались, когда рядом хрустнул гравий."
    show mt angry pioneer at cleft with dissolve 
    play music music_list["always_ready"] fadein 3
    "Я приподнял голову. Надо мной с сердитым видом стояла вожатая."
    mt "Скажите пожалуйста! Все люди как люди, один ты опять бездельничаешь и отрываешься от коллектива!"
    "Я аж задохнулся от возмущения:"
    if alt_uvao_D5_sh_mines:
        th "Ну да, конечно. Сама-то переработала прошлой ночью на пользу обществу, вот теперь на меня и срываешься."
        "Глядя на меня исподлобья она сварливо осведомилась:"
        mt "Ты почему в дом-то не идёшь спать? Стесняешься после сегодняшнего, что ли?"
        mt "Кстати, насчёт сегодняшнего - не думай, что ты победил. В моих силах сделать твоё пребывание в этом лагере не то, что более комфортным, но и вообще невыносимым.{w} Ты меня хорошо понимаешь, Семён?"
        "В её голосе не прозвучало явной угрозы, но сомневаться в серьёзности сказанных слов не приходилось. Я постарался ответить как можно спокойнее:"
        me "Я понимаю, Ольга Дмитриевна."
        show mt normal pioneer with dissolve
        mt "Вот и славно. Взаимопонимание - прекрасная вещь."
        hide mt with dissolve
        "С этими словами она повернулась и скрылась в домике."
        dreamgirl "Кажется, сегодняшние полежайки с Санычем отменяются."
        th "Кажется, да. Впрочем, ну её, пусть себе злится на здоровье. Мне с ней детей не крестить."
        "Я огляделся и почесал в затылке."
    else:
        th "Ну да, конечно! Особенно после обеда я сегодня бездельничал, аж чуть не помер от скуки!"
        "Кажется, эмоции ясно отразились на моём лице, потому что она вдруг усмехнулась:"
        show mt smile pioneer with dissolve
        mt "Ладно, горе ты моё луковое, ты почему в дом-то не идёшь спать? Под звёздами приятнее, что ли?"
        hide mt with dissolve
        "Пока я растерянно хлопал глазами, вожатая скрылась внутри домика."
    th "В самом деле, с чего это я здесь разлёгся? В постели-то оно и вправду спать приятнее."
    "Не успел я выбраться из шезлонга, как Ольга уже выбежала наружу."
    show mt normal pioneer far at cleft with dissolve
    if alt_uvao_D5_sh_mines:
        th "Ну надо же, какие мы деловые! Даже не оборачивается!"
    else:
        mt "Спокойной ночи!"
        "Бросила она, деловито проносясь мимо меня."
    dreamgirl "Ну да, очередной сеанс самоотверженной работы на благо общества."
    if alt_uvao_D5_sh_mines:
        extend " По крайней мере, хоть перегаром она завтра на нас дышать точно не будет."
    else:
        extend " Надеюсь, хоть перегаром она завтра на нас дышать не будет."
    show mt normal pioneer far at left with ease
    me "Ольга Дмитриевна, а Вы куда на ночь-то глядя?"
    show mt rage pioneer far at left with dspr
    "Увидев лицо вожатой, я поспешил добавить:"
    me "Ой, извините. Я имею в виду, случилось что-то?"
    show mt angry pioneer far with dspr
    "Она смерила меня крайне недовольным взглядом, но всё-таки соблаговолила ответить:"
    mt "Да ничего не случилось. Хочу в медпункт зайти, проведать Шурика."
    if alt_uvao_true:
        "Она поколебалась немного и добавила:"
        show mt normal pioneer far with dspr
        mt "Да и с Виолеттой обсужу кое-что по нашей тематике."
    if alt_uvao_D5_sh_mines:
        "Ольга недовольно дернула плечами и заторопилась было дальше, но, сделав несколько шагов, внезапно притормозила и снова обернулась."
    else:
        "Кивнув на прощание, Ольга заторопилась было дальше, но, сделав несколько шагов, внезапно притормозила и снова обернулась."
    show mt normal pioneer far at fleft with ease
    mt "А кстати, Семён, ты в библиотеку записывался?"
    me "Нет…"
    "Растерянно ответил я, совершенно сбитый с толку таким неожиданным поворотом."
    mt "Вот и я знаю, что нет. Поэтому учебник, который у тебя на тумбочке лежал, я сегодня отнесла обратно в библиотеку."
    show mt smile pioneer far with dspr
    mt "Нет у тебя читательского билета - нет и книжек!"
    if alt_uvao_D5_sh_mines:
        "Тут она ехидно усмехнулась и скрылась в темноте."
    else:
        show mt laugh pioneer far with dspr
        "Тут она самым легкомысленным образом показала мне язык и скрылась в темноте."
    hide mt with easeoutleft
    stop music fadeout 5
    "Ни злиться, ни расстраиваться сил уже совершенно не было, поэтому я махнул на всё рукой и пошёл спать."
    window hide
    play sound sfx_open_dooor_campus_1
    stop ambience fadeout 1
    scene bg int_house_of_mt_noitem_night with dissolve
    play ambience ambience_int_cabin_night fadein 1
    play sound sfx_close_door_campus_1
    window show
    "Кажется, раздеваться я начал уже на ходу, но не могу утверждать этого наверняка.{w} Единственное, что чётко отложилось у меня в памяти - это прикосновение прохладной подушки к щеке."

    window hide
    stop ambience fadeout 5
    stop music fadeout 5
    scene bg black with fade2
    $ renpy.pause (2)
return
#D5 End!
    